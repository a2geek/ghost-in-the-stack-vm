100 DIM Z(52): POKE -16298,0:TEXT :CALL -936:VTAB 4:TAB 9:PRINT "APPLE CASINO BLACKJACK":PRINT "   COPYRIGHT 1978 APPLE COMPUTER INC.":PRINT 
101 PRINT :PRINT "USE THE FOLLOWING COMMANDS":PRINT 
110 PRINT "H=HIT":PRINT "S=STAY":PRINT "D=DOUBLE":PRINT "X=SPLIT(ONE ALLOWED)":PRINT :PRINT "PLEASE INPUT YOUR TOTAL STAKE ";:INPUT S:GR 
200 VTAB 23:PRINT "INPUT YOUR BET          STAKE IS ";S
201 FOR L=1 TO 25:X= PEEK (-16336):NEXT L:VTAB 23:TAB 15:INPUT B
202 GOSUB 5000:COLOR= 4:FOR L=1 TO 38:HLIN 0,39 AT L:NEXT L
203 COLOR= 8:HLIN 0,39 AT 0:HLIN 0,39 AT 39:FOR L=1 TO 52:Z(L)=0:NEXT L
204 D=0:P1=0:P2=0:DA=0:PA1=0:PA2=0:PL=0
210 REM  DEALERS TWO CARDS
211 X=2:Y=2:GOSUB 2000:D=D+T:DA=DA+W:F=W:GOSUB 3000:X=8:GOSUB 2000:DC=C:D=D+T:DA=DA+W:X=2:Y=11
300 GOSUB 2000:IF X=2 THEN E=C:P1=P1+T:PA1=PA1+W:GOSUB 3000:X=X+6:IF X=8 THEN GOTO 300
320 IF F#1 THEN GOTO 340:VTAB 22:PRINT "DO YOU WANT INSURANCE? Y=YES N=NO":INPUT A$
321 GOSUB 5000
322 IF A$#"Y" THEN GOTO 340:IF D#21 THEN GOTO 330:S=S+B:G=4:GOSUB 4000:GOTO 350
330 S=S-B/2:VTAB 22:PRINT "YOU LOST INSURANCE BET"
331 FOR L=1 TO 1000:NEXT L:GOSUB 5000
340 IF D#21 THEN GOTO 360:S=S-B:G=1:GOSUB 4000
350 X=8:Y=2:C=DC:GOSUB 3000:GOTO 200
360 IF P1#21 THEN GOTO 370:S=S+B+B/2:G=4:GOSUB 4000:FOR L=1 TO 100
361 COLOR= 13:IF L MOD 2=0 THEN COLOR= 1:IF L=100 THEN COLOR= 4
362 HLIN 14,18 AT 15:VLIN 16,20 AT 18:HLIN 15,17 AT 20:VLIN 21,23 AT 15:HLIN 16,18 AT 23
363 X= PEEK (-16336)- PEEK (-16336)
364 VLIN 15,24 AT 24:VLIN 15,24 AT 25:HLIN 14,17 AT 16:VLIN 17,19 AT 17:HLIN 14,16 AT 19:VLIN 20,24 AT 14:HLIN 14,18 AT 24
366 NEXT L:GOTO 200
368 REM  DOUBLE
369 VTAB 22:PRINT "YOU CAN NOT SPLIT":FOR L=1 TO 1000:NEXT L:GOSUB 5000
370 INPUT A$:GOSUB 5000:IF A$#"D" THEN 400:B=2*B:X=14:GOSUB 2000:P1=P1+T:PA1=PA1+W:GOSUB 3000
380 IF P1<22 THEN GOTO 1000:IF PA1>0 THEN GOTO 390:GOTO 508
390 PA1=PA1-1:P1=P1-10:GOTO 380
392 REM  SPLIT
400 IF A$="S" THEN GOTO 1000
410 IF A$#"X" THEN GOTO 500
411 IF C#E THEN GOTO 369:P1=P1/2:P2=P1:X=2:Y=19:GOSUB 2000:C=E:GOSUB 3000:X=8:Y=11
420 GOSUB 2000:P1=P1+T:PA1=PA1+W:GOSUB 3000
430 IF P1<22 THEN GOTO 450:IF PA1>0 THEN GOTO 440:G=2:GOSUB 4000:PL=1:S=S-B:P1=0:GOTO 455
440 PA1=PA1-1:P1=P1-10:GOTO 430
450 IF E=1 THEN GOTO 455:INPUT A$:GOSUB 5000:X=X+6:IF A$="H" THEN GOTO 420
455 X=8:Y=19
460 GOSUB 2000:P2=P2+T:PA2=PA2+W:GOSUB 3000
470 IF P2<22 THEN GOTO 490:IF PA2>0 THEN GOTO 480:IF P1#0 THEN GOTO 475:COLOR= 15:HLIN 14,17 AT 37:HLIN 14,17 AT 38:GOTO 476
471 GOTO 1000
475 G=2:GOSUB 4000
476 S=S-B:PL=PL+1:P2=0:IF PL=2 THEN GOTO 350:GOTO 1000
480 PA2=PA2-1:P2=P2-10:GOTO 470
490 IF E=1 THEN GOTO 1000:INPUT A$:GOSUB 5000:X=X+6:IF A$="H" THEN GOTO 460:GOTO 1000
492 REM  STANDARD CALL
500 GOSUB 2000:P1=P1+T:PA1=PA1+W:GOSUB 3000
505 IF P1<22 THEN GOTO 520:IF PA1>0 THEN GOTO 510
506 G=2:GOSUB 4000:S=S-B:GOTO 350
508 VTAB 21:S=S-B:G=2:GOSUB 4000:GOTO 350
510 PA1=PA1-1:P1=P1-10:GOTO 505
520 IF A$="D" THEN GOTO 1000:X=X+6:INPUT A$:GOSUB 5000:IF A$="H" THEN GOTO 500
521 IF A$="D" THEN B=2*B:IF A$="D" THEN GOTO 500
999 REM  DEALERS HAND
1000 C=DC:X=8:Y=2:GOSUB 3000:GOTO 1010
1005 IF D>16 THEN GOTO 1030:X=X+6:GOSUB 2000:D=D+T:DA=DA+W:GOSUB 3000
1010 IF D<22 THEN GOTO 1005:IF DA>0 THEN GOTO 1020:D=0:GOTO 1030
1020 D=D-10:DA=DA-1:GOTO 1010
1030 IF P1=0 THEN GOTO 1070:X1=1070:IF P1>D THEN GOTO 1050:IF P1=D THEN GOTO 1060
1040 S=S-B:G=1:GOSUB 4000:GOTO X1
1050 S=S+B:G=4:GOSUB 4000:GOTO X1
1060 G=3:GOSUB 4000:X1=202:FOR L=1 TO 1500:NEXT L:
1070 IF X1#202 THEN X1=200:IF P2=0 THEN GOTO X1:IF P2>D THEN GOTO 1050:IF P2#D THEN GOTO 1040:G=3:GOSUB 4000:FOR L=1 TO 1000:NEXT L:GOTO 202
1999 REM  CARD FACE DISPLAY
2000 COLOR= 6:FOR L=Y TO Y+6:HLIN X,X+4 AT L:NEXT L
2010 H= RND (52):C=H/4+1:IF Z(H+1)=1 THEN GOTO 2010:Z(H+1)=1:T=C:IF C>10 THEN T=10:IF C=1 THEN T=11:W=0:IF T=11 THEN W=1:RETURN 
3000 COLOR= 0:GOTO 3000+C*10
3010 PLOT X+2,Y+3:GOTO 3502
3020 PLOT X+1,Y+1:VLIN Y+1,Y+3 AT X+2:VLIN Y+3,Y+5 AT X+1:PLOT X+2,Y+5:RETURN 
3030 PLOT X+2,Y+3:HLIN X+1,X+2 AT Y+5:GOTO 3503
3040 VLIN Y+1,Y+3 AT X+1:PLOT X+2,Y+3:GOTO 3504
3050 HLIN X+1,X+3 AT Y+1:HLIN X+1,X+3 AT Y+3:HLIN X+1,X+3 AT Y+5:PLOT X+1,Y+2:PLOT X+3,Y+4:RETURN 
3060 VLIN Y+1,Y+5 AT X+1:VLIN Y+3,Y+5 AT X+3:PLOT X+2,Y+3:PLOT X+2,Y+5:RETURN 
3070 GOTO 3503
3080 PLOT X+2,Y+3:GOTO 3501
3090 VLIN Y+2,Y+3 AT X+1:PLOT X+2,Y+3:GOTO 3503
3100 VLIN Y+1,Y+5 AT X:VLIN Y+1,Y+5 AT X+2:VLIN Y+1,Y+5 AT X+4:PLOT X+3,Y+1:PLOT X+3,Y+5:RETURN 
3110 HLIN X+1,X+3 AT Y+1:VLIN Y+2,Y+5 AT X+2:PLOT X+1,Y+5:RETURN 
3120 HLIN X+3,X+4 AT Y+6:GOTO 3501
3130 VLIN Y+1,Y+5 AT X+1:PLOT X+2,Y+3:PLOT X+3,Y+2:PLOT X+3,Y+4:PLOT X+4,Y+1:PLOT X+4,Y+5:RETURN 
3501 PLOT X+2,Y+5
3502 VLIN Y+1,Y+5 AT X+1
3503 HLIN X+1,X+3 AT Y+1
3504 VLIN Y+1,Y+5 AT X+3
3505 RETURN 
4000 G=G*9-4:COLOR= 1:HLIN G,G+3 AT 37:HLIN G,G+3 AT 38:RETURN 
5000 CALL -936:VTAB 21:PRINT "     LOSE     BUST     PUSH     WIN":RETURN :END 
65535 REM *COPYRIGHT 1978 BY APPLE ;COMPUTER,INC.*-- CONTRIBUTED BY J.LEGG
