10 REM  PACHINCO GAME 1979
12 REM  SINETU  AKIHABARA YOKOHAMA
13 REM  MUDANTENSHIYA KINSHI (C)
14 AY= ASC("Y"):AN= ASC("N")
20 GOSUB 4000
30 GR :GOSUB 1000:CALL -936
40 PRINT :PRINT "SPECIAL FEATURES (Y/N) : ":VTAB 24:FOR I=1 TO 500:NEXT I:GOSUB 3000
50 TAB 1:PRINT "POSTS ?";:FOR I=1 TO 500:NEXT I
52 GOSUB 1100:GOSUB 3000:IF KEY=AY THEN 60:GOSUB 2100:IF KEY>127 THEN 60
54 GOSUB 3000:IF KEY<128 THEN 52:IF KEY=AY THEN GOSUB 1100
60 TAB 1:PRINT "SPINNER ?";:FOR I=1 TO 500:NEXT I
62 GOSUB 1200:GOSUB 3000:IF KEY=AY THEN 70:GOSUB 2200:IF KEY>127 THEN 70
64 GOSUB 3000:IF KEY<128 THEN 62:IF KEY=AY THEN GOSUB 1200
70 REM  BEGIN
90 B=1:C=0:BAL=5:BNS=0:SCR=0:CALL -936:VTAB 22:PRINT "SCORE:";:TAB 16:PRINT "BONUS:";
95 TAB 34:PRINT "BALL:":VTAB 24:TAB 13:PRINT "HIGH SCORE: ";HSC;"0";:GOSUB 600:FOR I=1 TO 1000:NEXT I
100 P0=PDL (0)/8+5:IF P0>30 THEN P0=30:IF P0<5 THEN P0=5
110 COLOR= 0:IF P0=5 THEN 115:HLIN 5,P0-1 AT 39
115 IF P0=30 THEN 117:HLIN P0+5,34 AT 39
117 COLOR= 2:HLIN P0,P0+4 AT 39
120 IF B THEN 900:LX=X:LY=Y:X=X+DX:Y=Y+DY:IF X<0 THEN X=0:IF Y<0 THEN Y=0:IF X>39 THEN X=39:IF Y>38 THEN 520
140 COLOR= C:PLOT LX,LY:C= SCRN(X,Y):COLOR= 15:PLOT X,Y:IF C<2 THEN 100:GOTO 200+C*20
240 GOTO 500
260 SCR=SCR+2:COLOR= 9:VLIN 9,10 AT 19:VLIN 9,10 AT 20:COLOR= 7:VLIN 6,13 AT X:IF  SCRN(20,30)=0 THEN 500
270 COLOR= 4:FOR I=28 TO 31:HLIN 18,21 AT I:NEXT I:GOTO 500
280 N= RND (16)+5:FOR I=1 TO N:COLOR= C:PLOT X,Y:X= RND (4)+18:Y= RND (4)+28:C=4
285 COLOR= 15:PLOT X,Y:SCR=SCR+1:GOSUB 600:NEXT I
290 DX= RND (2)*3+ RND (2)-2:DY=-2:COLOR= C:PLOT X,Y:X=X+DX:Y=Y+DY:COLOR= 15:C= SCRN(X,Y):PLOT X,Y:GOTO 100
300 COLOR= 5:VLIN 34,35 AT 1:VLIN 34,35 AT 2:COLOR= 15:PLOT X,Y
310 SCR=SCR+10:GOTO 500
320 SCR=SCR+2:COLOR= 8:VLIN 9,10 AT 19:VLIN 9,10 AT 20:GOTO 500
340 SCR=SCR+10:COLOR= 8:VLIN 9,10 AT 19:VLIN 9,10 AT 20:COLOR= 6:VLIN 6,13 AT X:C=6:IF  SCRN(20,30)=0 THEN 500
350 COLOR= 14:FOR I=28 TO 31:HLIN 18,21 AT I:NEXT I:GOTO 500
360 SCR=SCR+2:GOTO 500
380 SCR=SCR+BNS+10:IF BNS<100 THEN 390:BAL=BAL+1:IF BAL>9 THEN BAL=9:PRINT "<CTRL-G>";
390 BNS=0:COLOR= 8:VLIN 9,10 AT 19:VLIN 9,10 AT 20:C=8
392 COLOR= 11:HLIN 15,24 AT 16:HLIN 15,24 AT 17:IF  SCRN(1,34)=2 THEN 500
394 COLOR= 0:VLIN 34,35 AT 1:VLIN 34,35 AT 2:VLIN 34,35 AT 37:VLIN 34,35 AT 38
396 VLIN 38,39 AT 19:VLIN 38,39 AT 20:GOTO 500
400 COLOR= 10:VLIN 34,35 AT 37:VLIN 34,35 AT 38:COLOR= 15:PLOT X,Y
410 SCR=SCR+10:GOTO 500
420 COLOR= 1:Z=(X-1)/2*2+1:HLIN Z,Z+1 AT 16:HLIN Z,Z+1 AT 17:DY=-DY:C=1
430 BNS=BNS+20:SCR=SCR+10:GOSUB 600:GOTO 100
440 SCR=SCR+5:COLOR= 9:VLIN 9,10 AT 19:VLIN 9,10 AT 20:IF  SCRN(20,30)=0 THEN 500
450 COLOR= 4:FOR I=28 TO 31:HLIN 18,21 AT I:NEXT I:GOTO 500
460 DX= RND (2)*3+ RND (2)-2:IF  RND (2) THEN DY=-DY
470 SCR=SCR+1:GOSUB 600:GOTO 100
480 N= RND (16)+5:FOR I=1 TO N:COLOR= C:PLOT X,Y:X= RND (4)+18:Y= RND (4)+28:C=14
485 COLOR= 15:PLOT X,Y:SCR=SCR+5:GOSUB 600:NEXT I
490 DX= RND (2)*3+ RND (2)-2:DY=-2:COLOR= C:PLOT X,Y:X=X+DX:Y=Y+DY:COLOR= 15:C= SCRN(X,Y):PLOT X,Y:GOTO 100
500 SX=X-SGN (DX):SY=Y-SGN (DY):IF NOT  SCRN(SX,SY) THEN 510
505 COLOR= C:PLOT X,Y:X=LX+SGN (DX):Y=LY+SGN (DY):C= SCRN(X,Y):COLOR= 15:PLOT X,Y:SX=LX:SY=LY
510 CX= SCRN(X,SY):CY= SCRN(SX,Y):IF CX OR NOT CY THEN DX=-DX:IF CY OR NOT CX THEN DY=-DY:GOSUB 600:GOTO 100
520 Y=39:IF LX<4 OR LX>35 OR  SCRN(X,Y)=0 THEN 530:DY=-DY:COLOR= C:PLOT LX,LY:C= SCRN(X,Y)
522 COLOR= 15:PLOT X,Y:GOSUB 600
525 DX=X-P0-2:IF DX=0 THEN DX=(X>LX)*2-1:IF X<5 THEN DX=2:IF X>34 THEN DX=-2:GOTO 100
530 COLOR= C:PLOT LX,LY:C= SCRN(X,Y):COLOR= 15:PLOT X,Y
540 B=1:BAL=BAL-1:FOR I=1 TO 20:GOSUB 600:NEXT I:COLOR= C:PLOT X,Y:IF BAL>0 THEN 100:GOTO 950
600 Z= PEEK (-16336)+ PEEK (-16336)- PEEK (-16336)+ PEEK (-16336)- PEEK (-16336)
610 VTAB 22:TAB 8:PRINT SCR;"0";:TAB 23:PRINT BNS;"0  ";:TAB 40:PRINT BAL:RETURN 
900 IF  PEEK (-16286)<128 AND  PEEK (-16287)<128 THEN 100
910 DX= RND (2)*3+ RND (2)-2:DY=-2:X= RND (2)*11+ RND (9)+10:Y=39:COLOR= 6:VLIN 6,13 AT 0:VLIN 6,13 AT 39
915 COLOR= 8:VLIN 9,10 AT 19:VLIN 9,10 AT 20:IF  SCRN(20,30)=0 THEN 920:COLOR= 4:FOR I=28 TO 31:HLIN 18,21 AT I:NEXT I
920 B=0:BNS=0:COLOR= 11:HLIN 15,24 AT 16:HLIN 15,24 AT 17:GOSUB 600:IF  SCRN(1,34)=2 THEN 100
930 COLOR= 0:VLIN 34,35 AT 1:VLIN 34,35 AT 2:VLIN 34,35 AT 37:VLIN 34,35 AT 38
940 VLIN 38,39 AT 19:VLIN 38,39 AT 20:GOTO 100
950 COLOR= 0:HLIN 5,34 AT 39:VTAB 24:TAB 10:IF SCR>=2000 THEN 980:IF SCR>=200 THEN 970
960 PRINT "   AMATEUR - GRADE ";SCR/20;"  ";:GOTO 990
970 PRINT " APPRENTICE - GRADE ";SCR/200;:GOTO 990
980 PRINT "<<< PINBALL WIZARD >>>";
990 FOR I=1 TO 2000:NEXT I:IF SCR>HSC THEN HSC=SCR:TAB 1:CALL -958:PRINT "SAME SETUP ? ";
995 GOSUB 3000:IF KEY<128 THEN 995:IF KEY#AN THEN 90:GOTO 30
1000 REM  BASIC SETUP
1010 COLOR= 2:VLIN 0,39 AT 0:HLIN 1,38 AT 0:VLIN 4,8 AT 19:VLIN 4,8 AT 20:VLIN 0,39 AT 39
1015 VLIN 34,39 AT 3:VLIN 34,39 AT 4:VLIN 34,39 AT 35:VLIN 34,39 AT 36
1017 VLIN 34,35 AT 1:VLIN 34,35 AT 2:VLIN 34,35 AT 37:VLIN 34,35 AT 38
1020 VLIN 4,10 AT 15:VLIN 4,10 AT 16:VLIN 4,10 AT 23:VLIN 4,10 AT 24:HLIN 17,22 AT 4:HLIN 17,22 AT 5
1025 COLOR= 12:HLIN 7,12 AT 0:HLIN 27,32 AT 0
1030 COLOR= 7:VLIN 6,13 AT 0:VLIN 6,13 AT 39
1035 COLOR= 3:VLIN 22,29 AT 0:VLIN 22,29 AT 39
1040 COLOR= 9:VLIN 9,10 AT 19:VLIN 9,10 AT 20
1050 COLOR= 11:HLIN 15,24 AT 16:HLIN 15,24 AT 17
1060 COLOR= 13:HLIN 9,10 AT 9:HLIN 9,10 AT 10:HLIN 29,30 AT 9:HLIN 29,30 AT 10
1070 HLIN 9,10 AT 25:HLIN 9,10 AT 26:HLIN 29,30 AT 25:HLIN 29,30 AT 26
1090 RETURN 
1100 REM  POSTS
1120 COLOR= 5:VLIN 4,5 AT 17:VLIN 4,5 AT 18:VLIN 34,35 AT 1:VLIN 34,35 AT 2
1130 COLOR= 10:VLIN 4,5 AT 21:VLIN 4,5 AT 22:VLIN 34,35 AT 37:VLIN 34,35 AT 38
1190 RETURN 
1200 REM  SPINNER
1220 COLOR= 14:HLIN 18,21 AT 28:HLIN 18,21 AT 29:HLIN 18,21 AT 30:HLIN 18,21 AT 31
1290 RETURN 
2100 REM  NO POSTS
2120 COLOR= 2:VLIN 4,5 AT 17:VLIN 4,5 AT 18:VLIN 4,5 AT 21:VLIN 4,5 AT 22
2130 VLIN 34,35 AT 1:VLIN 34,35 AT 2:VLIN 34,35 AT 37:VLIN 34,35 AT 38
2140 COLOR= 0:VLIN 38,39 AT 19:VLIN 38,39 AT 20
2190 RETURN 
2200 REM  NO SPINNER
2220 COLOR= 0:HLIN 18,21 AT 28:HLIN 18,21 AT 29:HLIN 18,21 AT 30:HLIN 18,21 AT 31
2290 RETURN 
3000 REM  GET KEY VALUE
3010 I=0
3020 KEY= PEEK (-16384):IF  PEEK (-16287)>127 THEN KEY=AN:IF  PEEK (-16286)>127 THEN KEY=AY
3030 I=I+1:IF I<20 AND KEY<128 THEN 3020: POKE -16368,0:RETURN 
4000 REM  TITLE
4100 TEXT : POKE 50,255:CALL -936:VTAB 5:TAB 15
4120 PRINT "PACHINCO GAME"
4200 VTAB 14:TAB 5
4210 PRINT "SINETU DENKI AKIHABARA YOKOHAMA"
4220 VTAB 16:TAB 4
4230 PRINT "MUDANTENSHIYA KINSHI (C) 1979"
4235 VTAB 18:TAB 3
4236 PRINT 
4240 VTAB 22:TAB 11
4250 PRINT "INSTRUCTIONS (Y/N) ?"
4260 GOSUB 3000:IF KEY<128 THEN 4260:IF KEY=AN THEN 4990
4300 GOSUB 4900
4310 PRINT :PRINT "           PACHINCO GAME":PRINT 
4330 PRINT :PRINT "   ALL OF THE ACTION TAKES PLACE WITHIN A 40 BY 40 'MACHINE' BORDERED BY BUMPERS";
4340 PRINT "OF VARIOUS COLORS.  YOU WILL BE GIVEN 5 BALLS PER GAME.  PRESSING EITHER PADDLE"
4350 PRINT "RESET BUTTON LAUNCHES THE BALL, RESETS  ALL FLAGS & BONUS, AND STARTS THE GAME."
4360 PRINT :PRINT "   THE ANALOG PADDLE CONTROLS THE MOVING'FLIPPER' AT THE BOTTOM OF THE SCREEN."
4370 PRINT "SCORING IS DETERMINED BY COLOR.  BORDERS(BLUE) HAVE NO VALUE; OTHERS AS FOLLOWS:";
4380 PRINT :PRINT "CENTER BUMPERS (YELLOW) = 10 POINTS     EDGE BUMPERS = 10 TO 50, OR 100 WHEN LIT";
4390 PRINT "FLAGS (PINK) = 100 PTS. + 200 PTS. BONUSSPECIAL (RED) SCORES BONUS+100, WHEN LIT";
4400 PRINT :PRINT "   HIT ANY KEY TO CONTINUE...";:FOR I=1 TO 1000:NEXT I
4410 GOSUB 3000:IF KEY<128 THEN 4410:GOSUB 4900
4420 PRINT "   IN ADDITION TO SCORING, SOME FEATURESHAVE SPECIAL FUNCTIONS, AS FOLLOWS:"
4430 PRINT :PRINT "BUMPERS - DEFLECT THE BALL AT A RANDOM            DIRECTION AND VELOCITY."
4440 PRINT "FLAGS   - DROP AFTER BEING HIT, RESET IF          SPECIAL IS HIT (SCORES BONUS).";
4450 PRINT "SPECIAL - RESETS FLAGS & POSTS WHEN LIT,          PLUS EXTRA BALL IF BONUS=1000.";
4460 PRINT :PRINT "OPTIONAL SPECIAL FEATURES:"
4470 PRINT :PRINT "POSTS (GREY) - TABS NEAR TOP OF MACHINE SET SIDE POSTS TO BLOCK BALL EXITS.  IF"
4480 PRINT "EITHER POST IS UP, CENTER POST IS ALSO. RESET BY SPECIAL.  SCORE: 100 POINTS."
4481 REM  MODIFIED BY BRUCE TOGNAZZINI 7/17/78
4490 PRINT :PRINT "SPINNER (GREEN) - SPINS BALL 5-20 TIMES WHEN HIT, AND RELEASES IT UNPREDICTABLY ";
4500 PRINT "IN AN UPWARD DIRECTION. SCORES 10 POINTSPER SPIN, OR 50 PTS. PER SPIN WHEN LIT."
4520 PRINT :PRINT "   HIT ANY KEY TO START GAME...";:FOR I=1 TO 1000:NEXT I
4530 GOSUB 3000:IF KEY<128 THEN 4530
4540  POKE 50,255:GOTO 4990
4900  POKE 50,63:VTAB 1:TAB 1:FOR I=1 TO 23:PRINT "                                        ";:NEXT I:VTAB 1:TAB 1:RETURN 
4990 RETURN 
9100 P0=PDL (0)/8+5:IF P0>30 THEN P0=30:IF P0<5 THEN P0=5
9999 HLIN P0+5,34 AT 39:COLOR= 2:HLIN P0,P0+4 AT 39
65535 REM 
65535 REM  SINETU DENKI AKIHABARA     MUDAN TENSHIYA KINSHI (C) 1979
