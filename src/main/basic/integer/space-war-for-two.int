10 REM ***  SPACE WAR PROGRAM   ***
20 REM * BY: BOB SANDER-CEDERLOF  *
30 REM * S-C SOFTWARE,P.O.BOX 5537*
40 REM *   RICHARDSON, TX 75080   *
50 REM * FROM 'SOFTSIDE' APPLE ED.*
60 REM *  VOL.I, NO.3, APRIL 1980 *
70 REM *  LISTED BY: TED M. SNOOK *
80 GOTO 810
100 REM  EXPLOSION SOUND & GRAPHICS
110 IF A<39 THEN PLOT A+1,B:IF A>0 THEN PLOT A-1,B:IF B>0 THEN PLOT A,B-1:IF B<39 THEN PLOT A,B+1:GOTO 160
120 IF A=39 THEN 130:IF B>0 THEN PLOT A+1,B-1:IF B<39 THEN PLOT A+1,B+1:BEEP= PEEK (-16336)
130 IF A=0 THEN 140:IF B>0 THEN PLOT A-1,B-1:IF B<39 THEN PLOT A-1,B+1:BEEP= PEEK (-16336)
140 GOTO 160
150 IF A>1 THEN PLOT A-2,B:IF A<38 THEN PLOT A+2,B:IF B>1 THEN PLOT A,B-2:IF B<38 THEN PLOT A,B+2
160 BEEP= PEEK (-16336)- PEEK (-16336)- PEEK (-16336):RETURN
200 REM  UPDATE REMAINING TIME
210 IF T2<2 THEN 220:IF T1=0 THEN 780:VTAB 24:TAB 1:PRINT "TIME:  ";T1;".";T0;" UNITS ";:T2=0:T0=T0-1:IF T0>=0 THEN 220:T0=9:T1=T1-1
220 T2=T2+1
230 REM  REMOVE
240 REM REMOVE
300 REM  UPDATE SPACE SHIP POSITIONS
310 FOR I=0 TO 1
320 P1=PDL (I)*40/256:IF P1=P(I) THEN 340
330 COLOR= 0:PLOT I*39,P(I):P(I)=P1:COLOR= C(I):PLOT 39*I,P1
340 NEXT I
400 REM  * MAIN GAME LISTING *
410 FOR I=0 TO 1
420 IF W(I)<10 OR  PEEK (-16287+I)<128 THEN 650
430 VTAB 22:TAB T(I):PRINT "  FIRE !!!      "
440 DX=1-I-I:X1=I*37+1:X2=39-X1:CALL 2
450 FOR X=X1 TO X2 STEP DX
460 IF W(I)=0 THEN 510
470 COLOR= 9:PLOT X,P(I)
480 A=X+DX:B=P(I):IF  SCRN(A,B)#0 THEN 540
490 W(I)=W(I)-1
500 NEXT X
510 VTAB 22:TAB T(I):PRINT "NOTHING HIT       "
520 VTAB 21:TAB T(I):PRINT W(I);" ENERGY UNITS  "
530 GOTO 590
540 COLOR= 15:GOSUB 110:COLOR= 9:GOSUB 120:COLOR= 0:GOSUB 110:COLOR= 15:GOSUB 150:COLOR= 0:GOSUB 120:COLOR= 0:GOSUB 150
550 VTAB 22:TAB T(I):PRINT "HIT ";
560 IF X+DX=39-39*I THEN 730
570 PRINT "A PLANET    ":S(I)=S(I)+10
580 COLOR= 0:PLOT X+DX,P(I)
590 COLOR= 0:IF DX>0 THEN 620
600 HLIN X,38 AT P(I)
610 GOTO 630
620 HLIN 1,X AT P(I)
630 VTAB 23:TAB T(I):PRINT "SCORE IS ";S(I);"  "
640 IF S(I)>499 THEN 760
650 IF W(I)>=100 THEN 680
660 W(I)=W(I)+1
670 VTAB 21:TAB T(I):PRINT W(I);" ENERGY UNITS  "
680 IF W(I)<90 THEN 710
690 S(I)=S(I)-S(I)/147
700 VTAB 23:TAB T(I):PRINT "SCORE IS ";S(I);"  "
710 NEXT I
720 GOTO 210
730 PRINT "A SPACE SHIP"
740 S(I)=S(I)+100
750 GOTO 590
760 VTAB 24:TAB Y(I):PRINT :PRINT "VICTOR !!            ";:GR
770 GOTO 1110
780 VTAB 24:TAB 1:PRINT "YOUR TIME HAS RUN OUT !!!"
790 GOTO 1110
800 REM  POKE LASER SOUND ROUTINE
810 REM Moved from $0002 to $0302: _
    0302-   AC 00 03    LDY $0300 _
    0305-   AD 30 C0    LDA $C030 _
    0308-   AE 01 03    LDX $0301 _
    030B-   CA          DEX _
    030C-   D0 FD       BNE $030B _
    030E-   88          DEY _
    030F-   D0 F4       BNE $0305 _
    0311-   CE 01 03    DEC $0301 _
    0314-   D0 EC       BNE $0302 _
    0316-   60          RTS
820 POKE 0x302,0xac: POKE 0x303,0x00: POKE 0x304,0x03: _
    POKE 0x305,0xad: POKE 0x306,0x30: POKE 0x307,0xc0: _
    POKE 0x308,0xae: POKE 0x309,0x01: POKE 0x309,0x03: _
    POKE 0x30b,0xca: _
    POKE 0x30c,0xd0: POKE 0x30d,0xfd: _
    POKE 0x30e,0x88: _
    POKE 0x30f,0xd0: POKE 0x310,0xf4: _
    POKE 0x311,0xce: POKE 0x312,0x01: POKE 0x313,0x03: _
    POKE 0x314,0xd0: POKE 0x315,0xec: _
    POKE 0x316,0x60
1000 REM  PRINT INSTRUCTIONS
1010 DIM P(1),S(1),C(1),W(1),T(1)
1020 TEXT :CALL -936
1030 VTAB 3:TAB 15:PRINT "SPACE WAR":TAB 15:PRINT "----- ---":VTAB 7
1040 PRINT "EACH PILOT CONTROLS HIS SHIP WITH A GAMEPADDLE.  IF HE HAS AT LEAST TEN ENERGY  UNITS, HE MAY FIRE A LASER. "
1050 PRINT "  MISS OR FALL SHORT ... NO POINTS":PRINT "  HIT A PLANET ......... 10 POINTS":PRINT "  HIT A SPACE SHIP .... 100 POINTS"
1060 PRINT "IF A PILOT IS TOO SLOW, HE MAY LOSE":PRINT "POINTS.  THE FIRST PILOT TO GET 500":PRINT "POINTS WINS THE WAR."
1070 PRINT :PRINT :PRINT "PRESS BOTH PADDLE BUTTONS TO BEGIN...."
1100 REM  WAIT FOR BOTH PADDLE BUTTONS
1110 IF  PEEK (-16286)<128 OR  PEEK (-16287)<128 THEN 1110
1200 REM  INIT GAME, AND DRAW 25               RANDOM PLANETS
1210 CALL -936:GR
1220 FOR I=1 TO 25
1230 X= RND (30)+5:Y= RND (40):COLOR=  RND (15)+1
1240 PLOT X,Y
1250 NEXT I
1260 W(0)=0:W(1)=0:T(0)=1:T(1)=21:P(0)=0:P(1)=0:S(0)=0:S(1)=0:C(0)=12:C(1)=6
1270 T0=0:T1=100:T2=0
1280 COLOR= C(0):PLOT 0,0
1290 COLOR= C(1):PLOT 39,0
1300 GOTO 210
1400 REM * THE FOLLOWING LINE NUMBERSSTART MAJOR SECTIONS OF THIS    PROGRAM.
1405 REM
1410 REM   10 * DOCUMENTATION
1420 REM  100 * EXPLOSION SOUND AND             GRAPHICS
1430 REM  200 * UPDATE REMAINING TIME
1440 REM  300 * UPDATE SPACE-SHIP               POSITIONS
1450 REM  400 * MAIN GAME
1460 REM  800 * POKE LASER SOUND                ROUTINE
1470 REM 1000 * PRINT INSTRUCTIONS
1480 REM 1100 * WAIT FOR BOTH PADDLE            BUTTONS
1490 REM 1200 * INIT GAME, DRAW 25              RANDOM PLANETS
1500 REM *** END OF SPACE WAR PROGRAM
