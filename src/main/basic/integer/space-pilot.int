5 GOTO 9000
38 CALL -936
39 LINE=1:Z=19:POS=19
40 X=PDL (0):POS=POS+(X>250)+(X>160)-(X<10)-(X<95):IF POS>37 THEN POS=37:IF POS<0 THEN POS=0
44 COUNT=COUNT+1:IF COUNT<7 THEN 50
45 VTAB LINE:TAB Z+1:PRINT "   ";:LINE=LINE+1:VTAB LINE:TAB Z+1:PRINT "(O)";
46 Y=2*LINE:X=POS:IF ( SCRN(X,Y)+ SCRN(X+1,Y)+ SCRN(X+2,Y))<>0 THEN 100
47 COUNT=0:REM  LINES 45-48 ADVANCE THE SHIP ONE LINE DOWN THE SCREEN
48 IF LINE>20 THEN 2000
50 IF  RND (11)>SKILL THEN 52
51 Y= RND (40)+1:GOTO 55
52 Y=1+39* RND (2)
55 VTAB LINE:TAB Z+1:PRINT "   ";
58 VTAB 24:TAB Y:PRINT "*"
59 VTAB LINE:TAB POS+1:PRINT "(O)";
61 J=2*LINE:X=POS
63 S= SCRN(X,J)+ SCRN(X+1,J)+ SCRN(X+2,J):IF S<>0 THEN 100
65 CC=CC+1:IF CC>TIME THEN 1000
70 Z=POS
75  POKE 0x300,255: POKE 0x301,2
76 CALL 0x302
80 IF  PEEK (-16287)>127 THEN 40
82 SLOW=SLOW+1
86  POKE 0x300,200+ RND (56): POKE 0x301,SP+1
87 CALL 0x302
90 GOTO 40
100 REM  EXPLODE
102 VTAB LINE:TAB POS+1:PRINT "(*)";
105 HITS=HITS+1:GOTO 115
110 FOR I=1 TO 30:Z= PEEK (-16334)- PEEK (-16334)+ PEEK (-16334)
112 NEXT I:PRINT "<CTRL-G>";:RETURN 
115 X=POS-1
117 IF X>34 THEN X=34:IF X<1 THEN X=1
118 IF LINE<2 THEN 119:VTAB LINE-1:TAB X:PRINT " . . . ";:GOSUB 110
119 VTAB LINE
120 TAB X:PRINT ".......";:GOSUB 110
130 VTAB (LINE+1):TAB X:PRINT ".......";:GOSUB 110
132 IF LINE>20 THEN 38
135 VTAB (LINE+2):TAB X:PRINT " . . . ";:GOSUB 110
140 VTAB LINE+3:TAB X:PRINT "  . .  ";:GOSUB 110
210 GOTO 38
1000 VTAB 17:CALL -958
1010 TAB 15:PRINT "GAME OVER<CTRL-G>"
1015 FOR I=1 TO 3: POKE 0x300,210+ RND (46): POKE 0x301,255
1016 CALL 0x302
1017 NEXT I
1020 TAB 15:PRINT "HITS = ",HITS
1035 TAB 15:PRINT "MADE IT ",MADEIT," TIMES"
1045 TAB 15:PRINT "SKILL LEVEL = ",SKILL
1060 SPEED=(TIME-SLOW)*50/TIME
1065 TAB 8
1070 PRINT :PRINT "AVERAGE SPEED WAS ",SPEED
1075 PRINT "(0=SLOW, 50=VERY FAST)"
1080 INPUT "ANOTHER?",Y$
1085 IF Y$="Y" THEN 9000
1100 END 
2000 VTAB 1:TAB 10:PRINT "YOU MADE IT !<CTRL-G><CTRL-G><CTRL-G><CTRL-G><CTRL-G><CTRL-G><CTRL-G><CTRL-G><CTRL-G><CTRL-G><CTRL-G><CTRL-G>"
2010 MADEIT=MADEIT+1
2020 IF COUNT<TIME THEN 38
2030 GOTO 1000
9000 REM   EVADE THE STARS
9010 TEXT :CALL -936:VTAB 5:PRINT "THIS IS THE SPACE PILOT GAME."
9017 PRINT :PRINT "YOU ARE DARTH VADER ON HIS WAY BACK TO   THE IMPERIAL HEADQUARTERS AFTER THE      DEATH STAR BATTLE."
9019 PRINT :PRINT "YOUR SPACESHIP LOOKS LIKE THIS   (O)"
9021 PRINT :PRINT "USE PADDLE ZERO TO STEER.":PRINT "PRESS THE BUTTON TO SPEED UP."
9030 PRINT :PRINT "THE GAME WILL LAST 1-4 MINUTES."
9040 PRINT :PRINT "GIVE ME YOUR SKILL LEVEL (1-10)  ":INPUT SKILL
9100 TIME=700
9200 SP=(11-SKILL)*3
9300 REM _
    Moved this to $300 to not conflict with interpreter _
        0302-   AD 30 C0    LDA   $C030 _
        0305-   88          DEY _
        0306-   D0 05       BNE   $030D _
        0308-   CE 01 03    DEC   $0301 _
        030B-   F0 09       BEQ   $0316 _
        030D-   CA          DEX _
        030E-   D0 F5       BNE   $0305 _
        0310-   AE 00 03    LDX   $0300 _
        0313-   4C 02 03    JMP   $0302 _
        0316-   60          RTS
9310  POKE 0x302,0xad: POKE 0x303,0x30: POKE 0x304,0xc0: _
      POKE 0x305,0x88: _
      POKE 0x306,0xd0: POKE 0x307,0x05: _
      POKE 0x308,0xce: POKE 0x309,0x01: POKE 0x30a,0x03
9320  POKE 0x30b,0xf0: POKE 0x30c,0x09: _
      POKE 0x30d,0xca: _
      POKE 0x30e,0xd0: POKE 0x30f,0xf5: _
      POKE 0x310,0xae: POKE 0xe11,0x00: POKE 0xe12,0x03: _
      POKE 0x313,0x4c: POKE 0x314,0x02: POKE 0x315,0x03: _
      POKE 0x316,0x60
9400 GOTO 38
10000 END 
