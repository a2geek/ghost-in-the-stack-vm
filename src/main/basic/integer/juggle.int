100 TEXT : POKE -16298,0
110 GOTO 150
120 Q=(PDL (0)-20)/6:IF Q<0 THEN Q=0:IF Q>=36 THEN Q=36:COLOR= D
130 HLIN Q,Q+3 AT 36:COLOR= A:IF P>Q THEN 140:IF Q THEN HLIN 0,Q-1 AT 36:P=Q:RETURN 
140 IF P=Q THEN RETURN :IF Q#36 THEN HLIN Q+4,39 AT 36:P=Q:RETURN 
150 GOSUB 540
160 DIM BALL(15),YO(15),YS(15),YN(15)
170 DIM XX(15)
180 HS=0
190 FOR S=1 TO 15:BALL(S)=S:NEXT S
200 CALL -936:FOR I=1 TO 500:NEXT I
210 D=2:A=0
220 PS=0:SC=0
230 B=0
240 CALL -936:GR 
250 VTAB 23
260 PRINT "SCORE         THROWS        HIGH"
270 TAB 29:PRINT "SCORE ";HS
280 B=B+1:X=B
290 FOR I=1 TO 50:NEXT I
300 Y= RND (120):XX(X)=Y/3:YO(X)=0
310 IF B=1 THEN XX(X)=20
320 YS(X)=3
330 FOR X=1 TO B
340 YN(X)=YO(X)+YS(X)
350 GOSUB 120
360 IF YN(X)>-1 AND YN(X)<36 THEN 460
370 YS(X)=YS(X)*-1
380 IF YN(X)<36 THEN 340
390 IF  SCRN(XX(X),YN(X))<>D THEN 820
400 PS=PS+1:SC=SC+B*10:VTAB 23:TAB 7:PRINT SC:VTAB 23:TAB 22:PRINT PS
410 T= PEEK (-16336)- PEEK (-16336)+ PEEK (-16336)- PEEK (-16336)
420 COLOR= BALL(X):PLOT XX(X),YN(X)
430 COLOR= 0:PLOT XX(X),YO(X)
440 YO(X)=YN(X)
450 GOTO 340
460 COLOR= BALL(X)
470 PLOT XX(X),YN(X)
480 COLOR= 0
490 PLOT XX(X),YO(X)
500 YO(X)=YN(X)
510 NEXT X
520 IF  PEEK (-16287)>127 THEN 280
530 GOTO 330
540 REM  TITLE AND INSTRUCTIONS
550 CALL -936
560 GR 
570 X=6:GOSUB 860
580 PLOT 7,23:GOSUB 940
590 PLOT 8,23:X=9:GOSUB 890
600 X=11:GOSUB 890
610 PLOT 12,23:GOSUB 940
620 PLOT 13,23:X=14:GOSUB 890
630 X=16:GOSUB 890
640 PLOT 17,23:GOSUB 940
650 PLOT 18,23:X=19:GOSUB 860
660 A=17:Y=12:GOSUB 830
670 PLOT 18,20
680 X=21:GOSUB 890
690 PLOT 22,23:GOSUB 940
700 PLOT 23,23:X=24:GOSUB 860
710 A=22:Y=12:GOSUB 830
720 PLOT 23,20
730 X=26:GOSUB 890
740 A=27:Y=23:GOSUB 830
750 X=31:GOSUB 890
760 A=32:Y=23:GOSUB 830
770 A=32:Y=12:GOSUB 830
780 A=32:Y=18:GOSUB 830
790 COLOR= 0:FOR X=1 TO 100:NEXT X:PLOT 34,18
800 GOTO 960
810 END 
820 GOTO 1130
830 FOR X=A TO A+2
840 GOSUB 920
850 NEXT X:RETURN 
860 FOR Y=23 TO 20 STEP -1
870 GOSUB 920
880 NEXT Y:RETURN 
890 FOR Y=23 TO 12 STEP -1
900 GOSUB 920
910 NEXT Y:RETURN 
920 GOSUB 940
930 PLOT X,Y:RETURN 
940 COLOR=  RND (15)+1
950 FOR B=1 TO 30:NEXT B:RETURN 
960 FOR J=1 TO 800:NEXT J
970 PRINT "                   BY":PRINT "         BOB FORD      MARCH 1979":PRINT 
980 FOR J=1 TO 1500:NEXT J
990 PRINT :PRINT :PRINT 
1000 PRINT "DO YOU NEED INSTRUCTIONS? (Y/N) ";
1010 INPUT A$
1020 IF A$#"Y" AND A$#"N" THEN 1000
1030 IF A$="N" THEN 160
1040 TEXT :CALL -936
1050 PRINT "THE OBJECT OF JUGGLE IS TO KEEP AS":PRINT "MANY BALLS IN THE AIR AS POSSIBLE"
1060 PRINT "BY GETTING THE PADDLE UNDER THEM":PRINT "BEFORE THEY HIT THE GROUND."
1070 PRINT :PRINT "TO RELEASE ADDITIONAL BALLS, USE THE":PRINT "PUSH BUTTON ON THE PADDLE."
1080 PRINT :PRINT "EACH BOUNCE OF A BALL SCORES TEN TIMES":PRINT "THE NUMBER OF BALLS IN THE AIR AT"
1090 PRINT "THAT TIME.":PRINT :PRINT "WHEN READY TO START PUSH BUTTON":PRINT "ON PADDLE 0."
1100 IF  PEEK (-16287)>127 THEN 160
1110 IF  PEEK (-16286)>127 THEN PRINT "     ***** USE THE OTHER PADDLE *****"
1120 GOTO 1100
1130 COLOR= BALL(X):PLOT XX(X),YN(X)
1140 COLOR= 0:PLOT XX(X),YO(X)
1150 FOR I=1 TO 50:Q= PEEK (-16336):NEXT I
1160 IF SC>HS THEN HS=SC
1170 PRINT "  PUSH BUTTON FOR ANOTHER TRY"
1180 IF  PEEK (-16287)>127 THEN 200
1190 GOTO 1180
