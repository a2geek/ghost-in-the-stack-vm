10 REM ---------------------------
20 REM !  STAR WARS IV           !
30 REM ! BY ERIC WALLER          !
40 REM !  INTERGER BASIC         !
50 REM ---------------------------
55 XREAD=0:YREAD=1
56 XRANGE=255:YRANGE=255
60 GOTO 5000
100 SCOURSE=PDL (0)*7/255:TB=3:C=SCOURSE:GOSUB 400:VTAB 24:TAB 1:PRINT "COURSE";
120 RETURN
200 COLOR= 0:HLIN XC-1,XC+1 AT YC:VLIN YC-1,YC+1 AT XC
202 PAD=PDL (XREAD):IF PAD<4 THEN XC=XC-1:IF PAD>XRANGE-5 THEN XC=XC+1
205 PAD=PDL (YREAD):IF PAD<5 THEN YC=YC-1:IF PAD>YRANGE-5 THEN YC=YC+1
210 IF XC<1 THEN XC=1:IF XC>38 THEN XC=38:IF YC<1 THEN YC=1:IF YC>38 THEN YC=38
250 COLOR= 15
251 IF  SCRN(XC,YC+1)#0 OR  SCRN(XC,YC-1)#0 OR  SCRN(XC-1,YC)#0 OR  SCRN(XC+1,YC)#0 THEN 500
252 COLOR= 15
255 HLIN XC-1,XC+1 AT YC:VLIN YC-1,YC+1 AT XC
256 COL= SCRN(XC+DX(SCOURSE),YC+DY(SCOURSE)):IF COL=0 OR COL=15 THEN 257:RETURN
257 REM
260 RETURN
400 TAB 1
410 VTAB 21:PRINT "7 0 1":PRINT "6 * 2":PRINT "5 4 3":RETURN
500 FOR XX=1 TO 12:COLOR= 15*(XX MOD 2):HLIN XC-1,XC+1 AT YC:VLIN YC-1,YC+1 AT XC:PRINT "<CTRL-G>";:NEXT XX
520 POP
540 CSCR=CSCR+1
600 CALL -936:CALL -384:PRINT "                 SCORES";:GOSUB 620:PRINT "TAI FIGHTERS DESTROYED=";TD;:GOSUB 620
610 PRINT "DEATH STARS DESTROYED=";PSCR;:GOSUB 620:PRINT "X-WING FIGHTERS DESTROYED=";CSCR;
615 PRINT " ";:IF  PEEK (36)<39 THEN 615:GOTO 630
620 PRINT " ";:IF  PEEK (36)<39 THEN 620:PRINT :RETURN
630 CALL -380:FOR X=1 TO 1200:NEXT X:CALL -936:GOTO 5020
700 IF TIME MOD 2#0 THEN RETURN
710 IF  PEEK (-16384)<128 THEN RETURN
711 TCOURSE= PEEK (-16384): POKE -16368,0:TCOURSE=TCOURSE- ASC("0"):IF TCOURSE<0 OR TCOURSE>7 THEN RETURN
715 Y1=YC:X1=XC
717 FOR ZZZ=1 TO 3: POKE 0x300,60: POKE 0x301,10:CALL 0x302:FOR Y=1 TO 6:NEXT Y,ZZZ
720 FOR X=1 TO 10:X1=X1+DX(TCOURSE):Y1=Y1+DY(TCOURSE):
721 IF X1>=0 AND X1<40 AND Y1>=0 AND Y1<40 THEN 722:X=X-1:GOTO 790
722 IF  SCRN(X1,Y1)=5 THEN 750:IF  SCRN(X1,Y1)=1 THEN 800:IF  SCRN(X1,Y1)=12 THEN 900
725 COLOR= 14:PLOT X1,Y1
730 NEXT X
740 GOTO 790
750 IF  RND (3)#0 THEN X=X-1
790 X1=XC:Y1=YC:COLOR= 0:IF X>10 THEN X=10:FOR Y=1 TO X
791 X1=X1+DX(TCOURSE):Y1=Y1+DY(TCOURSE):PLOT X1,Y1:NEXT Y
795 FOR ZZZ=1 TO 3: POKE 0x300,10: POKE 0x301,10:CALL 0x302:FOR Y=1 TO 6:NEXT Y,ZZZ:RETURN
800 COLOR= 1:GOSUB 890:COLOR= 0:GOSUB 890:PSCR=PSCR+1
805 POP
810 GOTO 600
890 FOR Q=1 TO 10:HLIN 20-Q,20+Q AT 20-Q:HLIN 20-Q,20+Q AT 20+Q:VLIN 20-Q,20+Q AT 20+Q
891 VLIN 20-Q,20+Q AT 20-Q: POKE 0x300,200: POKE 0x301,10:CALL 0x302:NEXT Q
892 PLOT 20,20
893 COLOR= 15:HLIN XC-1,XC+1 AT YC:VLIN YC-1,YC+1 AT XC
895 RETURN
900 FOR ZZZ=1 TO 3: POKE 0x300,10: POKE 0x301,10:CALL 0x302:FOR Y=1 TO 6:NEXT Y,ZZZ
901 IF X>10 THEN X=10:X1=XC:Y1=YC:COLOR= 0:FOR Y=1 TO X:X1=X1+DX(TCOURSE):Y1=Y1+DY(TCOURSE)
902 PLOT X1,Y1:NEXT Y
905 TD=TD+1
910 FOR X=1 TO 10:COLOR= (X MOD 2)*12:HLIN XT-1,XT+1 AT YT:VLIN YT-1,YT+1 AT XT-1
920 VLIN YT-1,YT+1 AT XT+1:PRINT "<CTRL-G>";:NEXT X:TAI=0:RETURN
1000 IF TAI=1 THEN RETURN :IF  RND (10)#0 THEN RETURN :TAI=1:
1010 XT= RND (30)+5:YT= RND (30)+5:IF XT>13 AND XT<25 AND YT>13 AND YT<25 THEN 1010
1020 IF ABS (XC-XT)<6 AND ABS (YC-YT)<6 THEN 1010:COLOR= 12:HLIN XT-1,XT+1 AT YT:VLIN YT-1,YT+1 AT XT-1
1021 VLIN YT-1,YT+1 AT XT+1:RETURN
2000 IF TAI=0 THEN RETURN :XD=SGN (XC-XT):YD=SGN (YC-YT)
2010 XT1=XT+XD:YT1=YT+YD:IF XT1>14 AND XT1<25 AND YT1<25 AND YT1>14 THEN GOSUB 2900
2020 IF ABS (XT1-XC)<6 AND ABS (YT1-YC)<6 THEN 2200:COLOR= 0
2025 HLIN XT-1,XT+1 AT YT:VLIN YT-1,YT+1 AT XT-1:VLIN YT-1,YT+1 AT XT+1
2030 COLOR= 12:XT=XT1:YT=YT1:HLIN XT-1,XT+1 AT YT:VLIN YT-1,YT+1 AT XT-1:VLIN YT-1,YT+1 AT XT+1
2100 RETURN
2200 XT1=XT:YT1=YT:GOTO 2030
2900 XD=ABS (25-XT):IF ABS (14-XT)<XD THEN XD=ABS (14-XT):YD=ABS (25-YT):IF ABS (14-YT)<YD THEN YD=ABS (14-YT):REM  FIND DISTANCES
2910 IF XD>YD THEN 2940
2920 IF ABS (25-XT)<ABS (14-XT) THEN 2930:XT1=14:RETURN
2930 XT1=25:RETURN
2940 IF ABS (14-YT)<ABS (25-YT) THEN 2950:YT1=25:RETURN
2950 YT1=14:RETURN
3000 IF WTAI=0 THEN 4000:COLOR= OLD:PLOT XW,YW:XW=XW+DX(WC):YW=YW+DY(WC)
3005 IF XW<0 OR XW>39 OR YW<0 OR YW>39 THEN 3900:IF  SCRN(XW,YW)=0 THEN 3950
3010 IF  SCRN(XW,YW)=12 THEN 3950:IF  SCRN(XW,YW)=15 THEN 500
3020 GOTO 3950
3900 WTAI=0:RETURN
3950 OLD= SCRN(XW,YW):IF OLD=12 THEN OLD=0:COLOR= 14:PLOT XW,YW:RETURN
4000 IF TAI=0 THEN RETURN :IF TIME MOD 4#0 THEN RETURN :IF  RND (4)#0 THEN RETURN
4002 OLD=0
4005 WTAI=1:XW=XT:YW=YT
4010 D1=XC-XT:D2=YC-YT:IF D1#0 THEN 4020:WC=0:IF D2>0 THEN WC=4:RETURN
4020 IF D2#0 THEN 4030:WC=2:IF D1<0 THEN WC=6:RETURN
4030 IF D2<0 THEN 4040:IF D1<0 THEN WC=5:IF D1>0 THEN WC=3:RETURN
4040 IF D1<0 THEN WC=7:IF D1>0 THEN WC=1:RETURN
5000 REM ------------------
5005 REM ! MAIN ROUTINE   !
5010 REM ------------------
5011 TIME=0
5012 MFLAG=0: POKE -16368,0
5013 PSCR=0:CSCR=0
5014 DIM DX(7),DY(7)
5015 CALL -936
5016 TD=0
5017 GOSUB 10000
5018 GOSUB 32000
5020 GR :COLOR= 5:PLOT 19,16:PLOT 16,17:PLOT 22,17:HLIN 18,20 AT 17:HLIN 17,21 AT 18:HLIN 16,22 AT 19:HLIN 17,21 AT 20:HLIN 18,20 AT 21
5030 COLOR= 1:HLIN 18,20 AT 19:VLIN 18,20 AT 19
5035 COLOR= 5
5040 PLOT 16,21:PLOT 22,21:PLOT 19,22
5045 WTAI=0
5050 XC=5:YC=5:TRP=0:TAI=0:DX(0)=0:DX(1)=1:DX(2)=1:DX(3)=1
5055 XC=XC+ RND (2)*20:YC=YC+ RND (2)*20
5060 DX(4)=0:DX(5)=-1:DX(6)=-1:DX(7)=-1
5070 DY(0)=-1:DY(1)=-1:DY(2)=0:DY(3)=1:DY(4)=1:DY(5)=1:DY(6)=0:DY(7)=-1
5080 SPEED=0:MFLAG=1
5085 IF PSCR=6 THEN 6000:IF CSCR=8 THEN 6500
5090 GOSUB 100:GOSUB 200:REM    OUTPUT STATUS THEN MOVE SHIP
5100 GOSUB 700:REM  SEE IF HE'S SHOOTING
5105 TIME=TIME+1
5106 ZZZ= PEEK (-16336)
5107 GOSUB 1000:GOSUB 2000:REM  SERVICE TAI FIGHTER
5110 GOSUB 3000:GOSUB 3000:REM  >>> SERVICE TAI'S WEAPONS
5120 GOTO 5080:REM   PLAY IT AGAIN SAM
6000 TEXT :CALL -936:PRINT "                 VICTORY !!":PRINT :PRINT :PRINT :PRINT
6010 PRINT "  YOUR BRAVERY (AND SKILL) HAS SAVED":PRINT "REBEL ALLIANCE. NOW THE REBELS HAVE A"
6020 PRINT "CHANCE TO OVER RUN THE EMPIRE AND":PRINT "RETURN TO THE OLD GOVERNMENT !!"
6030 GOTO 6550
6500 TEXT :CALL -936:PRINT "                 DEFEAT !!":PRINT :PRINT :PRINT :PRINT
6510 PRINT "  ONE OF THE REMAINING DEATH STARS WILL":PRINT "DESTROY THE REBEL BASE AND THUSLY CRUSH":
6520 PRINT "THE ONLY REMAINING THREAT TO THE EMPIRE."
6550 PRINT "YOUR RATING:";1000*PSCR/(CSCR+1)+10*TD:END
10000 TEXT :CALL -936:CALL -384:TAB 14:PRINT "              ":TAB 14:PRINT " STAR WARS IV ":TAB 14:PRINT "              "
10010 CALL -380:PRINT :PRINT "         WRITTEN BY ERIC WALLER"
10020 PRINT :PRINT :PRINT " IT IS A PERIOD OF CIVIL WAR. AFTER":PRINT "THE REBELS DESTROYED THE FIRST DEATH ":
10030 PRINT "STAR THE EMPIRE MADE 6 MORE. YOU MUST":PRINT "DESTROY THESE 6 DEATH STARS BEFORE "
10040 PRINT "THEIR TAI FIGHTERS DESTROY YOUR 8 ":PRINT "REMAINING X-WING FIGHTERS. TO DESTROY"
10050 PRINT "A DEATH STAR YOU MUST BLAST A HOLE INTO":PRINT "ITS TENDER CENTER (THE RED SECTION)"
10060 PRINT "  TO STEER USE YOUR PADDLES.  TO    ":PRINT "FIRE YOUR BLASTERS PUSH THE NUMBER ON"
10070 PRINT "THE KEYBOARD THAT CORRESPONDS WITH THE":PRINT "COURSE YOU WANT TO FIRE AT. SEE COMPASS.";
10075 PRINT ">>>                     <<<";
10080 TAB 10:PRINT "701":TAB 10:PRINT "6";:CALL -384:PRINT "*";:CALL -380:PRINT "2":TAB 10:PRINT "543"
19890 VTAB 23
19900 PRINT "-- HIT RETURN WHEN READY --": POKE -16368,0
19910 IF  PEEK (-16384)<128 THEN 19910
19920 CALL -936
19999 RETURN
32000 REM _
    Moved sound routine: _
        0302-   AD 30 C0    LDA $C030 _
        0305-   88          DEY _
        0306-   D0 05       BNE $030D _
        0308-   CE 01 03    DEC $0301 _
        030B-   F0 09       BEQ $0316 _
        030D-   CA          DEX _
        030E-   D0 F5       BNE $0305 _
        0310-   AE 00 03    LDX $0300 _
        0313-   4C 02 03    JMP $0302 _
        0316-   60          RTS
32005  POKE 0x302,0xad: POKE 0x303,0x30: POKE 0x304,0xc0: _
       POKE 0x305,0x88: _
       POKE 0x306,0xd0: POKE 0x307,0x05: _
       POKE 0x308,0xce: POKE 0x309,0x01: POKE 0x30a,0x03: _
       POKE 0x30b,0xf0: POKE 0x30c,0x09: _
       POKE 0x30d,0xca: _
       POKE 0x30e,0xd0: POKE 0x30f,0xf5: _
       POKE 0x310,0xae: POKE 0x311,0x00: POKE 0x312,0x03: _
       POKE 0x313,0x4c: POKE 0x314,0x02: POKE 0x315,0x03: _
       POKE 0x316,0x60: RETURN
