10 DIM A$(40),NAME$(50)
20 DIM SC(85),TSC(5),T(5),D(5),D1(5),D2(5),N(5),P(5),S(13)
30 SPEED=20
40 REM
50 REM  ***** INTRODUCTION *****
60 CALL -936
70 VTAB 9:A$="Y A H T Z E E":GOSUB 7030
75 VTAB 11:A$="COPYRIGHT 1978 APPLE COMPUTER INC.":GOSUB 7030
80 VTAB 15:A$="ADAPTED FOR APPLE II BY":GOSUB 7030
90 VTAB 17:A$="GARY A. FOOTE":GOSUB 7030
100 VTAB 19:A$="4/13/78":GOSUB 7030
110 REM  FROM AN ARTICLE IN DR. DOBBS BY JIM GEROW
120 REM   RUNS IN 16K MEMORY - APPLE INTEGER BASIC
130 GOSUB 9030
140 VTAB 22:INPUT "DO YOU WANT INSTRUCTIONS?",A$
150 IF  LEN(A$)=0 THEN 140:IF A$(1,1)#"Y" THEN 160:GOSUB 9040:GOSUB 7110
160 CALL -936
170 REM
180 REM  *** PLAYER SELECTION ***
190 A$="PLAYER SELECTION":GOSUB 7030
200 A$="(ENTER BLANK LINE WHEN DONE)":GOSUB 7030
210 PRINT :FOR X=1 TO 5
220 INPUT "ENTER NEXT PLAYER'S NAME:",A$
230 IF  LEN(A$)=0 THEN 290
240 IF  LEN(A$)<=10 THEN 250:PRINT "ONLY 10 LETTERS PLEASE":GOTO 220
250 N(X)= LEN(A$)-1:A$( LEN(A$)+1)="          "
260 P(X)=(X-1)*10+1:NAME$(P(X))=A$(1,10)
270 TSC(X)=0
280 NEXT X:N2=6
290 N2=X-1
300 CALL -936
310 IF N2=1 THEN 490
320 A$="WE WILL NOW ROLL DICE":GOSUB 7030
330 A$="TO CHOOSE PLAYER ORDER":GOSUB 7030
340 FOR X=1 TO N2
350 VTAB X+3:PRINT NAME$(P(X),P(X)+N(X));"'S TURN"
360 IF NAME$(P(X),P(X)+4)="APPLE" THEN 370:GOSUB 7120:GOTO 380
370 GOSUB 9030
380 GOSUB 6010:T(X)=TTL
390 VTAB 3+X:TAB 20:PRINT TTL:NEXT X
400 REM  *** SORT TOTALS FOR PLAYER ORDER ***
410 J=0:FOR I=1 TO N2-1
420 IF T(I)>=T(I+1) THEN 470
430 X=T(I+1):T(I+1)=T(I):T(I)=X
440 X=N(I+1):N(I+1)=N(I):N(I)=X
450 X=P(I+1):P(I+1)=P(I):P(I)=X
460 J=1
470 NEXT I:IF J#0 THEN 410
480 FOR X=1 TO 21:VTAB X:TAB 35:PRINT "     ":NEXT X
490 VTAB 12:A$="PLAYER ORDER":GOSUB 7030:PRINT
500 FOR X=1 TO N2
510 PRINT "PLAYER ";X,NAME$(P(X),P(X)+N(X))
520 NEXT X:GOSUB 7110:CALL -936
530 REM
540 REM   *** GAME LOGIC LOOP ***
550 FOR GAME=1 TO 6
560 FOR I=1 TO 17*N2:SC(I)=-1:NEXT I
570 GOSUB 8010:REM  DISPLAY SCOREBOARD
580 REM   *** ROUND LOGIC LOOP ***
590 FOR ROUND=1 TO 13
600 VTAB 24:TAB 8:PRINT "R O U N D  ";ROUND;"  G A M E  ";GAME;
610 REM   *** PLAYER LOGIC LOOP ***
620 FOR PL=1 TO N2
630 PP=(PL-1)*17:HL=20/N2*PL+9:IF N2=2 THEN HL=HL-3:IF N2=1 THEN HL=HL-8
640 COLOR= 7:PLOT HL-1,1
650 VTAB 20:TAB 1:CALL -868
660 VTAB 20: POKE 50,63:PRINT NAME$(P(PL),P(PL)+N(PL));"'S TURN";: POKE 50,255
670 IF NAME$(P(PL),P(PL)+4)#"APPLE" THEN 690:GOSUB 6010:L=0:GOSUB 3020:GOSUB 2040:GOTO 950
680 REM  *** ROLL DICE ***
690 GOSUB 7120:GOSUB 6010:J1=1:VTAB 20:TAB 21:PRINT "ROLL# ";J1
700 GOSUB 5020
710 VTAB 22:CALL -868:VTAB 23:CALL -868
720 VTAB 22:TAB 1:PRINT "ENTER: P FOR POSSIBILITIES, S FOR SCORE"
730 VTAB 23: POKE 34,22: POKE 35,22:IF J1<3 THEN 740:INPUT A$:GOTO 750
740 INPUT "R FOR ANOTHER ROLL:",A$
750  POKE 34,0: POKE 35,24:GOSUB 9020
760 J=2:FOR I=1 TO 13:IF I=7 THEN J=J+2:IF SC(PP+I)#-1 THEN 770:VTAB J:TAB HL:PRINT "  ";
770 J=J+1:NEXT I
780 IF SC(PP+14)#-1 THEN 790:VTAB 8:TAB HL:PRINT "  ";
790 IF  LEN(A$)=0 THEN 710
800 IF A$(1,1)#"P" THEN 810:GOSUB 4010:GOTO 710
810 IF A$(1,1)#"S" THEN 820:GOSUB 2010:GOTO 950
820 IF J1=3 THEN 710
830 IF A$(1,1)#"R" THEN 710
840 REM  *** RE-ROLL DICE ***
850 IF  LEN(A$)=1 THEN 860:A$=A$(2, LEN(A$)):GOTO 870
860 GOSUB 9020: POKE 35,22: POKE 34,22:VTAB 23:TAB 1:INPUT "DICE TO RE-ROLL?",A$: POKE 34,0: POKE 35,24
870 IF  LEN(A$)=0 THEN 860
880 FOR X=1 TO 5:D2(X)=0:NEXT X
890 FOR X=1 TO  LEN(A$)
900 T= ASC(A$(X,X))-176:IF T<1 OR T>5 THEN 860
910 D(T)= RND (6)+1:D2(T)=1
920 NEXT X
930 TTL=D(1)+D(2)+D(3)+D(4)+D(5):GOSUB 6030:GOSUB 5020
940 J1=J1+1:VTAB 20:TAB 21:PRINT "ROLL# ";J1:GOTO 710
950 FOR X=1 TO 21:TAB 35:VTAB X:PRINT "     ":NEXT X
960 VTAB 1:TAB HL:PRINT PL
970 NEXT PL
980 NEXT ROUND
990 REM   *** SCORE GAME TOTALS ***
1000 FOR X=1 TO N2:TSC(X)=TSC(X)+SC(X*17):T(X)=TSC(X):D(X)=X:NEXT X
1010 CALL -936:VTAB 8:A$="GAME AND SERIES TOTALS":GOSUB 7030:PRINT
1020 REM   *** SORT & PRINT SCORES ***
1030 IF N2=1 THEN 1100
1040 J=0:FOR I=1 TO N2-1
1050 IF T(I)>=T(I+1) THEN 1090
1060 X=T(I+1):T(I+1)=T(I):T(I)=X
1070 X=D(I+1):D(I+1)=D(I):D(I)=X
1080 J=1
1090 NEXT I:IF J#0 THEN 1040
1100 FOR X=1 TO N2
1110 PRINT NAME$(P(D(X)),P(D(X))+N(D(X)));:TAB 12:PRINT SC(D(X)*17),T(X)
1120 IF SC(D(X)*17)=375 THEN PRINT "CONGRATUATIONS - A PERFECT GAME!!"
1130 NEXT X
1140 PRINT :A$="HIT SPACE BAR FOR NEXT GAME":GOSUB 7030
1150 A$="HIT RETURN TO END SERIES":GOSUB 7030
1160 IF  PEEK (-16384)<127 THEN 1160:IF  PEEK (-16384)#160 THEN 1180
1170  POKE -16368,0:NEXT GAME
1180  POKE -16368,0:PRINT :PRINT "CONGRATUATIONS, YOU WON ";NAME$(P(D(1)),P(D(1))+N(D(1)))
1190 END
2000 REM
2010 REM   *** PLAYER SCORING ***
2020 IF  LEN(A$)=1 THEN 2030:T= ASC(A$(2,2))-176:IF  LEN(A$)=3 AND T=1 THEN T=10+( ASC(A$(3,3))-176):GOTO 2040
2030 GOSUB 9020:VTAB 22: POKE 34,22: POKE 35,22:INPUT "WHAT ROW # DO YOU WISH TO SCORE?",T: POKE 34,0: POKE 35,24
2040 IF T<1 OR T>13 THEN 2030
2050 IF SC(PP+T)#-1 THEN 2030
2060 SC(PP+T)=S(T)
2070 J=T+1:IF T>6 THEN J=J+2:JJ=J:TT=S(T)
2080 VTAB JJ:TAB HL:PRINT TT:PLOT HL-1,JJ*2-1:REM  FLASH SCORES
2090 IF TT>9 THEN PLOT HL,JJ*2-1
2100 T=0:FOR I=1 TO 6:IF SC(PP+I)>0 THEN T=T+SC(PP+I)
2110 NEXT I:SC(PP+14)=T:REM UPPER SCORE
2120 SC(PP+15)=0:IF T<63 THEN 2130:SC(PP+15)=35:VTAB 8:TAB HL: POKE 50,63:PRINT 35;: POKE 50,255
2130 SC(PP+14)=SC(PP+14)+SC(PP+15):REM BONUS
2140 T=0:FOR I=7 TO 13:IF SC(PP+I)>0 THEN T=T+SC(PP+I):REM LOWER SCORE
2150 NEXT I:SC(PP+16)=T:SC(PP+17)=SC(PP+16)+SC(PP+14):REM TOTAL SCORE
2160 VTAB 9:TAB HL:PRINT SC(PP+14);
2170 VTAB 17:TAB HL:PRINT SC(PP+16);
2180 VTAB 18:TAB HL:PRINT SC(PP+17);
2190 GOSUB 9030:VTAB JJ:TAB HL:PRINT TT:GOSUB 9020:RETURN
3000 REM
3010 REM  *** APPLE ROLL AND SCORING ***
3020 L=L+1:VTAB 20:TAB 21:PRINT "ROLL# ";L;:GOSUB 5020
3030 IF S(12)=0 THEN 3040:T=12:RETURN :REM  SCORE YAHTZEE
3040 IF S(11)=0 THEN 3050:T=11:RETURN :REM  SCORE LG.STRT
3050 IF S(9)=0 THEN 3060:T=9:RETURN :REM  SCORE SM.STRT
3060 IF SF=0 THEN 3250
3070 IF L#3 AND SC(PP+11)=-1 THEN 3110
3080 IF S(10)=0 THEN 3250:REM  SCORE SM.STRT
3090 IF SC(PP+11)<0 THEN 3100:T=10:RETURN
3100 IF L#3 THEN 3110:T=10:RETURN
3110 I=0:REM  TRY FOR LG.STRT
3120 I=I+1:IF D1(I)#D1(I+1) THEN 3130:K=D1(I):GOTO 3170
3130 IF I<4 THEN 3120
3140 IF D1(1)=1 AND D1(2)#2 THEN 3150:GOTO 3160
3150 K=1:GOTO 3170
3160 K=6
3170 REM  APPLE ROLLS FOR LG.STRT
3180 I=0
3190 I=I+1:IF D(I)=K THEN 3220
3200 IF I<5 THEN 3190
3210 PRINT "ERROR - NOTHING MATCHES"
3220 FOR N=1 TO 5:D2(N)=0:NEXT N:D(I)= RND (6)+1:D2(I)=1
3230 TTL=D(1)+D(2)+D(3)+D(4)+D(5)
3240 GOSUB 6030:GOTO 3020
3250 IF S(8)=0 THEN 3320
3260 IF L#3 THEN 3300
3270 IF TTL>18 THEN 3290
3280 IF SC(PP+D1(2))#-1 THEN 3290:T=D1(2):RETURN
3290 T=8:RETURN :REM  SCORE 4/KIND
3300 IF D1(1)#D1(2) THEN 3310:K=D1(5):GOTO 3170
3310 K=D1(1):GOTO 3170
3320 IF S(7)=0 THEN 3410
3330 J=3:IF L#3 THEN 3360:REM  SCORE 3/KIND
3340 IF SC(PP+D1(3))#-1 THEN 3350:T=D1(3):RETURN
3350 T=7:RETURN
3360 REM  APPLE ROLLS AGIAN
3370 FOR I=1 TO 5:D2(I)=0
3380 IF D(I)=D1(J) THEN 3400
3390 D(I)= RND (6)+1:D2(I)=1
3400 NEXT I:GOTO 3230
3410 I=7:IF L#3 THEN 3450
3420 I=I-1:IF S(I)/I<3 THEN 3430:T=I:RETURN
3430 IF I>1 THEN 3420
3440 GOTO 3560
3450 J=5:K=0
3460 J=J-1:IF D1(J)#D1(J+1) THEN 3490
3470 T=D1(J)
3480 K=1:IF SC(PP+D1(J))=-1 THEN 3360
3490 IF J>1 THEN 3460
3500 IF SF=1 AND SC(PP+11)=-1 THEN 3110
3510 J=0
3520 J=J+1:IF SC(PP+D1(J))=-1 THEN 3360
3530 IF J<5 THEN 3520
3540 REM  APPLE ROLLS ALL DICE AGAIN
3550 GOSUB 6010:GOTO 3020
3560 IF SC(PP+13)#-1 THEN 3570:T=13:RETURN :REM SCORE CHANCE
3570 IF SC(PP+12)#-1 THEN 3580:T=12:RETURN :REM ELSE TAKE A ZERO IN
3580 IF SC(PP+11)#-1 THEN 3590:T=11:RETURN :REM SOME CATAGORY
3590 IF SC(PP+9)#-1 THEN 3600:T=9:RETURN
3600 IF SC(PP+10)#-1 THEN 3610:T=10:RETURN
3610 IF SC(PP+8)#-1 THEN 3620:T=8:RETURN
3620 T=0:REM TRY SCORE IN UPPER HALF
3630 T=T+1
3640 IF SC(PP+T)=-1 THEN RETURN
3650 IF T<=6 THEN 3630
3660 PRINT "ERROR - NO FREE ROWS":RETURN
4000 REM
4010 REM   *** PRINT SCORING POSSIBILITIES ***
4020 J=2: POKE 50,127:FOR I=1 TO 13
4030 IF SC(PP+I)#-1 THEN 4080
4040 IF J1>2 THEN 4060
4050 IF S(I)=0 THEN 4080
4060 VTAB J:IF I>6 THEN VTAB J+2
4070 TAB HL:PRINT S(I);
4080 J=J+1:NEXT I: POKE 50,255:RETURN
5000 REM
5010 REM   *** TALLY SCORING CHOICES ***
5020 FOR N=1 TO 5:D1(N)=D(N):NEXT N:SF=0
5030 J=0:FOR N=1 TO 4:REM PUT DICE IN NUMERICAL ORDER
5040 IF D1(N)<=D1(N+1) THEN 5060
5050 J=1:T=D1(N):D1(N)=D1(N+1):D1(N+1)=T
5060 NEXT N:IF J#0 THEN 5030
5070 FOR J=1 TO 13:S(J)=0:NEXT J
5080 I=2
5090 IF D(1)#D(I) THEN 5190
5100 I=I+1:IF I<6 THEN 5090
5110 IF SC(PP+12)=-1 THEN S(12)=50:REM CHECK 5/KIND
5120 IF SC(PP+8)=-1 THEN S(8)=TTL
5130 IF SC(PP+7)=-1 THEN S(7)=TTL
5140 IF SC(PP+13)=-1 THEN S(13)=TTL
5150 FOR I=1 TO 5:REM CHECK UPPER HALF
5160 IF SC(PP+D1(I))#-1 THEN 5180
5170 S(D1(I))=S(D1(I))+D1(I)
5180 NEXT I:RETURN
5190 I=2
5200 IF D1(1)#D1(I) THEN 5230
5210 I=I+1:IF I<5 THEN 5200
5220 GOTO 5260
5230 I=3:REM CHECK 4/KIND
5240 IF D1(2)#D1(I) THEN 5270
5250 I=I+1:IF I<6 THEN 5240
5260 K=4:GOTO 5120
5270 IF D1(1)=D1(2) AND D1(1)=D1(3) THEN 5330
5280 IF D1(2)=D1(3) AND D1(2)=D1(4) THEN 5330
5290 IF NOT (D1(3)=D1(4) AND D1(3)=D1(5)) THEN 5350
5300 IF D1(1)#D1(2) THEN 5340:REM CHECK FULL HOUSE
5310 IF SC(PP+9)#-1 THEN 5320:S(9)=25:GOTO 5340
5320 GOTO 5340
5330 IF D1(4)=D1(5) THEN 5310
5340 K=3:GOTO 5130
5350 FOR I=1 TO 4:IF D1(5)#D1(I)+5-I THEN 5360:NEXT I:GOTO 5370
5360 GOTO 5380
5370 IF SC(PP+11)#-1 THEN 5380:S(11)=40:GOTO 5460:REM CHECK STRTS
5380 FOR I=1 TO 5:D2(I)=0:NEXT I:D2(1)=D1(1):J=1
5390 FOR I=2 TO 5
5400 IF D1(I)=D2(J) THEN 5420
5410 J=J+1:D2(J)=D1(I)
5420 NEXT I:IF J<4 THEN 5140
5430 FOR I=1 TO 3:IF D2(4)#D2(I)+4-I THEN 5440:NEXT I:GOTO 5460
5440 FOR I=2 TO 4:IF D2(5)#D2(I)+5-I THEN 5450:NEXT I:GOTO 5460
5450 GOTO 5140
5460 SF=1:IF SC(PP+10)#-1 THEN 5470:S(10)=30:GOTO 5140
5470 GOTO 5140
6000 REM
6010 REM  *** ROLL DICE AND DISPLAY ***
6020 TTL=0:FOR N=1 TO 5:D(N)= RND (6)+1:TTL=TTL+D(N):D2(N)=1:NEXT N
6030 FOR Z=1 TO 5:IF D2(Z)=0 THEN 6060
6040 Y=2:XX=5:IF Z=1 THEN Y=1:IF Z=5 THEN 6050:IF D2(Z+1)#1 THEN XX=4
6050 FOR N=Y TO XX:TAB 35:VTAB (Z-1)*4+N:PRINT "     ":NEXT N
6060 NEXT Z
6070 FOR N=1 TO 5:IF D2(N)=0 THEN 6160
6080 Y=(N-1)*4:GOSUB 6170:Y=Y+1:GOTO 6080+D(N)*10
6090 GOSUB 6180:XX=37:GOSUB 6190:GOSUB 6180:GOTO 6150
6100 XX=38:GOSUB 6190:GOSUB 6180:XX=36:GOSUB 6190:GOTO 6150
6110 XX=38:GOSUB 6190:XX=37:GOSUB 6190:XX=36:GOSUB 6190:GOTO 6150
6120 XX=99:GOSUB 6190:GOSUB 6180:XX=99:GOSUB 6190:GOTO 6150
6130 XX=99:GOSUB 6190:XX=37:GOSUB 6190:XX=99:GOSUB 6190:GOTO 6150
6140 XX=99:GOSUB 6190:GOSUB 6190:GOSUB 6190
6150 FOR Z=1 TO SPEED:XX= PEEK (-16336):NEXT Z:GOSUB 6170
6160 NEXT N:RETURN
6170 VTAB Y+1:TAB 35: POKE 50,63:PRINT "-----": POKE 50,255:RETURN
6180 XX=0
6190 Y=Y+1:VTAB Y:TAB 35: POKE 50,63:PRINT "     ";: POKE 50,255
6200 IF XX=0 THEN 6230
6210 IF XX=99 THEN 6220:TAB XX:PRINT "*":GOTO 6230
6220 TAB 36:PRINT "*";:TAB 38:PRINT "*"
6230 RETURN
7000 REM
7010 REM  *** PRINT ROUTINES ***
7020  POKE 50,63
7030 TAB (40- LEN(A$)+1)/2
7040 PRINT A$: POKE 50,255:RETURN
7050 TAB (40- LEN(A$)+1)/2
7060 FOR L=1 TO  LEN(A$)
7070 IF A$(L,L)#" " THEN  POKE 50,127
7080 PRINT A$(L,L);: POKE 50,255
7090 NEXT L:RETURN
7100 REM  *** MESSAGES ***
7110 VTAB 22:A$="HIT SPACE TO CONTINUE":GOSUB 7020:GOTO 7130
7120 GOSUB 9020:TAB 1:VTAB 23:A$=">>> PRESS SPACE BAR TO ROLL DICE <<<":GOSUB 7050
7130 Z= RND (10):IF  PEEK (-16384)#160 THEN 7130
7140  POKE -16368,0:VTAB 23:TAB 1:CALL -868:RETURN
8000 REM
8010 REM  *** SCORECARD ***"
8020 CALL -936
8030 PRINT "  PLAYERS >>"
8040 PRINT " 1 ACES"
8050 PRINT " 2 TWOS"
8060 PRINT " 3 THREES"
8070 PRINT " 4 FOURS"
8080 PRINT " 5 FIVES"
8090 PRINT " 6 SIXES"
8100 PRINT "   BONUS"
8110 PRINT "UPPER TOTAL"
8120 PRINT " 7 3/KIND"
8130 PRINT " 8 4/KIND"
8140 PRINT " 9 FL.HOUSE"
8150 PRINT "10 SM.STRT"
8160 PRINT "11 LG.STRT"
8170 PRINT "12 YAHTZEE"
8180 PRINT "13 CHANCE"
8190 PRINT "LOWER TOTAL"
8200 PRINT "GRAND TOTAL";
8210 FOR X=1 TO N2:VTAB 1:XX=20/N2*X+9:IF N2=2 THEN XX=XX-3:IF N2=1 THEN XX=21:TAB XX:PRINT X;:NEXT X
8220 FOR X=1 TO 5:VTAB X*4-1:TAB 33:PRINT X;:NEXT X
8230 RETURN
9000 REM
9010 REM  *** RULES ***
9020 VTAB 22:TAB 1:CALL -868:VTAB 23:TAB 1:CALL -868:RETURN
9030 FOR XX=1 TO SPEED*50:NEXT XX:RETURN
9040 CALL -936:PRINT "YAHTZEE IS A POPULAR GAME IN WHICH EACH"
9050 PRINT "PLAYER SCORES THE VALUE OF HIS DICE IN"
9060 PRINT "ONE OF SEVERAL SCORING CATEGORIES."
9070 PRINT "YOU BEGIN YOUR TURN BY ROLLING"
9080 PRINT "ALL FIVE DICE. IF YOU LIKE, YOU CAN"
9090 PRINT "RE-ROLL ANY OR ALL THE DICE TWICE MORE"
9100 PRINT "BEFORE SELECTING YOUR SCORING CATAGORY"
9110 PRINT "PLAYERS SCORE IN THE FOLLOWING MANNER."
9120 PRINT "EACH OF THE THIRTEEN CATEGORIES HAS A"
9130 PRINT "RULE FOR SCORING. THEY ARE:"
9140 PRINT :PRINT "  UPPER SECTION"
9150 PRINT "1.ACES  - SUM OF ALL 'ONES'."
9160 PRINT "2.TWOS  - SUM OF ALL 'TWOS'."
9170 PRINT "3.THREES- SUM OF ALL 'THREES'."
9180 PRINT "4.FOURS - SUM OF ALL 'FOURS'."
9190 PRINT "5.FIVES - SUM OF ALL 'FIVES'."
9200 PRINT "6.SIXES - SUM OF ALL 'SIXES'."
9210 PRINT :PRINT "IF THE TOTAL FOR THE UPPER SECTION IS"
9220 PRINT "63 OR MORE, A 35 POINT BONUS IS ADDED.":GOSUB 7110:CALL -936
9230 PRINT "  LOWER SECTION":PRINT
9240 PRINT "7.3 OF A KIND- IF YOU HAVE 3 OF A KIND,"
9250 PRINT "               YOU SUM ALL DICE."
9260 PRINT "8.4 OF A KIND- IF YOU HAVE 4 OF A KIND,"
9270 PRINT "               YOU SUM ALL DICE."
9280 PRINT "9.FULL HOUSE - IF YOU HAVE 3 OF ONE"
9290 PRINT "               DICE AND 2 OF ANOTHER,"
9300 PRINT "               SCORE 25 POINTS."
9310 PRINT "10.SM.STRAIGHT-IF YOU HAVE A SEQUENCE"
9320 PRINT "               OF 4 DICE, SCORE 30 PTS."
9330 PRINT "11.LG.STRAIGHT-IF YOU HAVE A SEQUENCE"
9340 PRINT "               OF 5 DICE, SCORE 40 PTS."
9350 PRINT "12.YAHTZEE    -IF YOU HAVE 5 OF A KIND,"
9360 PRINT "               SCORE 50 POINTS."
9370 PRINT "13.CHANCE     -SUM OF ALL DICE.":GOSUB 7110:CALL -936
9380 PRINT "AFTER EACH TURN, A PLAYER MUST CHOOSE"
9390 PRINT "ONE OF THESE CATEGORIES, EVEN IF IT"
9400 PRINT "SCORES A ZERO."
9410 PRINT "THE COMPUTER WILL ADVISE YOU OF THE"
9420 PRINT "POSSIBLE SCORING CHOICES IF YOU DESIRE."
9430 PRINT :PRINT "THE GAME CONTINUES UNTIL ALL CATEGORIES"
9440 PRINT "ARE FILLED. THE SCORING ENDS AFTER SIX"
9450 PRINT "GAMES OR WHEN YOU REQUEST.":PRINT
9460 PRINT "IF YOU WISH FOR THE COMPUTER TO PLAY,"
9470 PRINT "ENTER A NAME BEGINNING WITH 'APPLE'"
9480 PRINT "FOR ONE OR MORE PLAYER'S NAMES."
9490 RETURN
65535 REM  *** COPYRIGHT 1978 BY GARY A. FOOTE ***
