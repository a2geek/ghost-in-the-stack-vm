0 REM SUB VIEW BY DON GRAHAM  COPYRIGHT (C) 1978 PROGRAMMA INT'L
10 REM *** SUB VIEW ***
20 REM COPYRIGHT 1978, DON GRAHAM
30 GOSUB 1420
40 GOSUB 1240:FOR P=1 TO 3000:NEXT P
50 GOSUB 1550
60 GR :CALL -936:GOSUB 790:COLOR= 0:I=6:X=7
70 PRINT 
80 GOSUB 280:PRINT " THESE ARE THE LOCAL SHIPS IN THE AREA":CALL -198:PRINT 
90 INPUT "  PRESS RETURN FOR INTELLIGENCE REPORT",B$
100 A=0:B=0:C=0:D=0:E=0:F=0:G=0:H=0:AA=0:BB=0:CC=0:DD=0:EE=0:FF=0:GG=0:HH=0:N=0
110 CALL -936:GOSUB 790:I=6:X=7:GOSUB 370
120 PRINT " INTELLIGENCE REPORTS THAT THE ENEMY IS":PRINT "  MASQUERADING AS THESE LOCAL SHIPS":CALL -198
130 PRINT :INPUT "   PRESS RETURN TO GO ON WATCH",B$
140 QQ= RND (5)+8:SE=0:XE=0:SF=0
150 N=N+1:IF N=QQ THEN 1020: POKE -16368,0
160 TEXT :CALL -936:VTAB 10:PRINT "CAPTAIN...THERE IS A SHIP IN THE AREA..":PRINT :PRINT :PRINT "   PRESS RETURN TO BRING UP PERISCOPE"
165 GOSUB 1540
170 PRINT :INPUT "    PRESS ANY KEY TO FIRE A TORPEDO",B$
180 X=0:GR :GOSUB 640
190 YY=20* RND (8):COLOR= 0:X=15:I=18
200 GOSUB 480+YY
210 FOR PP=1 TO  RND (250)+100
220 GOTO 800
230 NEXT PP
240 PRINT " DESTROYER IN AREA!!..DIVE!";:GOSUB 1510:PRINT "  DIVE!        ":GOSUB 1510
250 GOTO 1140:
260 N=N+1:IF N=QQ THEN 1020
270  POKE -16368,0:GOTO 150
280 ZZ=10* RND (8)+290:IF I>=37 AND X=25 THEN RETURN :IF I>=37 THEN X=25:IF I>=37 THEN I=6:GOTO ZZ
290 IF A=1 THEN 280:GOSUB 480:I=I+10:A=1:GOTO 280
300 IF B=1 THEN 280:GOSUB 500:I=I+10:B=1:GOTO 280
310 IF C=1 THEN 280:GOSUB 520:I=I+10:C=1:GOTO 280
320 IF D=1 THEN 280:GOSUB 540:I=I+10:D=1:GOTO 280
330 IF E=1 THEN 280:GOSUB 560:I=I+10:E=1:GOTO 280
340 IF F=1 THEN 280:GOSUB 580:I=I+10:F=1:GOTO 280
350 IF G=1 THEN 280:GOSUB 600:I=I+10:G=1:GOTO 280
360 IF H=1 THEN 280:GOSUB 620:I=I+10:H=1:GOTO 280
370 ZZ=10* RND (8)+390:IF I>=37 AND X=25 THEN RETURN :IF I>=37 THEN X=25:IF I>=37 THEN I=6
380 TT= RND (2)*7:COLOR= TT:GOTO ZZ
390 IF A=1 THEN 370:GOSUB 480:I=I+10:A=1:IF TT=7 THEN AA=1:GOTO 370
400 IF B=1 THEN 370:GOSUB 500:I=I+10:B=1:IF TT=7 THEN BB=1:GOTO 370
410 IF C=1 THEN 370:GOSUB 520:I=I+10:C=1:IF TT=7 THEN CC=1:GOTO 370
420 IF D=1 THEN 370:GOSUB 540:I=I+10:D=1:IF TT=7 THEN DD=1:GOTO 370
430 IF E=1 THEN 370:GOSUB 560:I=I+10:E=1:IF TT=7 THEN EE=1:GOTO 370
440 IF F=1 THEN 370:GOSUB 580:I=I+10:F=1:IF TT=7 THEN FF=1:GOTO 370
450 IF G=1 THEN 370:GOSUB 600:I=I+10:G=1:IF TT=7 THEN GG=1:GOTO 370
460 IF H=1 THEN 370:GOSUB 620:I=I+10:H=1:IF TT=7 THEN HH=1:GOTO 370
480 HLIN 1+X,9+X AT I:HLIN 0+X,9+X AT I-1:HLIN 3+X,7+X AT I-2:PLOT 4+X,I-3:PLOT 6+X,I-3
490 RETURN 
500 HLIN 0+X,7+X AT I:HLIN 3+X,7+X AT I-1:HLIN 3+X,6+X AT I-2:PLOT 5+X,I-3
510 RETURN 
520 HLIN 0+X,9+X AT I:PLOT 5+X,I-1
530 RETURN 
540 HLIN 2+X,6+X AT I:HLIN 4+X,5+X AT I-1:PLOT 5+X,I-2
550 RETURN 
560 HLIN 1+X,7+X AT I:HLIN 0+X,8+X AT I-1:HLIN 4+X,5+X AT I-2
570 RETURN 
580 HLIN 0+X,7+X AT I:HLIN 0+X,1+X AT I-1:
590 RETURN 
600 HLIN 0+X,8+X AT I:HLIN 0+X,8+X AT I-1:HLIN 1+X,7+X AT I-2:HLIN 2+X,6+X AT I-3:PLOT 3+X,I-4:PLOT 5+X,I-4
610 RETURN 
620 HLIN 0+X,7+X AT I:HLIN 1+X,2+X AT I-1:
630 RETURN 
640 COLOR= 7:FOR I=0 TO 18:HLIN 0,39 AT I:NEXT I
650 COLOR= 6:FOR I=19 TO 39:HLIN 0,39 AT I:NEXT I
660 COLOR= 0:FOR I=0 TO 1:HLIN 0,39 AT I:NEXT I
670 FOR I=38 TO 39:HLIN 0,39 AT I:NEXT I
680 FOR I=0 TO 3:VLIN 0,39 AT I:NEXT I
690 FOR I=35 TO 39:VLIN 0,39 AT I:NEXT I
700 FOR I=2 TO 5:HLIN 4,15-X AT I:HLIN 24+X,34 AT I:X=X+3:NEXT I:X=0
710 FOR I=4 TO 7:VLIN 0,10-X AT I:VLIN 29+X,39 AT I:X=X+2:NEXT I:X=0
720 FOR I=35 TO 39:HLIN 4,9+X AT I:HLIN 30-X,34 AT I:X=X+3:NEXT I:X=0
730 FOR I=31 TO 35:VLIN 0,3+X AT I:VLIN 34-X,39 AT I:X=X+2:NEXT I:X=0
740 VLIN 0,5 AT 19:VLIN 34,39 AT 19
750 FOR I=18 TO 19:HLIN 0,8 AT I:HLIN 31,39 AT I:NEXT I
760 VLIN 0,6 AT 11:VLIN 31,39 AT 11:VLIN 0,6 AT 28:VLIN 31,39 AT 28
770 VLIN 0,5 AT 20:VLIN 34,39 AT 20
780 RETURN 
790 COLOR= 7:FOR I=0 TO 39:HLIN 0,39 AT I:NEXT I:COLOR= 9:HLIN 0,39 AT 0:HLIN 0,39 AT 39:VLIN 0,39 AT 0:VLIN 0,39 AT 39:RETURN 
800 IF  PEEK (-16384)>127 THEN 810:GOTO 230
810 RR=1: POKE -16368,0:MM=10:GOSUB 1500
820 COLOR= 2:VLIN 33-RR,34-RR AT 19:COLOR= 7:PLOT 18,33-RR:PLOT 20,33-RR:
830 PLOT 17,34-RR:PLOT 21,34-RR
840 FOR P=1 TO MM:NEXT P
850 COLOR= 6:VLIN 33-RR,34-RR AT 19:PLOT 18,33-RR:PLOT 20,33-RR:PLOT 17,34-RR:PLOT 21,34-RR
860 RR=RR+1:IF RR>=15 THEN 870:IF RR=10 THEN MM=100:GOTO 820
870 FOR P=1 TO 100:NEXT P
880 COLOR= 13:HLIN 18,20 AT 18:HLIN 18,20 AT 17:
890 FOR P=1 TO 100:NEXT P
900 COLOR= 9:VLIN  RND (4)+15,18 AT 19:VLIN  RND (4)+15,18 AT 18:VLIN  RND (4)+15,18 AT 20
910 FOR P=1 TO 100:NEXT P
920 COLOR= 15:FOR P=1 TO 10:HLIN 17+ RND (4),22+ RND (4) AT 18-P:FOR J=1 TO 50:NEXT J:NEXT P
930 COLOR= 0
940 IF YY=0 AND AA=1 THEN 1670
950 IF YY=20 AND BB=1 THEN 1670
960 IF YY=40 AND CC=1 THEN 1670
970 IF YY=60 AND DD=1 THEN 1670
980 IF YY=80 AND EE=1 THEN 1670:IF YY=100 AND FF=1 THEN 1670:IF YY=120 AND GG=1 THEN 1670:IF YY=140 AND HH=1 THEN 1670
990  POKE -16368,0:PRINT " ENEMY DESTROYED CAPTAIN! ";:GOSUB 1510:PRINT "DIVE!":GOSUB 1510:SE=SE+1
1000 FOR I=39 TO 0 STEP -1:FOR P=1 TO 100:NEXT P:HLIN 0,39 AT I:NEXT I
1010 GOTO 150
1020 TEXT :CALL -936
1030 VTAB 2:TAB 4:PRINT "<CTRL-G>COM <CTRL-G>FLEET <CTRL-G>CAPTAIN <CTRL-G>EVALUATION <CTRL-G>REPORT"
1040 PRINT :PRINT :PRINT :CALL -198
1050 TAB 10:PRINT "<CTRL-G>SHIPS <CTRL-G>SIGHTED.<CTRL-G><CTRL-G>....<CTRL-G>.";:PRINT QQ-1
1055 FOR P=1 TO 100:NEXT P
1060 PRINT :TAB 10:PRINT "<CTRL-G>SHIPS <CTRL-G>SUNK"
1065 CALL -198
1070 PRINT :TAB 14:PRINT "<CTRL-G>ENEMY<CTRL-G>...<CTRL-G>.<CTRL-G>..<CTRL-G>..<CTRL-G>..";:PRINT SE
1075 CALL -198
1076 FOR P=1 TO 100:NEXT P
1080 PRINT :TAB 14:PRINT "<CTRL-G>FRIENDLY.<CTRL-G>..<CTRL-G>..<CTRL-G>..";:PRINT SF
1085 CALL -198
1086 FOR P=1 TO 100:NEXT P
1090 PRINT :PRINT :TAB 10:PRINT "<CTRL-G>SHIPS <CTRL-G>ESCAPED"
1095 CALL -198
1096 FOR P=1 TO 100:NEXT P
1100 PRINT :TAB 14:PRINT "<CTRL-G>ENEMY.<CTRL-G>..<CTRL-G>.<CTRL-G>..<CTRL-G>..<CTRL-G>..";:PRINT XE
1105 CALL -198
1106 FOR P=1 TO 100:NEXT P
1110 PRINT :PRINT :PRINT 
1120 TAB 8:PRINT "<CTRL-G>CAPT<CTRL-G>AIN'<CTRL-G>S EF<CTRL-G>FICE<CTRL-G>NCY<CTRL-G>...";:PRINT (SE-XE-SF+1)*100/(1+SE+XE);:PRINT "%"
1125 CALL -198
1130 TAB 10:PRINT "...ANOTHER COMMAND..."
1132 INPUT "                (Y/N)?",G$
1134 GR :IF G$="Y" THEN 100:END 
1140 IF YY=0 AND AA=0 THEN 1640
1150 IF YY=20 AND BB=0 THEN 1640
1160 IF YY=40 AND CC=0 THEN 1640
1170 IF YY=60 AND DD=0 THEN 1640
1180 IF YY=80 AND EE=0 THEN 1640
1190 IF YY=100 AND FF=0 THEN 1640
1200 IF YY=120 AND GG=0 THEN 1640
1210 IF YY=140 AND HH=0 THEN 1640
1220 FOR I=39 TO 0 STEP -1:FOR P=1 TO 100:NEXT P:HLIN 0,39 AT I:NEXT I
1230 GOTO 150
1240 TEXT :CALL -936
1250 FOR I=0 TO 9
1260 CALL -936
1270 VTAB 5+I:TAB 6+I:PRINT "S":GOSUB 1480
1280 VTAB 23-I:TAB 7+I:PRINT "U"
1290 VTAB 5+I:TAB 17:PRINT "B"
1310 VTAB 5+I:TAB 19:PRINT "-"
1320 VTAB 23-I:TAB 21:PRINT "V"
1330 VTAB 5+I:TAB 22:PRINT "I"
1340 VTAB 23-I:TAB 32-I:PRINT "E"
1350 VTAB 5+I:TAB 33-I:PRINT "W"
1360 GOSUB 1480
1370 NEXT I
1380 VTAB 14:TAB 11:PRINT "***":VTAB 14:TAB 26:PRINT "***"
1390 VTAB 15:TAB 19:PRINT "BY"
1400 VTAB 16:TAB 13:PRINT "  DON GRAHAM"
1410 RETURN 
1420  REM _
      Modification: Sound routine was in ZP @ $02. _
      Impacted interpreter and had trouble moving the interpreter where Applesoft _
      did not impact. So moving the routine to $300. _
         0303-   AD 30 C0    LDA   $C030 _
         0306-   AD 00 03    LDA   $0300 _
         0309-   20 A8 FC    JSR   $FCA8 _
         030C-   AD 01 03    LDA   $0301 _
         030F-   D0 05       BNE   $0316 _
         0311-   CE 02 03    DEC   $0302 _
         0314-   F0 06       BEQ   $031C _
         0316-   CE 01 03    DEC   $0301 _
         0319-   4C 03 03    JMP   $0303 _
         031C-   60          RTS
1430  POKE 0x303,0xad: POKE 0x304,0x30: POKE 0x305,0xc0: _
      POKE 0x306,0xad: POKE 0x307,0x00: POKE 0x308,0x03: _
      POKE 0x309,0x20: POKE 0x30a,0xa8: POKE 0x30b,0xfc: _
      POKE 0x30c,0xad: POKE 0x30d,0x01: POKE 0x30e,0x03
1440  POKE 0x30f,0xd0: POKE 0x310,0x05: _
      POKE 0x311,0xce: POKE 0x312,0x02: POKE 0x313,0x03: _
      POKE 0x314,0xf0: POKE 0x315,0x06: _
      POKE 0x316,0xce: POKE 0x317,0x01: POKE 0x318,0x03: _
      POKE 0x319,0x4c: POKE 0x31a,0x03: POKE 0x31b,0x03: _
      POKE 0x31c,0x60
1450 REM OLD LINE NOT NEEDED
1460 RETURN 
1470 FOR P=1 TO 30: POKE 0x300,25: POKE 0x302,1: POKE 0x301,50:CALL 0x303:NEXT P:RETURN
1480 FOR P=1 TO 5: POKE 0x300,2: POKE 0x302,2: POKE 0x301,1:CALL 0x303:NEXT P:RETURN
1490 FOR J=1 TO 15:FOR K=40 TO 2 STEP -4: POKE 0x300,K: POKE 0x302,1: POKE 0x301,10:CALL 0x303:NEXT K:NEXT J:RETURN
1500 FOR T=10 TO 1 STEP -1: POKE 0x300,40: POKE 0x302,1: POKE 0x301,T:CALL 0x303:NEXT T:RETURN
1510 FOR P=1 TO 3:FOR K=1 TO 40: POKE 0x300,K: POKE 0x302,1: POKE 0x301,1:CALL 0x303:NEXT K:NEXT P:RETURN
1520 FOR P=1 TO 3:FOR K=1 TO 10: POKE 0x300,K: POKE 0x302,1: POKE 0x301,100:CALL 0x303:NEXT K:NEXT P:RETURN
1530 FOR P=1 TO 30: POKE 0x300, RND (5)+3: POKE 0x302, RND (6)+1: POKE 0x301, RND (5)+1:CALL 0x303:NEXT P:RETURN
1540 FOR P=1 TO 10: POKE 0x300,5: POKE 0x302,1: POKE 0x301,100:CALL 0x303:NEXT P:RETURN
1550 CALL -936:TAB 10:PRINT "<CTRL-G>MESS<CTRL-G>AG<CTRL-G>E FROM<CTRL-G><CTRL-F> COM F<CTRL-G>LEE<CTRL-G>T"
1560 PRINT :PRINT "YOU ARE ASSIGNED CAPTAIN OF SUBMARINE":PRINT "BLUEGILL THIS DATE":CALL -198
1565 FOR P=1 TO 500:NEXT P
1570 PRINT :PRINT "PROCEED TO SECTOR ZEBRA DELTA":CALL -198
1575 FOR P=1 TO 1500:NEXT P
1580 PRINT :PRINT "INTELLIGENCE HAS INFORMED US THAT THE":PRINT "ENEMY IS MASQUERADING AS LOCAL SHIPPING":CALL -198
1585 FOR P=1 TO 1500:NEXT P
1590 PRINT :PRINT "UPON ARRIVAL AT ZEBRA DELTA INTELLIGENCEREPORTS WILL BE AVAILABLE":CALL -198
1595 FOR P=1 TO 1500:NEXT P
1600 PRINT :PRINT "INTELLIGENCE ALSO INFORMS US THAT ZEBRA DELTA IS SATURATED WITH ENEMY DESTROYERS":CALL -198
1605 FOR P=1 TO 1500:NEXT P
1610 PRINT :PRINT "KEEP PERISCOPE DEPTH TO A MINIMUM.":CALL -198
1615 FOR P=1 TO 1500:NEXT P
1620 PRINT :PRINT "<CTRL-G>PRESS <CTRL-G>RETURN <CTRL-G>FOR <CTRL-G>ACCEPTANCE <CTRL-G>OF <CTRL-G><CTRL-F>THIS":INPUT "<CTRL-G>COMMAND",B$
1630 RETURN 
1640 PRINT "   CAPTAIN THAT WAS AN ENEMY SHIP!!":XE=XE+1
1650 FOR I=39 TO 0 STEP -1:FOR P=1 TO 100:NEXT P:HLIN 0,39 AT I:NEXT I
1660 GOTO 150
1670 PRINT "CAPTAIN THAT WAS A FRIENDLY LOCAL SHIP!":GOSUB 1510:TAB 14:PRINT " DIVE!":GOSUB 1510:SF=SF+1
1680  POKE -16368,0:
1690 FOR I=39 TO 0 STEP -1:FOR P=1 TO 25:NEXT P:HLIN 0,39 AT I:NEXT I
1700 GOTO 150
