10 DIM C(4)
15 REM _
    Moved sound routine from ZP $02 to $303: _
        0303-   AD 30 C0   LDA $C030 _
        0306-   AD 00 03   LDA $0300 _
        0309-   20 A8 FC   JSR $FCA8 _
        030C-   AD 01 03   LDA $0301 _
        030F-   D0 05      BNE $0316 _
        0311-   CE 02 03   DEC $0302 _
        0314-   F0 06      BEQ $031C _
        0316-   CE 01 03   DEC $0301 _
        0319-   4C 03 03   JMP $0303 _
        031C-   60         RTS
20  POKE 0x303,0xad: POKE 0x304,0x30: POKE 0x305,0xC0: _
    POKE 0x306,0xad: POKE 0x307,0x00: POKE 0x308,0x03: _
    POKE 0x309,0x20: POKE 0x30a,0xa8: POKE 0x30b,0xfc: _
    POKE 0x30c,0xad: POKE 0x30d,0x01: POKE 0x30e,0x03: _
    POKE 0x30f,0xd0: POKE 0x310,0x05: _
    POKE 0x311,0xce: POKE 0x312,0x02: POKE 0x313,0x03: _
    POKE 0x314,0xf0: POKE 0x315,0x06: _
    POKE 0x316,0xce: POKE 0x317,0x01: POKE 0x318,0x03: _
    POKE 0x319,0x4c: POKE 0x31a,0x03: POKE 0x31b,0x03: _
    POKE 0x31c,0x60
30 TEXT :CALL -936:VTAB 2:TAB 4:PRINT "48K APPLE II PLUS COLOR DEMO"
35 VTAB 6
36 TAB 5:PRINT "1  LINES"
37 TAB 5:PRINT "2  CROSS"
38 TAB 5:PRINT "3  WEAVING"
40 TAB 5:PRINT "4  TUNNEL"
42 TAB 5:PRINT "5  CIRCLE"
44 TAB 5:PRINT "6  SPIRAL"
46 TAB 5:PRINT "7  TONE"
48 TAB 5:PRINT "8  SPRING"
50 TAB 5:PRINT "9  HYPERBOLA"
52 TAB 5:PRINT "10 COLOR BARS"
60 PRINT :TAB 10:CALL -384:PRINT "HIT ANY KEY NEW DEMO":CALL -380
62 Z=0:PRINT :INPUT "WHICH DEMO DO YOU WANT",I:GR :IF I>0 AND I<20 THEN GOTO 100*I:GOTO 30
100 I=1+I MOD 79:J=I+(I>39)*(79-I-I):GOSUB 2000:GOSUB 10000:GOTO 100
200 I=1+I MOD 39:J=I:GOSUB 2000:J=39-I:GOSUB 2000:GOSUB 10000:GOTO 200
300 J=J+I:J=J MOD 22+1:FOR I=1 TO 1529:COLOR= I MOD J+7:PLOT (2*I) MOD 37,(3*I) MOD 35:GOSUB 10000:NEXT I:GOTO 300
400 FOR I=1 TO 4:C(I)= RND (16):NEXT I
410 FOR I=3 TO 1 STEP -1:C(I+1)=C(I):NEXT I:C(1)= RND (16):FOR I=1 TO 5:FOR J=1 TO 4
420 COLOR= C(J):L=J*5+14+I:K=39-L:HLIN K,L AT K:VLIN K,L AT L:HLIN K,L AT L:VLIN K,L AT K:NEXT J,I:GOSUB 10000:GOTO 410
500 Z=20:GOTO 900
600 LL= RND (50)+1
605 COLOR=  RND (16):FOR I=0 TO 18 STEP 2:J=39-I:HLIN I,J AT I:GOSUB 640:VLIN I,J AT J:GOSUB 640
610 HLIN I+2,J AT J:GOSUB 640:VLIN I+2,J AT I+2:GOSUB 640:NEXT I
620 COLOR=  RND (16):FOR I=18 TO 0 STEP -2:J=39-I:VLIN I+2,J AT I+2:GOSUB 640:HLIN I+2,J AT J:GOSUB 640
630 VLIN I,J AT J:GOSUB 640:HLIN I,J AT I:GOSUB 640:NEXT I:GOSUB 10000:GOTO 600
640 FOR K=1 TO LL:NEXT K:RETURN
650 K=I+7:L=K+K*5+K*26+70:L=32767/L*(PDL (0)/10): POKE 0x300,K: POKE 0x301,L MOD 256: POKE 0x302,L/256+1:CALL 0x303:RETURN
700 I= RND (30)+3:J=I*I*5+I*26+70:K=32767/J*(PDL (0)/10): POKE 0x300,I: POKE 0x301,K MOD 256
710  POKE 0x302,(K>255)+1:CALL 0x303:GOSUB 10000:GOTO 700
800 X=3:A=1000:P=A:L=20:W=4:Y=0:J=1:COLOR= 6:HLIN 0,39 AT 4:COLOR= 9:GOSUB 880:COLOR= 12:VLIN 5,M-2 AT X
810 N=2*A-P-A/W:COLOR= 0:GOSUB 880:VLIN 5,39 AT X:X=X+1:IF X<39 THEN 820:X=3:VLIN 5,39 AT 1:VLIN 5,39 AT 2
820 P=A:A=N:Y=A/100:COLOR= 12:GOSUB 880:COLOR= 9:VLIN 5,M-2 AT X:COLOR= 15:PLOT X-2,M:FOR I=0 TO J:NEXT I:GOSUB 10000:GOTO 810
880 M=L-Y:L1=M-1:L2=M+1:VLIN L1,L2 AT X-1:VLIN L1,L2 AT X:VLIN L1,L2 AT X+1:GOSUB 10000:RETURN
900 I=1+I MOD 15:FOR Y=0 TO 39:FOR X=0 TO 39:COLOR= I+(ABS (20-X)-Z)*(ABS (20-Y)-Z)/25
910 PLOT X,Y:GOSUB 10000:NEXT X,Y:GOTO 900
1000 CALL -936
2000 COLOR=  RND (16):HLIN 0,39 AT J:COLOR=  RND (16):VLIN 0,39 AT J:RETURN
10000 IF  PEEK (-16384)<128 THEN RETURN : POKE -16368,0:POP :GOTO 30
