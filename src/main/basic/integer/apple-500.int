10 GOTO 1000
20 PLOT X+1,Y:PLOT X+4,Y:HLIN X,X+4 AT Y+1:HLIN X,X+4 AT Y+2:HLIN X,X+4 AT Y+3:PLOT X+1,Y+4:PLOT X+4,Y+4
25 OT= PEEK (-16336)- PEEK (-16336)
30 RETURN 
100 COLOR= 0:X=18:Y=P:GOSUB 20
110 P=33-PDL (0)*10/77+1:COLOR= 6:Y=P:GOSUB 20
200 FOR I=1 TO 3
202 IF Z(I)#0 THEN 250
204 IF  RND (5)#0 THEN 250
206 IF SP>29 THEN 208:IF SP<21 THEN 210:GOTO 250
208 Z(I)=1:X(I)=1:Y(I)= RND (31)+2:GOTO 250
210 Z(I)=1:X(I)=34:Y(I)= RND (31)+2
250 NEXT I
260 FOR I=1 TO 3
262 IF Z(I)=0 THEN 290
264 IF SP>29 THEN 266:IF SP<21 THEN 268:GOTO 290
266 IF X(I)<30 THEN 290:X=X(I):Y=Y(I):COLOR= 0:GOSUB 20:Z(I)=0
268 IF X(I)>4 THEN 290:X=X(I):Y=Y(I):COLOR= 0:GOSUB 20:Z(I)=0
290 NEXT I
300 FOR I=1 TO 3
302 IF Z(I)=0 THEN 310
304 X=X(I):Y=Y(I):COLOR= 0:GOSUB 20
306 X(I)=X(I)+(SP-25)/5
308 X=X(I):Y=Y(I):COLOR= 13:GOSUB 20
310 NEXT I
400 IF  SCRN(18,P)=13 THEN 420:IF  SCRN(18,P+4)=13 THEN 420:IF  SCRN(22,P)=13 THEN 420:IF  SCRN(22,P+4)=13 THEN 420:IF  SCRN(20,P+2)=13 THEN 420
402 IF P=1 THEN 420:IF P=34 THEN 420
410 GOTO 500
420 COLOR= 1
430 HLIN 19,21 AT P+1:HLIN 18,22 AT P+2:HLIN 19,21 AT P+3:PLOT 18,P:PLOT 20,P:PLOT 22,P:PLOT 18,P+4:PLOT 20,P+4:PLOT 22,P+4
440 FOR I=1 TO 10:CALL -1051:NEXT I
450 COLOR= 0
460 HLIN 18,22 AT P:HLIN 18,22 AT P+1:HLIN 18,22 AT P+2:HLIN 18,22 AT P+3:HLIN 18,22 AT P+4
470 T=T+160:SP=0
500 IF  PEEK (-16287)>127 THEN 510:IF SP>0 THEN SP=SP-1:GOTO 600
510 IF SP<50 THEN SP=SP+1
600 S=S+SP:T=T+28
620 VTAB 22:PRINT H;:TAB 11:PRINT S;:TAB 21:PRINT T/100;:TAB 31:PRINT SP;" "
630 IF T>=6000 THEN 700
690 GOTO 100
700 CALL -936
710 PRINT "***** GAME OVER *****":CALL -1051
720 IF H<S THEN GOTO 730:PRINT "YOUR SCORE IS ";S;" METERS":GOTO 740
730 PRINT "YOUR HI-SCORE IS ";S;" METERS":H=S
740 INPUT "DO YOU WANT TO PLAY AGAIN ? (Y/N)",A$
750 IF A$="Y" THEN 760:TEXT :CALL -936:END 
760 SP=0:T=0:S=0:CALL -936:GOTO 1016
1000 DIM X(3),Y(3),Z(3):SP=0:P=1:Z(1)=0:Z(2)=0:Z(3)=0
1002 TEXT :CALL -936
1004 VTAB 5:TAB 10:PRINT "** APPLE 500 **":VTAB 7:TAB 10:PRINT "CAR RACE GAME !!"
1006 VTAB 10:PRINT " YOUR TIME LIMIT IS '60' SECONDS"
1008 PRINT 
1012 VTAB 18:INPUT "PLEASE HIT 'RETURN'",A$
1014 GR :COLOR= 4:HLIN 0,39 AT 0:HLIN 0,39 AT 39
1016 CALL -936:PRINT "HI-SCORE  SCORE     TIME     SPEED"
1018 GOTO 100
1100 REM  **********************
1110 REM  **  APPLE     500   **
1120 REM  **********************
