5 K=-16384:ZZ=-16336:KX=-16368
10 TEXT :CALL -936:VTAB 2:TAB 11
15 PRINT "* STAR SHIP ATTACK *"
16 TAB 11:PRINT "--------------------":PRINT :PRINT :TAB 6
17 REM  PROGRAM BY MARC SCHWARTZ
18 REM  IN APRIL 79 KILOBAUD
20 PRINT "YOUR SHIP IS ON THE LAUNCH PAD":PRINT :TAB 7
25 PRINT "THE STAR SHIP IS APPROACHING":PRINT :TAB 15
30 PRINT "YOUR MISSION:":PRINT :TAB 2
35 PRINT "CAPTURE OR DESTROY THE BLACK STAR SHIP":TAB 3
40 PRINT "BEFORE IT DESTROYS THE FOOD STATIONS":TAB 14
45 PRINT ".......OR  YOU!":PRINT :PRINT :TAB 7
50 T= RND (4)+4
55 PRINT "YOU HAVE ";T;" PHASOR TORPEDOES":PRINT : POKE KX,0
60 INPUT "SPEED?(FROM 1(SLOW) TO 300(FAST))  ",S:Q=300-S
65 PRINT :TAB 4
70 PRINT "DO YOU WANT INSTRUCTIONS (Y/N)?"
75 IF  PEEK (K)=206 THEN 80:IF  PEEK (K)=217 THEN 2100:GOTO 75
80 CALL -936:TP=0:GR
85 COLOR= 6:FOR N=0 TO 39:VLIN 0,39 AT N:NEXT N
90 COLOR= 14:M=8:FOR L=4 TO 28 STEP 8:PLOT L,M:NEXT L
95 COLOR= 13:HLIN 0,13 AT 39:HLIN 18,39 AT 39:HLIN 0,14 AT 38:HLIN 17,39 AT 38
100 X=15:Y=37:A=23:B=18:HIT=0
105 REM
110 ZX=0:ZY=0:F=0
115 X=X+(PDL (0)-128)/75:Y=Y+(PDL (1)-128)/75
120 C=2* RND (2)-1:D=2* RND (2)-1:A=A+C:B=B+D
125 IF A<1 OR A>38 OR B<1 THEN 700
130 IF B>38 THEN B=38
135 IF X<1 OR X>38 OR Y<1 THEN 900
140 IF Y>37 THEN Y=37
145 COLOR= 1:HLIN X,X+1 AT Y
150 COLOR= 0:HLIN A-1,A+1 AT B:VLIN B-1,B+1 AT A
155 IF Y=B AND X=A OR Y=B AND X+1=A THEN 800
160 REM  TORPEDO
165  POKE -16296,0: POKE -16294,0
170 IF  PEEK (-16286)<128 THEN 180
175 XY=Y:ZX=1:PZ=PDL (1):GOTO 190
180 IF  PEEK (-16287)<128 THEN 240
185 XY=X:ZY=1:PZ=PDL (0)
190 IF TP<T THEN 200
195 CALL -936:PRINT "NO TORPEDOES LEFT":GOTO 240
200 TP=TP+1:TT=2*(PZ/128)-1
205 FOR ST=XY+TT TO 39*(PZ/128) STEP TT:COLOR= 1
210 IF ZX THEN IF X=A AND ST=B THEN 1000:IF ZY THEN IF ST=A AND Y=B THEN 1000
215 IF ZX THEN PLOT X,ST:IF ZY THEN PLOT ST,Y
220 M= PEEK (ZZ)
225 REM
230 COLOR= 6:IF ZX THEN PLOT X,ST:IF ZY THEN PLOT ST,Y
235 NEXT ST
240 M=8:FOR L=4 TO 28 STEP 8
245 IF  SCRN(L,M)#14 THEN 255
250 F=F+1
255 NEXT L:IF F=0 THEN 1500
260 FOR N=1 TO Q:NEXT N
265 COLOR= 6:HLIN A-1,A+1 AT B:VLIN B-1,B+1 AT A:HLIN X,X+1 AT Y
270 REM  LASER RAY:LA=0
275 Z= RND (ABS (X-A)+ABS (Y-B))/3+3
280 IF Z#3 THEN 110:COLOR= 15:HLIN 0,39 AT B:VLIN 0,39 AT A
285 FOR N=1 TO 10:M= PEEK (ZZ)- PEEK (ZZ):NEXT N
290 IF  SCRN(X,Y)=15 OR  SCRN(X+1,Y)=15 THEN LA=1
295 COLOR= 6:HLIN 0,39 AT B:VLIN 0,39 AT A
300 IF LA=1 THEN GOSUB 1100
305 GOTO 110
700 CALL -936:PRINT "ENEMY DRIVEN OFF.  GOOD WORK, CAPTAIN!":COLOR= 13:HLIN X,X+1 AT Y-1:GOTO 2000
800 CALL -936:PRINT "ENEMY CAPTURED. EXCELLENT!":GOTO 2000
900 CALL -936:PRINT "STRAYED INTO ENEMY AIRSPACE.":PRINT "SHIP LOST."
910 COLOR= 0:HLIN A-1,A+1 AT B:VLIN B-1,B+1 AT A:GOTO 2000
1000 CALL -936:PRINT "STAR SHIP HIT!! CONGRATULATIONS!":GOTO 2000
1100 CALL -936:FOR N=1 TO 10:M= PEEK (ZZ):NEXT N
1105 IF LA=1 THEN HIT=HIT+1:LA=0:IF HIT=2 THEN 1115
1110 PRINT "DEFLECTOR SHIELD DAMAGED BY LASER RAY":RETURN
1115 CALL -936:PRINT "SHIP DESTROYED BY LASER RAY!":COLOR= 0:HLIN A-1,A+1 AT B:VLIN B-1,B+1 AT A:GOTO 2000
1500 CALL -936:PRINT "ALL FOOD STATIONS DESTROYED.":PRINT "COLONY NO LONGER VIABLE.":COLOR= 0:HLIN A-1,A+1 AT B:VLIN B-1,B+1 AT A:GOTO 2000
2000 FOR N=1 TO 1500:NEXT N:GOTO 10
2100 CALL -936
2105 PRINT "ONE PADDLE MOVES YOUR SHIP FROM SIDE TO SIDE.  THE OTHER MOVES IT UP AND DOWN.":PRINT
2110 PRINT "PUSHING THE RESET BUTTON ON A PADDLE":PRINT "FIRES A PHASOR TORPEDO IN THE":PRINT "DIRECTION THE PADDLE IS POINTING."
2115 PRINT :PRINT "ONLY A DIRECT HIT ON THE CENTRAL CONTROLSECTION OF THE STAR SHIP DESTROYS IT.":PRINT
2120 PRINT "YOU MAY CAPTURE THE STAR SHIP BY GETTINGCLOSE ENOUGH TO THE CONTROL SECTION TO":PRINT "BOARD IT.":PRINT
2122 PRINT "BEWARE OF THE WHITE LASER RAYS FROM THE STAR SHIP. IF YOU ARE HIT TWICE BY THEM,";
2123 PRINT "YOUR DEFLECTOR SHIELD WILL BE DESTROYED."
2124 PRINT "CAUTION: THE CLOSER YOU GET TO THE STAR SHIP, THE MORE RAPIDLY IT FIRES!":PRINT
2125 TAB 8:PRINT "WHEN YOU ARE READY TO BEGIN,"
2127  POKE -16368,0
2129 REM *** FIXME: WAS Z$! ***
2130 TAB 8:PRINT "PRESS THE 'RETURN' KEY.":TAB 16:INPUT Z
2140 GOTO 80
