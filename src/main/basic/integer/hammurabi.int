10 REM    ***    HAMMURABI   ***
20 REM 
25 TEXT :CALL -936
27 VTAB 6:TAB 12:PRINT "H A M M U R A B I":VTAB 10
28 PRINT "COPYRIGHT (C) 1978, APPLE COMPUTER, INC."
29 PRINT :TAB 6
30 PRINT "TRY YOUR HAND AT GOVERNING"
31 PRINT :TAB 6
32 PRINT "ANCIENT SUMERIA SUCCESSFULLY"
34 PRINT :TAB 6:PRINT "FOR A 10 YEAR TERM OF OFFICE"
35 PRINT 
40 PRINT :DIM N$(80)
41 PRINT "20 BUSHELS OF GRAIN WILL FEED A PERSON":PRINT "FOR ONE YEAR.   HALF A BUSHEL PER ACRE"
42 PRINT "IS NEEDED FOR SEED GRAIN.":PRINT :PRINT "EACH CITIZEN CAN FARM AT MOST TEN ACRES.":FOR I=1 TO 2000:NEXT I
43 INPUT "PLEASE TYPE YOUR NAME: ",N$
44 TEXT :CALL -936: POKE 34,5
45 CALL -936:VTAB 1:PRINT "BUSHELS IN STORE =":PRINT "ACRES OF LAND = ":PRINT "PEOPLE ="
46 IF  LEN(N$)=0 THEN 43
47 VTAB 4:PRINT "*  *  *  *  *  *  *  *  *  *  *  *";
52 VTAB 6
53 L= LEN(N$)+ ASC(N$)
57 FOR I=1 TO L:X= RND (23):NEXT I
90 D1=0:P1=0
100 Z=0:P=95:S=2500:H=3000:E=H-8
101 E= RND (390)
110 Y=3:A=H/Y:I=5:Q=1
210 D=0
211 IF Z=0 THEN 220
215 PRINT :PRINT :PRINT "HAMMURABI, I BEG TO REPORT TO YOU,":Z=Z+1
217 PRINT "IN YEAR ";Z;", ";D;" PEOPLE STARVED, ";I;" PEOPLE CAME TO THE CITY."
218 P=P+I
219 GOSUB 775
220 GOSUB 759
227 IF Q>0 THEN 230
228 P=1+(P/2):GOSUB 759
229 PRINT :PRINT "A HORRIBLE PLAGUE STRUCK:":TAB 15:PRINT "HALF THE PEOPLE DIED.":PRINT 
230 PRINT "POPULATION IS NOW ";P:PRINT 
232 PRINT "THE CITY NOW OWNS ";A;" ACRES."
234 PRINT 
235 PRINT "YOU HARVESTED ";Y;" BUSHELS PER ACRE."
236 PRINT 
250 PRINT "RATS ATE ";E;" BUSHELS"
252 PRINT 
260 PRINT "YOU NOW HAVE ";S;" BUSHELS IN STORE"
265 PRINT 
270 IF Z>=11 THEN 860
310 C= RND (10):Y=C+17
312 PRINT "LAND IS TRADING AT ";Y;" BUSHELS PER ACRE."
313 PRINT :PRINT 
314 PRINT 
320 PRINT "HOW MANY ACRES DO YOU WISH TO BUY ";
321 INPUT Q:IF Q<0 THEN 850
322 IF Y*Q<=S THEN 330
323 GOSUB 710
324 GOTO 320
330 IF Q=0 THEN 340
331 A=A+Q:S=S-Y*Q:C=0
332 GOSUB 759
334 GOTO 400
340 PRINT "HOW MANY ACRES DO YOU WISH TO SELL ";
341 INPUT Q:IF Q<0 THEN 850
342 IF Q<A THEN 350
343 GOSUB 720
344 GOTO 340
350 A=A-Q:S=S+Y*Q:C=0
355 GOSUB 759
400 PRINT 
410 PRINT "HOW MANY BUSHELS DO YOU WISH TO FEED"
411 INPUT "YOUR PEOPLE ",Q
412 IF Q<0 THEN 850
418 REM   TRYING TO USE MORE GRAIN THAN YOU HAVE
420 IF Q>S THEN 421:GOSUB 759:GOTO 430
421 GOSUB 710
422 GOTO 410
430 S=S-Q:C=1:PRINT 
435 GOSUB 759
439 TAB 1
440 PRINT "HOW MANY ACRES DO YOU WISH TO PLANT":PRINT "WITH SEED? ";
441 INPUT D:IF D=0 THEN 511
442 IF D<0 THEN 850
444 REM    TRYING TO PLANT MORE ACRES THAN YOU OWN?
445 IF D<=A THEN 450
446 GOSUB 720
447 GOTO 440
449 REM    ENOUGH GRAIN FOR SEED?
450 IF (D/2)<=S THEN 455
452 GOSUB 710
453 GOTO 440
454 REM   ENOUGH PEOPLE TO TEND THE CROPS?
455 IF D<=10*P THEN 510
460 PRINT "BUT YOU ONLY HAVE ";P;" PEOPLE TO TEND THE FIELDS.  THINK AGAIN"
470 GOTO 440
510 S=S-(D/2)
511 GOSUB 800
512 REM    A BOUNTIFUL HARVEST
515 Y=C:H=D*Y:E=0
521 GOSUB 800
522 IF (C/2)*2<>C THEN 530
523 REM   THE RATS ARE RUNNING WILD
525 IF C=0 THEN C=1:E=(S/C)
530 S=S-E+H:GOSUB 759
531 GOSUB 800
532 IF P=0 THEN P=1
533 I=1+(C*((2*A+S/10)/10)/P)
540 C=Q/20
541 REM   HORRORS,A PLAGUE
542 Q= RND (100)/15
550 IF P<C THEN 210
552 D=P-C:IF D*20>9*P THEN 560
553 IF P=0 THEN P=1:IF Z=0 THEN Z=1:P1=D1*100/(P*Z)
555 P=C:D1=D1+D:GOTO 215
560 PRINT :PRINT "YOU STARVED ";D;" PEOPLE IN ONE YEAR!"
565 PRINT "DUE TO EXTREME MISMANAGEMENT YOU HAVE ":PRINT "NOT ONLY BEEN IMPEACHED "
566 PRINT "AND THROWN OUT OF OFFICE BUT YOU HAVE"
567 PRINT "ALSO BEEN DECLARED NATIONAL FINK."
569 GOTO 990
710 PRINT "HAMMURABI:  THINK AGAIN, YOU HAVE ONLY"
711 PRINT S;" BUSHELS OF GRAIN.  TRY AGAIN."
712 RETURN 
720 PRINT "HAMMURABI: THINK AGAIN."
721 PRINT "YOU ONLY HAVE ";A;" ACRES."
730 RETURN 
759 VTAB 1:TAB 20:PRINT "";S;"    "
761 VTAB 2:TAB 17:PRINT "";A;"   "
763 VTAB 3:TAB 10:PRINT "";P;"   "
765 VTAB 24:RETURN 
775 IF D=0 THEN RETURN 
777 REM 
778 REM 
779 RETURN 
800 C= RND (5)+1
801 RETURN 
850 PRINT :PRINT "HAMMURABI: I CANNOT DO WHAT YOU WISH."
855 PRINT "GET YOURSELF ANOTHER STEWARD!!!"
856 PRINT :PRINT "I QUIT!  GAME OVER!"
857 GOTO 990
860 PRINT "YEAR ";Z;"  - DO YOU WANT TO RETIRE?"
861 INPUT "TYPE G TO GO ON, OR S TO STOP. ",A$:IF A$="G" THEN 310
862 PRINT "IN YOUR ";Z;" YEAR TERM OF OFFICE, ";P1;" PERCENT OF THE "
863 PRINT "POPULATION STARVED PER YEAR BECAUSE OF  YOUR RULE."
864 IF P=0 THEN P=1
865 PRINT "A TOTAL OF ";D1;" PEOPLE DIED!!":L=A/P
870 PRINT "YOU STARTED WITH 10 ACRES PER PERSON AND ENDED WITH"
875 PRINT L;" ACRES PER PERSON.":PRINT 
880 IF P1>33 THEN 565
882 IF P>110 AND A>1100 AND S>2500 THEN 1020
885 IF L<7 THEN 1000
890 IF P1>10 THEN 940
892 IF L<10 AND D1>30 THEN 940
895 IF P1>3 THEN 960
896 IF L<10 THEN 960
900 PRINT "A FANTASTIC PERFORMANCE! !  CHARLEMANGE, DISRAELI, AND JEFFERSON COMBINED"
905 PRINT "COULD NOT HAVE DONE BETTER!":GOTO 990
940 PRINT "YOUR HEAVY-HANDED PERFORMANCE SMACKS OF "
941 PRINT "NERO AND IVAN THE TERRIBLE."
945 PRINT "THE PEOPLE (REMAINING) FIND YOU AN"
946 PRINT "UNPLEASANT RULER AND HATE YOUR GUTS!":GOTO 990
960 PRINT "YOUR PERFORMANCE COULD HAVE BEEN"
961 PRINT "SOMEWHAT BETTER, BUT REALLY WASN'T TOO"
962 PRINT "BAD AT ALL. ";(P*8*(1+ RND (12)))/1000;" PEOPLE"
963 PRINT "WOULD LIKE TO SEE YOU DEAD BUT WE ALL"
964 PRINT "HAVE OUR TRIVIAL LITTLE PROBLEMS."
990 PRINT "<CTRL-G><CTRL-G><CTRL-G><CTRL-G><CTRL-G><CTRL-G><CTRL-G><CTRL-G><CTRL-G>":PRINT "SO LONG FOR NOW."
999 END 
1000 PRINT "YOU LIVED OFF YOUR CAPITAL.":PRINT "YOU WERE SUPPOSED TO FARM FOR A LIVING,":PRINT "NOT LIVE OFF YOUR INHERITANCE."
1010 GOTO 990
1020 PRINT "YOU GREW A LOT.  YOU'RE AN EXCELLENT":PRINT "KING BUT YOUR NEIGHBORS CALL YOU AN"
1023 PRINT "IMPERIALIST PIG.  WATCH OUT FOR WAR."
1026 GOTO 990
1027 END 
