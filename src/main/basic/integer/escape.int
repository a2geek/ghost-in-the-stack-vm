1 A=0
8 DIM MAZE(1600),INCR(4),ASKT(3)
9 INCR(1)=1:INCR(2)=40:INCR(3)=-1:INCR(4)=-40
10 DIM LEFT(5),RIGHT(5),TOP(5),STR$(40)
11 DIM PRH(18),PRL(18),PRC(18),DN$(20),DETR(3)
12 CHANCE=3:TEXT :CALL -936:VTAB 6:TAB 15:PRINT "ESCAPE!":PRINT
13 REM
14 PRINT "COPYRIGHT 1978, SILAS S. WARNER":PRINT "     ALL RIGHTS RESERVED"
15 VTAB 21:INPUT "READ PRIOR GAME FROM TAPE? (Y OR N)",STR$:IF  LEN(STR$)=0 THEN 20:IF  ASC(STR$)=206 THEN 20
16 GOSUB 1600:GR :GOTO 2990
20 FOR K=1 TO 1600:MAZE(K)=0:NEXT K
25 TEXT :GOSUB 1000
30 POS=42:NEW=42:MAZE(42)=15
40 OLDIR= RND (4)+1:DIR=OLDIR
60 NEXT=POS+INCR(DIR):SECD=NEXT+INCR(DIR)
70 IF SECD>1519 OR SECD<1 THEN 80:IF (SECD MOD 40)>0 THEN 90
80 DIR=((DIR+1) MOD 4)+1:IF DIR=OLDIR THEN 120:GOTO 60
90 IF MAZE(SECD)>0 THEN 80
100 MAZE(NEXT)=15:MAZE(SECD)=15:POS=SECD
110 GOTO 40
120 OLDIR=POS
130 POS=POS+2:X=(POS-1)/40:Y=(POS-1)-(40*X)
131 IF Y>38 THEN POS=POS+42:IF POS>1519 THEN POS=42
135 IF POS=OLDIR THEN 175
139 IF MAZE(POS)>0 THEN 130
140 DIR=0:FOR K=1 TO 4:SECD=POS-(2*INCR(K)):IF (SECD MOD 40)=0 THEN 160
145 IF SECD>1600 OR SECD<1 THEN 160
150 IF MAZE(SECD)>0 THEN DIR=K
160 NEXT K:IF DIR=0 THEN 130:SECD=POS:NEXT=POS-INCR(DIR)
170 GOTO 100
175 FOR K=1 TO 19:POS=(80*K)-1:MAZE(POS)=15* RND (2)
178 POS=2*K+1520:MAZE(POS)=15* RND (2):NEXT K
180 MAZE(2)=8:GR :GOTO 2000
190 GOSUB 500
200 FOR K=1 TO 4:LEFT(K)=LEFT(K+1):RIGHT(K)=RIGHT(K+1):NEXT K
202 K=POS-INCR(DIR):BACK=MAZE(K)
205 IF LIMIT<6 THEN 270
210 K=POS+(4*INCR(DIR))
220 J=K+INCR(DIR):IF J<1 THEN 269:J=MAZE(J)
229 K=K+INCR(RIT)
230 RIGHT(5)=MAZE(K)>0:K=K-(2*INCR(RIT))
240 LEFT(5)=MAZE(K)>0
250 IF J=0 OR LIMIT<6 THEN 270
260 GOTO 1260
269 EXIT=1:RIGHT(5)=0:LEFT(5)=0
270 LIMIT=LIMIT-1
290 RETURN
300 LIMIT=-1:K=POS-INCR(DIR):BACK=MAZE(K)
302 EXIT=0
305 RIT=DIR-1:IF RIT<1 THEN RIT=4
310 LIMIT=LIMIT+1:IF LIMIT>5 THEN 1120
330 K=POS+(LIMIT*INCR(DIR)):IF K>0 THEN 332
331 EXIT=1:LIMIT=LIMIT+1:RIGHT(LIMIT)=0:LEFT(LIMIT)=0:GOTO 1120
332 IF MAZE(K)=0 THEN 1120
335 IF LIMIT=5 THEN 310
340 K=K+INCR(RIT):RIGHT(LIMIT+1)=(MAZE(K)>0)
350 K=K-(2*INCR(RIT)):LEFT(LIMIT+1)=(MAZE(K)>0)
360 GOTO 310
500 FOR I=1 TO 18:LLIM=PRL(I):ULIM=PRH(I):K=PRC(ULIM)
505 LHIR=39-LLIM:UHIR=39-ULIM
509 IF LIMIT<K THEN 700
510 IF LEFT(K)=0 THEN 600:COLOR= 12-(4*(K=LIMIT))
512 COLOR= 12-(4*(K=LIMIT))
515 VLIN ULIM+1,UHIR-1 AT ULIM
520 COLOR= 8:IF LLIM<0 THEN 580:HLIN LLIM,ULIM-1 AT UHIR
530 HLIN LLIM,ULIM-1 AT ULIM:VLIN ULIM,UHIR AT LLIM
540 COLOR= 15:VLIN LLIM,ULIM-1 AT LLIM:COLOR= 4:VLIN UHIR+1,LHIR AT LLIM
550 GOTO 600
560 COLOR= 8:IF EXIT=1 THEN COLOR= 11
563 VLIN ULIM,20 AT ULIM:HLIN ULIM,UHIR AT ULIM+1:VLIN ULIM,20 AT UHIR
565 IF EXIT=1 THEN COLOR= 5
568 VLIN 20,UHIR AT UHIR:HLIN ULIM,UHIR AT UHIR-1:VLIN 20,UHIR AT ULIM
570 GOTO 700
580 HLIN 0,ULIM-1 AT UHIR:HLIN 0,ULIM-1 AT ULIM
600 IF RIGHT(K)=0 THEN 690:COLOR= 12-(4*(K=LIMIT))
605 VLIN ULIM+1,UHIR-1 AT UHIR
610 COLOR= 8:IF LLIM<0 THEN 680:HLIN UHIR+1,LHIR AT UHIR
620 HLIN UHIR+1,LHIR AT ULIM:VLIN ULIM,UHIR AT LHIR
630 COLOR= 15:VLIN LLIM,ULIM-1 AT LHIR:COLOR= 4:VLIN UHIR+1,LHIR AT LHIR
640 GOTO 690
680 HLIN UHIR+1,39 AT UHIR:HLIN UHIR+1,39 AT ULIM
690 IF LIMIT=K THEN 560
700 NEXT I:K=2:LLIM=1:ULIM=5:GOSUB 770
720 K=4:LLIM=10:ULIM=13:GOSUB 770
725 K=5:LLIM=14:ULIM=16:GOSUB 770
730 K=2:LLIM=0:ULIM=5:GOSUB 770
740 RETURN
770 IF LIMIT<K THEN RETURN :UHIR=39-ULIM:LHIR=39-LLIM
771 I=LEFT(K):COLOR= 12+(3*I):VLIN LLIM+1,ULIM AT LLIM
772 COLOR= 12-(4*I):VLIN ULIM+1,UHIR-1 AT LLIM
774 COLOR= 12-(8*I):VLIN UHIR,LHIR-1 AT LLIM
780 I=RIGHT(K):COLOR= 12+(3*I):VLIN LLIM+1,ULIM AT LHIR
782 COLOR= 12-(4*I):VLIN ULIM+1,UHIR-1 AT LHIR
784 COLOR= 12-(8*I):VLIN UHIR,LHIR-1 AT LHIR
786 IF LIMIT>K THEN RETURN
800 COLOR= 8:HLIN ULIM,UHIR AT ULIM+1:VLIN ULIM+1,UHIR-1 AT ULIM
810 HLIN ULIM,UHIR AT UHIR-1:VLIN ULIM+1,UHIR-1 AT UHIR
820 RETURN
880 IF  PEEK (-16384)<128 THEN 880: POKE -16368,0: POKE -16384,0:RETURN
890 PRINT :PRINT :PRINT :PRINT :PRINT
900 IF LEFT(1)=0 THEN 901: POKE 1890,188: POKE 1891,237
901 IF LIMIT<2 THEN 902: POKE 1892,161: POKE 1764,129
902 IF RIGHT(1)=0 THEN 903: POKE 1893,237: POKE 1894,190
903 IF BACK=0 THEN 904: POKE 1892,161: POKE 2020,150
904 IF COMP=0 THEN RETURN :K=DIR:IF COMP=2 THEN K=DIR-2:IF K<1 THEN K=K+4
905 FOR J=-4 TO 0: POKE 1638+J, ASC(DN$(5*K+J))
906 NEXT J:RETURN
910  POKE 1890,60: POKE 1891,45:RETURN
920  POKE 1892,33: POKE 1764,1:RETURN
930  POKE 1893,45: POKE 1894,62:RETURN
940  POKE 1892,33: POKE 2020,22:RETURN
1000 CALL -936
1001 PRINT :PRINT " I AM NOT ALLOWED TO SHOW YOU THE WAY"
1005 PRINT " OUT OF THE BUILDING.":PRINT
1010 PRINT " PRESS THE RETURN KEY TO MOVE AHEAD."
1015 PRINT " THE KEYS <- AND -> TURN YOU."
1020 PRINT " THE / KEY TURNS YOU AROUND.":PRINT
1030 PRINT " ASK THE PEOPLE YOU MEET THE WAY OUT.":PRINT
1035 PRINT " SOME OF THEM WILL TELL YOU THE TRUTH,"
1037 PRINT " SOME OF THEM WILL ALWAYS LIE, AND"
1040 PRINT " SOME MAY DO EITHER ONE."
1060 PRINT " THAT IS ALL I AM ALLOWED TO TELL YOU."
1070 PRINT " HERE IS A PASS. YOU MAY NEED IT."
1071 PRINT :PRINT " NOW CONSTRUCTING NEW UNIQUE MAZE":PRINT "(WAIT 120 SEC)":PRINT :
1072 PRINT " YOU CAN QUIT BY PRESSING Q."
1080 MAP=0:COMP=0:PASS=1:RETURN
1090 COLOR= 12:FOR K=0 TO 39:HLIN 0,39 AT K:NEXT K:RETURN
1100 COLOR= 12:FOR K=0 TO 39:VLIN 0,39 AT K:NEXT K:RETURN
1110 COLOR= 12:FOR K=0 TO 39:VLIN 0,39 AT 39-K:NEXT K:RETURN
1120 COLOR= 15:FOR K=0 TO 19:HLIN 0,39 AT K:NEXT K
1121 COLOR= 4:FOR K=20 TO 39:HLIN 0,39 AT K:NEXT K
1122 IF LEFT(1) THEN 1123:COLOR= 12:VLIN 1,38 AT 0:GOTO 1125
1123 COLOR= 8:VLIN 6,33 AT 0
1125 IF RIGHT(1) THEN 1127:COLOR= 12:VLIN 1,38 AT 39:GOTO 1130
1127 COLOR= 8:VLIN 6,33 AT 39
1130 FOR I=1 TO 18:K=PRC(I)
1140 IF LIMIT<K THEN 1230:IF LEFT(K) THEN 1160
1150 COLOR= 12:VLIN I+1,38-I AT I:GOTO 1170
1160 ULIM=TOP(K)+1:COLOR= 8:VLIN ULIM,39-ULIM AT I
1170 IF RIGHT(K) THEN 1190
1180 COLOR= 12:VLIN I+1,38-I AT 39-I:GOTO 1250
1190 ULIM=TOP(K)+1:COLOR= 8:VLIN ULIM,39-ULIM AT 39-I:GOTO 1250
1230 COLOR= 8:IF LIMIT=K-1 THEN ULIM=TOP(K)+1
1235 VLIN I,39-I AT I:HLIN I,39-I AT I
1240 VLIN I,39-I AT 39-I:HLIN I,39-I AT 39-I
1250 NEXT I
1260 IF LIMIT>5 THEN RETURN :COLOR= 8
1300 HLIN 19,20 AT 19:HLIN 19,20 AT 20
1320 RETURN
1500 A= PEEK (205)- PEEK (75):A=256*A+ PEEK (204)- PEEK (74):GOSUB 1700
1510  POKE 60,4: POKE 61,8: POKE 62,5: POKE 63,8:CALL -307
1520  POKE 60, PEEK (74): POKE 61, PEEK (75): POKE 62, PEEK (204): POKE 63, PEEK (205):CALL -307
1590 RETURN
1600 GOSUB 1700
1610  POKE 60,4: POKE 61,8: POKE 62,5: POKE 63,8:CALL -259
1620 IF A<0 THEN 1690:K=A+ PEEK (74)+ PEEK (75)*256:IF K>( PEEK (202)+ PEEK (203)*256) THEN 1690
1630  POKE 204,K MOD 256: POKE 205,K/256: POKE 60, PEEK (74): POKE 61, PEEK (75): POKE 62, PEEK (204): POKE 63, PEEK (205):CALL -259
1670 RETURN
1690 CALL -936:PRINT "MEM FULL",K:GOTO 9999
1700 INPUT "START TAPE THEN HIT RETURN",STR$:PRINT "NOW WAIT FOR THE SECOND BEEP":RETURN
2000 TOP(1)=5:TOP(2)=9:TOP(3)=13:TOP(4)=16:TOP(5)=18
2021 DETR(1)= RND (3)+1:DETR(2)= RND (2)+1:GOTO 2021+DETR(1)
2022 DETR(2)=DETR(2)+1:DETR(3)=5-DETR(2):GOTO 2030
2023 DETR(2)=(2*DETR(2)-1):DETR(3)=4-DETR(2):GOTO 2030
2024 DETR(3)=3-DETR(2)
2030 PRH(1)=5:PRL(1)=-1:PRC(1)=1
2040 PRH(2)=9:PRL(2)=5:PRC(2)=1
2050 PRH(3)=13:PRL(3)=9:PRC(3)=1
2060 PRH(4)=4:PRL(4)=-1:PRC(4)=1
2070 PRH(5)=16:PRL(5)=13:PRC(5)=1
2080 PRH(6)=3:PRL(6)=-1:PRC(6)=2
2090 PRH(7)=18:PRL(7)=16:PRC(7)=2
2100 PRH(8)=12:PRL(8)=8:PRC(8)=2
2110 PRH(9)=8:PRL(9)=4:PRC(9)=2
2120 PRH(10)=15:PRL(10)=12:PRC(10)=3
2130 PRH(11)=2:PRL(11)=-1:PRC(11)=3
2140 PRH(12)=17:PRL(12)=15:PRC(12)=3
2150 PRH(13)=11:PRL(13)=7:PRC(13)=3
2160 PRH(14)=7:PRL(14)=3:PRC(14)=4
2170 PRH(15)=14:PRL(15)=11:PRC(15)=4
2171 PRH(16)=6:PRL(16)=2:PRC(16)=4
2180 PRH(17)=10:PRL(17)=6:PRC(17)=5
2190 PRH(18)=1:PRL(18)=-1:PRC(18)=5
2191 DN$="SOUTHEAST NORTHWEST "
2200 POS=1518:DIR=3:IF MAZE(1517)=0 THEN DIR=4
2210 RIT=DIR-1:IF RIT<1 THEN RIT=4
2220 FOR I=1 TO 10:CALL -1059:NEXT I
2990 GOSUB 1100:GOSUB 300:GOTO 3000
3000 PRINT :PRINT :PRINT :PRINT
3010 IF POS<40 THEN 9000:IF  RND (30)<CHANCE THEN 4500
3013 IF MAP=0 THEN 3016:PRINT "M FOR MAP";
3016 IF PASS=0 THEN 3020:TAB 25:PRINT "YOU HAVE A PASS";
3020 GOSUB 900:GOTO 3030
3025 CALL -1059
3030 IF  PEEK (-16384)<127 THEN 3030:KEY= PEEK (-16384): POKE -16368,0: POKE -16384,0
3040 IF KEY=205 THEN 3500
3050 IF KEY=141 OR KEY=198 THEN 3200
3060 IF KEY=136 OR KEY=204 THEN 3100
3070 IF KEY=149 OR KEY=210 THEN 3300
3080 IF KEY=175 OR KEY=194 THEN 3400
3090 IF KEY=209 THEN 6000:GOTO 3025
3100 IF LEFT(1)=0 THEN 3025:GOSUB 910
3102 GOSUB 1100
3105 DIR=DIR+1:IF DIR>4 THEN DIR=DIR-4
3130 GOSUB 300
3180 PRINT :PRINT :PRINT :PRINT :GOSUB 900
3200 IF LIMIT<2 THEN 3025:GOSUB 920
3210 POS=POS+INCR(DIR):GOSUB 190
3280 GOTO 3000
3300 IF RIGHT(1)=0 THEN 3025:GOSUB 930:GOSUB 1110
3310 DIR=DIR-1:IF DIR<1 THEN DIR=DIR+4
3330 GOSUB 300
3340 GOTO 3180
3400 IF BACK<1 THEN 3025:GOSUB 940:GOSUB 1110
3410 DIR=DIR+2:IF DIR>4 THEN DIR=DIR-4
3440 GOSUB 300
3450 GOTO 3000
3500 IF MAP=0 THEN 3025:GR :COLOR= 15
3501 PRINT " THE EXIT IS AT THE NORTHWEST"
3502 PRINT :PRINT
3520 FOR X=0 TO 39:FOR Y=0 TO 39:J=Y
3521 IF MAP=1 THEN 3530
3522 IF X<1 THEN 3530
3525 IF J>0 AND J<38 THEN J=38-J
3530 COLOR= MAZE(40*X+J+1)
3540 PLOT X,Y
3550 NEXT Y:NEXT X
3660 GOSUB 880:PRINT
3690 GOSUB 1120:GOTO 3020
3960 INPUT STR$:GOSUB 1090:GOSUB 1120:GOTO 3020
4000 COLOR= (2*HT)+(HT=0)
4010 ULIM=13:IF HT=2 THEN ULIM=8:IF HT=3 THEN ULIM=10
4020 LLIM=13:IF HT=1 THEN LLIM=11:IF HT=2 THEN LLIM=5:IF HT=3 THEN LLIM=7
4030 IF LLIM=ULIM THEN 4100
4040 J=ULIM+1-LLIM
4050 FOR K=LLIM TO ULIM:HLIN 9+J,21-J AT K:J=J-1:NEXT K
4100 FOR K=ULIM+1 TO 17:HLIN 9,21 AT K:NEXT K
4110 HLIN 6,24 AT 18
4120 COLOR= 11:FOR K=0 TO 9:HLIN 10,20 AT K+19:NEXT K
4130 FOR K=1 TO 4:HLIN 10+K,20-K AT K+28:NEXT K
4140 COLOR= 5:FOR K=0 TO 3:HLIN 14-(3*K),16+(3*K) AT K+33:NEXT K
4145 FOR K=37 TO 39:HLIN 4,26 AT K:NEXT K
4150 COLOR= 15:VLIN 33,36 AT 13:VLIN 33,36 AT 17
4160 VLIN 36,39 AT 14:VLIN 36,39 AT 16
4190 RETURN
4500 HT= RND (4):GIFT= RND (7):GOSUB 4000
4501 IF HT>0 THEN 4560:PRINT
4510 PRINT "YOU SHOULD NOT BE IN THE BUILDING."
4520 PRINT "I'LL HAVE TO TAKE YOUR PASS AWAY.":PRINT
4530 PRINT :GOSUB 880
4541 IF PASS=0 THEN 4544:IF PASS=2 AND  RND (10)#1 THEN 4544
4542 PASS=0:GOTO 4900
4544 IF  RND (2)=1 THEN 4551
4545 PRINT "YOU DON'T HAVE A VALID PASS?"
4546 PRINT "YOU ARE UNDER ARREST. COME ALONG"
4547 PRINT "QUIETLY OR YOU WILL BE SHOT."
4548 GOSUB 880:PRINT :PRINT "            YOU LOST!"
4549 GOTO 9800
4550 PASS=0:GOTO 4900
4551 PRINT "YOU DON'T HAVE A VALID PASS?"
4552 PRINT "THEN TAKE THIS ONE AND LEAVE THE"
4553 PRINT "BUILDING AT ONCE!"
4554 INPUT STR$:PASS=1:GOTO 4900
4560 FOR J=1 TO 3:ASKT(J)=0:NEXT J
4600 IF ASKT(1)>0 AND ASKT(2)>0 AND ASKT(3)>0 THEN 4900
4608 PRINT "(1) WHICH DIRECTION AM I GOING?"
4610 PRINT "(2) DO YOU HAVE ANYTHING TO GIVE ME?"
4620 PRINT "(3) DO YOU TELL THE TRUTH?"
4625 I=DETR(HT)<( RND (2)+2)
4630 PRINT "TYPE 1,2, OR 3(0 TO GO ON)";
4631  POKE -16368,0: POKE -16384,0
4632 IF  PEEK (-16384)<128 THEN 4632:KEY= PEEK (-16384)
4633 QUES=KEY-176:IF QUES>-1 AND QUES<4 THEN 4636
4634 FOR K=1 TO 22: POKE -16336,0:NEXT K:GOTO 4631
4636 PRINT
4637  POKE -16368,0: POKE -16384,0
4640 IF QUES=0 THEN 4900
4642 IF ASKT(QUES)=1 THEN 4690:ASKT(QUES)=1
4645 I=(DETR(HT)< RND (2)+1)
4650 IF QUES=3 THEN 4710
4660 IF QUES=1 THEN 4730
4670 GOTO 4780
4690 PRINT "YOU ALREADY ASKED THAT QUESTION!"
4691 PRINT :PRINT :GOSUB 880:PRINT :GOTO 4600
4710 GOTO 4708+(3*DETR(HT))+ RND (3)
4711 PRINT "I ALWAYS TELL THE TRUTH.":GOTO 4720
4712 GOTO 4711
4713 PRINT "I'M NOT ALLOWED TO ANSWER THAT.":GOTO 4720
4714 PRINT "SOMETIMES I TELL THE TRUTH.":GOTO 4720
4715 PRINT "I ALWAYS LIE.":GOTO 4720
4716 GOTO 4711
4717 GOTO 4711
4718 GOTO 4714
4719 GOTO 4713
4720 PRINT :PRINT :GOSUB 880:PRINT :GOTO 4600
4730 PRINT "  YOU ARE HEADING      .":PRINT :PRINT
4740 K=DIR:IF I THEN 4770
4750 K=DIR+ RND (3)+1:IF K>4 THEN K=K-4
4770 GOSUB 905:GOSUB 880:PRINT :GOTO 4600
4780 IF GIFT>0 THEN 4790:PRINT "I HAVE NOTHING TO GIVE YOU":GOTO 4691
4790 PRINT
4800 GOTO 10*GIFT+4800
4810 IF I THEN 4825
4815 PRINT "HERE IS A FALSE MAP.IT SHOWS THIS"
4817 PRINT "BUILDING, BUT IT'S MIXED UP.":PRINT
4818 GOTO 4880
4820 IF I THEN 4815
4825 PRINT "HERE IS A TRUE MAP. IT SHOWS THE"
4826 PRINT "CORRIDORS IN THIS BUILDING."
4827 PRINT :GOTO 4880
4830 IF I THEN 4845
4835 PRINT "HERE IS A COMPASS THAT POINTS"
4836 PRINT "SOUTH. IT WILL ALWAYS SHOW THE"
4837 PRINT "OPPOSITE OF THE TRUE DIRECTION."
4838 GOTO 4880
4840 IF I THEN 4835
4845 PRINT "HERE IS A COMPASS THAT ALWAYS"
4846 PRINT "SHOWS YOU THE TRUE DIRECTION YOU"
4847 PRINT "ARE TRAVELING.":GOTO 4880
4850 IF I THEN 4865
4855 PRINT "HERE IS A FORGED PASS. IT MAY"
4856 PRINT "FOOL THE GUARDS ABOUT 10% OF THE"
4857 PRINT "TIME":GOTO 4880
4860 IF I THEN 4855
4865 PRINT "HERE IS A PASS. THE GUARDS WILL"
4866 PRINT "ALWAYS ACCEPT IT AND LET YOU PASS."
4867 PRINT :GOTO 4880
4880 INPUT "DO YOU WANT IT?",STR$:IF  LEN(STR$)>0 THEN 4891
4890 PRINT :PRINT :GOTO 4880
4891 PRINT :PRINT :IF  ASC(STR$)=206 THEN 4600:GOTO 4891+GIFT
4892 MAP=1:GOTO 4600
4893 MAP=2:GOTO 4600
4894 COMP=1:GOTO 4600
4895 COMP=2:GOTO 4600
4896 PASS=1:GOTO 4600
4897 PASS=2:GOTO 4600
4900 GOSUB 1120:PRINT :PRINT :PRINT :PRINT :GOTO 3020
6000 PRINT :PRINT :PRINT :INPUT "SAVE THIS GAME ON TAPE?(Y OR N)",STR$
6010 IF  LEN(STR$)=0 THEN 9900:IF  ASC(STR$)=206 THEN 9900:GOSUB 1500:GOTO 9900
9000 PRINT :PRINT :PRINT
9010 IF  LEN(STR$)=0 THEN 9800
9020 FOR J=1 TO 15:ULIM=17-J:UHIR=39-ULIM
9030 COLOR= 11:VLIN ULIM,19 AT ULIM:HLIN ULIM,UHIR AT ULIM:VLIN ULIM,19 AT UHIR
9040 COLOR= 5:VLIN 20,UHIR AT ULIM:HLIN ULIM,UHIR AT UHIR:VLIN 20,UHIR AT UHIR
9050 NEXT J
9800 INPUT "        CARE TO TRY AGAIN?",STR$
9810 IF  LEN(STR$)=0 THEN 9900:IF  ASC(STR$)#206 THEN 13
9900 TEXT :CALL -936
9999 END
