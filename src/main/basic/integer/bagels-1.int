10 REM --------------------------
20 REM ! BAGELS. BY ERIC WALLER !
30 REM ! 7/28/77 APPLE ][ BASIC !
40 REM --------------------------
50 DIM N(2),A$(10),TST(2),GUESS(2):GAME=0:TRY=0:GOSUB 4000
60 GOSUB 1000:REM - SET UP GAME 
70 INPUT "YOUR GUESS",G:IF G=0 THEN 500
80 IF G>=100 AND G<=999 THEN 100
90 PRINT "ALL GUESSES MUST BE BETWEEN 100 AND":PRINT "999 INCLUSIVE. THANK YOU.":GOTO 70
100 GOSUB 2000:IF F0=0 THEN 70
110 PRINT ">>> YOU GOT IT IN<CTRL-G><CTRL-G><CTRL-G><CTRL-G> ";T;" TRYS <<<":TRY=TRY+T:GAME=GAME+1
120 FOR X=0 TO 1500:NEXT X:GOTO 60
500 PRINT " YOU GOT<CTRL-G><CTRL-G> ";GAME;" NUMBERS RIGHT USING":PRINT TRY;" TRYS"
510 PRINT "YOUR RATING=";:IF TRY<>0 THEN PRINT (100*GAME+TRY-1)/TRY
520 IF TRY=0 THEN PRINT 0
530 END 
1000 FOR X=0 TO 2:N(X)= RND (10):IF X=0 AND N(X)=0 THEN 1000
1010 NEXT X:CALL -936
1020 FOR X=0 TO 2:TST(X)=N(X):NEXT X:GOSUB 3000:IF F1=1 THEN 1000
1030 T=0:RETURN 
2000 F0=0:FOR X=2 TO 0 STEP -1
2010 TST(X)=G MOD 10
2020 GUESS(X)=G MOD 10:G=G/10
2030 NEXT X
2040 GOSUB 3000
2050 IF F1=0 THEN 2070:PRINT "PLEASE ENTER A GUESS IN WHICH ALL":PRINT "DIGITS ARE DIFFERENT. THANK YOU."
2060 RETURN 
2070 P=0:F=0:FOR X=0 TO 2:FOR Y=0 TO 2
2080 IF X=Y AND N(X)=GUESS(Y) THEN F=F+1
2090 IF X<>Y AND N(X)=GUESS(Y) THEN P=P+1
2100 NEXT Y,X
2110 T=T+1:IF F<3 THEN 2130
2120 PRINT "*<CTRL-G><CTRL-G><CTRL-G>":F0=1:RETURN 
2130 IF F+P>0 THEN 2150
2140 PRINT "O":RETURN 
2150 IF P=0 THEN 2170
2160 FOR X=1 TO P:PRINT "T  ";:NEXT X
2170 IF F=0 THEN 2190
2180 FOR X=1 TO F:PRINT "TT  ";:NEXT X
2190 PRINT :RETURN 
3000 FOR X=0 TO 2:FOR Y=0 TO 2:IF X=Y OR TST(X)<>TST(Y) THEN 3020
3010 F1=1:RETURN 
3020 NEXT Y,X:F1=0:RETURN 
4000 TEXT :CALL -936:TAB 10:VTAB 10:PRINT "B A G E L S":TAB 10:PRINT "- - - - - -"
4010 PRINT "TT = 1  DIGIT RIGHT IN CORRECT PLACE":PRINT "T  = 1  DIGIT RIGHT, IN WRONG PLACE"
4020 PRINT "O  = NO CORRECT DIGITS":PRINT "*  = YOU GOT ALL THE CORRECT DIGITS IN":PRINT "   THE RIGHT PLACE (YOU WIN)"
4030 PRINT ">> TO END GAME, ENTER '0' AS YOUR GUESS"
4040 INPUT "PLEASE PRESS RETURN WHEN READY",A$:RETURN 
