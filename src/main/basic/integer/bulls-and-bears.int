4 CALL -936
5 GOSUB 130
6 PRINT :PRINT :PRINT 
10 INPUT "ENTER NUMBER OF PLAYERS ",PN
20 IF PN<1 THEN 10
24 IF PN>8 THEN 10
30 DIM PL(3*PN)
40 DIM CO((PN+1)*15)
50 FOR X=1 TO (PN+1)*15
60 CO(X)=0
70 NEXT X
80 GOSUB 5000
125 GOTO 180
130 TAB 6:PRINT "SPEAKEASY SOFTWARE PRESENTS"
140 VTAB 3
150 TAB 8:PRINT "**BULLS AND BEARS**"
155 PRINT 
160 TAB 6:PRINT "A GAME OF CORPORATE FINANCE"
161 VTAB 12:TAB 5:PRINT "INQUIRIES:";
162 PRINT "SPEAKEASY SOFTWARE LTD"
163 TAB 10:PRINT "BOX 1220"
164 TAB 10:PRINT "KEMPTVILLE ONTARIO"
165 TAB 10:PRINT "CANADA K0G 1J0"
170 RETURN 
180 DIM A$(10*PN)
181 DIM PNP(PN+1)
189 DIM T$(20)
190 FOR X=1 TO PN
200 PRINT "ENTER NAME FOR PLAYER ",X;
210 INPUT T$
212 IF  LEN(T$)>10 THEN 200
213 IF  LEN(T$)=0 THEN 200
215 PNP(X)= LEN(A$)+1
220 A$( LEN(A$)+1)=T$
230 NEXT X
235 CALL -936
237 TAB 10:PRINT "**BULLS AND BEARS**"
240 TAB 13:PRINT "PLAYER LIST"
250 FOR X=1 TO PN
255 IF PN=X THEN PNP(X+1)=1+ LEN(A$)
260 PRINT "PLAYER NUMBER ",X,"";
270 PRINT A$(PNP(X),PNP(X+1)-1)
280 NEXT X
290 VTAB 15:PRINT "GAME HINT:";
300 PRINT "AVOID LETTING OTHER PLAYERS "
310 TAB 5:PRINT "SEE YOUR CASH OR HOLDINGS"
320 TAB 5:PRINT "THIS WILL AID YOU IN MAKING"
330 TAB 5:PRINT "SURPRISE TAKE OVER BIDS ETC."
340 FOR X=1 TO (PN*1000):NEXT X
350 CALL -936
410 GOTO 7000
638 CO(T1*15-(15-X))=CO(T1*15-(15-X))+Y
1000 INPUT T$
1005 IF  LEN(T$)=0 THEN 1030
1010 IF  ASC(T$)=217 THEN 1060
1020 IF  ASC(T$)=206 THEN 1050
1030 PRINT "PLEASE RE-TYPE LINE"
1035 PRINT "ANSWER MUST BE 'Y' OR 'N'"
1040 GOTO 1000
1050 POP :GOTO NL
1060 RETURN 
2000 REM  PLAYER BANKIMG
2005 FOR X=1 TO PN
2006 GOSUB 3000:CALL -936
2010 TAB 12:PRINT "PLAYER FINANCE"
2020 PRINT "PLAYER ",X," -";
2030 PRINT A$(PNP(X),PNP(X+1)-1)
2040 PRINT "DO YOU WANT YOUR CASH BALANCE";
2050 NL=2070:GOSUB 1000
2060 PRINT "YOUR CASH BALANCE IS ";PL(X*3-1);"000"
2070 PRINT "DO YOU WANT YOUR SHAREHOLDINGS ";
2080 NL=2180:GOSUB 1000
2090 TEXT :CALL -936
2095 T2=0
2100 PRINT "SHAREHOLDINGS - PLAYER ";X;" - ";A$(PNP(X),PNP(X+1)-1)
2105 PRINT "CORPORATION";:TAB 20:PRINT "SHARES";
2107 TAB 30:PRINT "VALUE"
2110 FOR Y=1 TO (PN+1)
2115 IF CO(Y*15-(15-X))=0 THEN 2150
2120 PRINT C$(CNP(Y),CNP(Y+1)-1),
2125 TAB 20
2130 PRINT CO(Y*15-(15-X));"000",
2140 T1=CO(Y*15-(15-X))*CO(Y*15-1)
2145 T2=T2+T1:TAB (30):PRINT T1;"000"
2150 NEXT Y
2155 VTAB (PN+1+4)
2160 PRINT "TOTAL VALUE OF SHARES = ";T2;"000"
2170 PRINT "YOUR NET WORTH IS ";T2+PL(X*3-1)-PL(X*3);"000"
2180 PRINT "YOU OWE THE BANK ";PL(X*3);"000"
2190 PRINT "DO YOU WANT TO BORROW ";
2200 NL=2340:GOSUB 1000
2210 PRINT "INTEREST RATES ARE ";PL(0);"% PER YEAR"
2220 PRINT "HOW MUCH DO YOU WANT TO BORROW ";
2230 INPUT T1:T2=0
2235 T1=ABS (T1)
2237 IF T1>9 OR T1=0 THEN 2240
2238 PRINT "MINIMUM IS $10000":GOTO 2220
2240 FOR Y=1 TO (PN+1)
2250 T2=T2+(CO(Y*15-(15-X))*CO(Y*15-1))
2260 NEXT Y
2270 T2=T2+PL(X*3-1)-PL(X*3)
2280 IF T1>(T2/2) THEN 2330
2290 PRINT T1;"000","APPROVED"
2300 PL(X*3-1)=PL(X*3-1)+T1
2310 PL(X*3)=PL(X*3)+T1
2315 GOSUB 8000
2320 GOTO 2450
2330 PRINT "AMOUNT EXCESSIVE - REFUSED"
2335 GOSUB 8100
2340 IF PL(X*3)<1 THEN 2450
2345 PRINT "DO YOU WANT TO MAKE A PAYMENT ON YOUR LOAN OF ";
2350 PRINT PL(X*3);"000";
2360 NL=2450:GOSUB 1000
2370 INPUT "ENTER AMOUNT OF LOAN PAYMENT ",T1
2375 T1=ABS (T1)
2380 IF T1>PL(X*3-1) THEN 2430
2390 PL(X*3-1)=PL(X*3-1)-T1
2400 PL(X*3)=PL(X*3)-T1
2410 PRINT "THANK YOU"
2415 GOSUB 8000
2420 GOTO 2450
2430 PRINT "INSUFFICIENT FUNDS"
2440 GOTO 2370
2450 NEXT X:RETURN 
3000 REM  MARKET DISPLAY ROUTINE
3010 CALL -936:VTAB 1
3020 TAB 12:PRINT "MARKET REPORT - 19";YEAR
3030 VTAB 3
3040 PRINT "COMPANY";:TAB 18
3050 PRINT "EARNINGS BID ASK"
3060 FOR Y=1 TO (PN+1)
3070 PRINT C$(CNP(Y),CNP(Y+1)-1);
3080 TAB 20:PRINT CE(Y);"000";
3082 TAB 27:PRINT CO(Y*15-1);
3084 TAB 33:PRINT CO(Y*15)
3100 NEXT Y
3105 VTAB 19: POKE 34,19
3110 RETURN 
4000 REM  CORPORATE FINANCE
4010 FOR X=1 TO (PN+1)
4020 T1=1:FOR Y=1 TO (PN-1)
4030 IF CO(X*15-(15-T1))<CO(X*15-(15-(Y+1))) THEN T1=Y+1
4040 NEXT Y
4045 IF CO(X*15-(15-T1))=0 THEN 4850
4050  POKE 34,0:CALL -936
4060 TAB 10:PRINT "CORPORATION STATUS REPORT"
4070 PRINT C$(CNP(X),CNP(X+1)-1);
4080 TAB 18:PRINT "DIRECTOR - ";
4090 TAB 29:PRINT A$(PNP(T1),PNP(T1+1)-1)
4100 PRINT "CORPORATION - ",X
4110 PRINT "  CASH  SHARES AVAIL BID  ASK"
4120 PRINT CO(X*15-5);"000";
4130 TAB 11:PRINT CO(X*15-2);"000";
4140 TAB 22:PRINT CO(X*15-1);
4150 TAB 27:PRINT CO(X*15)
4160 PRINT :PRINT "PRODUCTION UNITS = ";CO(X*15-3)
4170 PRINT :PRINT "LOANS OUTSTANDING = ";CO(X*15-4);"000"
4180 PRINT :PRINT "PER SHARE BOOK VALUE = ";
4190 T1=(CO(X*15-5)+CO(X*15-3)*10-CO(X*15-4))/20
4200 PRINT T1
4210  POKE 34,16
4220 PRINT "DO YOU WANT TO DECLARE DIVIDENDS ";
4230 NL=4340:GOSUB 1000
4240 PRINT "DIVIDENDS PER THOUSAND SHARES = ";
4250 T2=(CO(X*15-5)-100)/20
4252 IF T2<0 THEN T2=0
4255 PRINT T2
4260 IF T2=0 THEN 4340
4270 INPUT "ENTER DIVIDEND PER THOUSAND SHARES ",T1
4280 IF T1>T2 THEN 4240
4290 FOR Y=1 TO PN
4300 PL(Y*3-1)=PL(Y*3-1)+CO(X*15-(15-Y))*T1
4310 NEXT Y
4320 CO(X*15-5)=CO(X*15-5)-T1*20
4330 PRINT "CASH BALANCE NOW IS ";CO(X*15-5);"000"
4340 IF CO(X*15-3)=0 THEN 4450
4370 PRINT "DO YOU WANT TO SELL PRODUCTION UNITS ";
4380 NL=4450:GOSUB 1000
4390 PRINT "RESALE VALUE IS $9000"
4400 PRINT "LIMIT IS ";CO(X*15-3)
4410 INPUT " HOW MANY ",T1
4415 T1=ABS (T1)
4420 IF T1>CO(X*15-3) THEN 4400
4430 CO(X*15-5)=CO(X*15-5)+T1*9
4440 CO(X*15-3)=CO(X*15-3)-T1
4445 PRINT "CASH BALANCE NOW IS ";CO(X*15-5);"000"
4450 PRINT "DO YOU WANT TO BUY PRODUCTION"
4460 PRINT "UNITS AT $10000 EACH "
4470 NL=4580:GOSUB 1000
4480 INPUT "HOW MANY ",T1
4490 IF CO(X*15-5)>(T1*10) THEN 4500
4494 PRINT "INSUFFICIENT CASH"
4496 GOTO 4450
4500 CO(X*15-5)=CO(X*15-5)-T1*10
4510 CO(X*15-3)=CO(X*15-3)+T1
4520 PRINT "CASH BALANCE NOW IS ";CO(X*15-5);"000"
4580 IF CO(X*15-4)<1 THEN 4700
4590 PRINT "THE COMPANY OWES THE BANK ";CO(X*15-4);"000"
4600 PRINT "DO YOU WANT TO MAKE A LOAN PAYMENT ";
4610 NL=4700:GOSUB 1000
4620 INPUT "ENTER AMOUNT OF LOAN PAYMENT ",T1
4625 T1=ABS (T1)
4630 IF T1<=CO(X*15-5) THEN 4640
4634 PRINT "INSUFFICIENT FUNDS":GOTO 4620
4640 CO(X*15-5)=CO(X*15-5)-ABS (T1)
4650 CO(X*15-4)=CO(X*15-4)-ABS (T1)
4660 PRINT "THANK YOU "
4700 PRINT "DOES THE COMPANY WANT T0 BORROW MONEY ";
4710 NL=4850:GOSUB 1000
4715 PRINT "INTEREST RATES ARE ";CO(0);"% PER YEAR"
4720 INPUT "HOW MUCH",T1:T1=ABS (T1)
4724 IF T1>9 OR T1=0 THEN 4730
4726 PRINT "MINIMUM IS $10000":GOTO 4720
4730 T2=CO(X*15-5)+CO(X*15-3)*10
4740 T2=(T2-CO(X*15-4))/2
4750 IF T2>T1 THEN 4760
4755 PRINT "AMOUNT EXCESSIVE - REFUSED":GOSUB 8100:GOTO 4850
4760 CO(X*15-5)=CO(X*15-5)+ABS (T1)
4770 CO(X*15-4)=CO(X*15-4)+ABS (T1)
4780 PRINT T1;"000","APPROVED"
4790 GOSUB 8000
4850 NEXT X
4860 RETURN 
5000 REM   INIT VARIABLES
5010 REM  1/ PLAYER'S CASH
5020 FOR X=1 TO PN
5030 PL(X*3-1)=50
5040 PL(X*3)=0
5050 NEXT X
5060 DIM CE(PN+1)
5070 FOR X=1 TO (PN+1)
5075 CE(X)=0
5080 CO(X*15-5)=100
5090 CO(X*15-2)=20
5100 CO(X*15-1)=4
5110 CO(X*15)=6
5120 NEXT X
5125 DIM C$(150)
5130 C$="OFF-SHORE OILELECTRO LTDNORTHMINE HOLDINGSMCGOON CONSTR.GASSO"
5140 C$(62)="ALTER COMPUTERDATAMACHINEBOING AIRCRAFTHUGE CORP."
5145 DIM CNP(10)
5150 CNP(1)=1:CNP(2)=14:CNP(3)=25
5160 CNP(4)=43:CNP(5)=57:CNP(6)=62
5170 CNP(7)=76:CNP(8)=87:CNP(9)=101
5180 CNP(10)=111
5200 CO(0)=10+ RND (10)
5205 PL(0)=10+ RND (10)
5210 YEAR=10+ RND (68)
5215 TSI=20
5220 RETURN 
6000 FOR X=1 TO PN
6005 GOSUB 3000:CALL -936
6008 TAB 8:PRINT "PLAYER TRADING"
6010 PRINT A$(PNP(X),PNP(X+1)-1)
6020 PRINT "DO YOU WANT TO BUY OR SELL STOCK ";
6030 NL=6460:GOSUB 1000
6040 INPUT "FOR WHAT COMPANY (NUMBER) ",T1
6050 IF T1>(PN+1) THEN 6040
6055 IF T1<1 THEN 6040
6060 INPUT "BUY OR SELL ",T$
6065 IF  LEN(T$)=0 THEN 6060
6070 T2=1:IF  ASC(T$)=211 THEN T2=2
6075  POKE 34,0:CALL -936:VTAB 5:TAB 8
6080 PRINT C$(CNP(T1),CNP(T1+1)-1)
6085 TAB 8:PRINT "COMPANY NUMBER ",T1
6090 VTAB 10:PRINT "BID",CO(T1*15-1)
6100 VTAB 12:PRINT "ASKED",CO(T1*15)
6110 VTAB 14:PRINT "PRODUCTION UNITS",CO(T1*15-3)
6120 VTAB 16:PRINT "SHARES AVAILABLE",CO(T1*15-2);"000"
6130 PRINT "HOW MANY SHARES TO ";
6140 IF T2=2 THEN PRINT "SELL";
6150 IF T2=1 THEN PRINT "BUY ";
6160 PRINT " (,000S)";
6170 INPUT Y:IF T2=1 THEN 6360
6180 IF Y>CO(T1*15-(15-X)) THEN 6230
6200 CO(T1*15-(15-X))=CO(T1*15-(15-X))-Y
6205 CO(T1*15-2)=CO(T1*15-2)+Y
6210 PL(X*3-1)=PL(X*3-1)+Y*CO(T1*15-1)
6220 PRINT "STOCK SALE COMPLETE"
6223 PRINT "CREDITED TO YOUR ACCOUNT ";Y*CO(T1*15-1);"000"
6230 PRINT "YOU DO NOT OWN ";Y;"000","SHARES"
6240 PRINT "IN ";C$(CNP(T1),CNP(T1+1)-1)
6245 GOSUB 8100
6250 GOTO 6460
6360 IF Y*CO(T1*15)>PL(X*3-1) THEN 6430
6370 PRINT "PURCHASE PRICE IS ";Y*CO(T1*15);"000"
6390 PL(X*3-1)=PL(X*3-1)-(Y*CO(T1*15))
6392 CO(T1*15-2)=CO(T1*15-2)-Y
6395 CO(T1*15-(15-X))=CO(T1*15-(15-X))+Y
6400 PRINT "STOCK PURCHASE COMPLETE"
6410 GOTO 6450
6420 REM 
6430 PRINT "INSUFFICIENT FUNDS"
6435 GOSUB 8100
6440 GOTO 6460
6450 GOTO 6020
6460 NEXT X
6470 RETURN 
7000 GOSUB 3000
7010 GOSUB 2000
7020 GOSUB 6000
7030 GOSUB 4000
7050 GOSUB 9000
7055 CALL -936
7069 GOTO 10000
7070 END 
8000 REM  WAIT SUBROUTINE - SILENT
8010 FOR T1=1 TO 750:NEXT T1
8020 RETURN 
8100 REM  WAIT SUBROUTINE - BUZZER
8110 FOR T1=1 TO 750:T2= PEEK (-16336):NEXT T1
8120 RETURN 
9000 TEXT 
9005 FOR X=1 TO PN
9010 PL(X*3)=PL(X*3)+(PL(X*3)*PL(0)/100)
9015 NEXT X
9020 FOR X=1 TO PN+1
9025 CO(X*15-4)=CO(X*15-4)+(CO(X*15-4)*CO(0)/100)
9030 CE(X)=CO(X*15-3)*(5- RND (8))
9033 T1=CO(X*15-5)+(CO(X*15-3)*10)
9035 T1=T1-CO(X*15-4)
9040 T1=T1/20
9049 T2=CE(X)*(5+ RND (10))
9053 T2=T2/20
9060 IF T2>T1 THEN 9100
9070 CO(X*15-1)=T1-1
9080 CO(X*15)=T1+1
9090 GOTO 9200
9100 CO(X*15-1)=T2-1
9110 CO(X*15)=T2+1
9200 IF YEAR=29 THEN CE(X)=CE(X)*-1
9210 CO(X*15-5)=CO(X*15-5)+CE(X)
9230 IF CO(X*15-5)>0 THEN 9300
9240 CO(X*15-5)=100
9250 CO(X*15-4)=0
9270 CO(X*15-3)=0
9280 CO(X*15-1)=4:CO(X*15)=5
9300 NEXT X
9305 YEAR=YEAR+1
9310 CO(0)=10+ RND (10)
9320 PL(0)=10+ RND (10)
9330 RETURN 
10000 CALL -936:VTAB (3):PRINT "GAME TO CONTINUE ? ";
10010 NL=11000:GOSUB 1000
10020 X= RND (50)
10030 IF X/10#X/10+X MOD 10 THEN 7000
10040 CALL -936:CALL -1052:CALL -1052
10050 VTAB (3):TAB 8:PRINT "NEWS FLASH ! "
10060 IF X=0 THEN 10100
10070 IF X=10 THEN 10200
10080 IF X=20 THEN 10300
10090 IF X=30 THEN 10400
10100 PRINT :PRINT "THE TAXMAN COMETH !":CALL -1052:CALL -1052
10110 PRINT "THE FEDERAL GOVERNMENT HAS LEVIED A CAPITAL GAINS TAX !"
10120 PRINT "10 % OF YOUR CASH HAS JUST DISAPPEARED "
10130 FOR X=1 TO PN:PL(X*3-1)=PL(X*3-1)-PL(X*3-1)/10:NEXT X
10140 GOTO 10500
10200 PRINT :PRINT "SPECIAL DEPRECIATION ALLOWANCE !"
10210 PRINT "THE PRESIDENT IN HIS FIGHT AGAINST THE RECESSION, HAS GRANTED A SPECIAL DEPRECIATION ALLOWANCE"
10220 PRINT "COMPANIES WITH PRODUCTION UNITS HAVE JUST RECEIVED $5,000 CASH FOR EACH PRODUCTION UNIT"
10230 FOR X=1 TO (PN+1)
10240 CO(X*15-5)=CO(X*15-5)+5*CO(X*15-3):NEXT X
10250 GOTO 10500
10300 PRINT :PRINT "STOCK FRAUD DISCOVERED !"
10310 PRINT "WALL STREET IN A PANIC. THE MAFIA HAS BEEN ISSUING FALSE STOCK CERTIFICATES FOR YEARS."
10320 PRINT "EVERY PLAYER LOSES $10,000 CASH !"
10330 FOR X=1 TO PN:PL(X*3-1)=PL(X*3-1)-10:IF PL(X*3-1)<0 THEN PL(X*3-1)=0:NEXT X
10340 GOTO 10500
10400 PRINT :PRINT "ARABS SELL OIL FOR $5 A BARREL !"
10410 PRINT "PRINCE KAHLID-FUZZY, IN RETURN FOR NUCLEAR ARMS -FOR DEFENCE PURPOSES OF COURSE,- AGREES TO SELL HIS OIL FOR $5"
10420 PRINT "INDUSTRY PROSPERS !"
10430 PRINT "HEAVY TRADING ON MARKET - ALL STOCK PRICES UP $3.00"
10440 FOR X=1 TO (PN+1):CO(X*15)=CO(X*15)+3:CO(X*15-1)=CO(X*15-1)+3:NEXT X
10500 FOR X=1 TO 5000:NEXT X
10510 GOTO 7000
11000 CALL -936:VTAB (3):PRINT "PLAYER";:TAB 20:PRINT "NET WORTH":VTAB (5)
11010 FOR X=1 TO PN:PL(X*3-2)=0
11020 FOR Y=1 TO (PN+1)
11030 PL(X*3-2)=PL(X*3-2)+CO(Y*15-15+X)*CO(Y*15-1)
11040 NEXT Y
11050 PL(X*3-2)=PL(X*3-2)+PL(X*3-1)-PL(X*3)
11060 PRINT A$(PNP(X),PNP(X+1)-1);
11070 TAB 20:PRINT PL(X*3-2);"000"
11080 PRINT :NEXT X
11090 PRINT "END OF GAME ":END 
