10 I=0:J=0:K=0:L=0:M=0:N=0:R=0:DIM P(6),C(6)
20  POKE 50,63:TEXT :CALL -936
30 VTAB 1:TAB 2:PRINT "   ";:TAB 9:PRINT "    ";:TAB 17:PRINT "    ";:TAB 25:PRINT " ";:TAB 33:PRINT "     "
40 PRINT " ";:TAB 5:PRINT " ";:TAB 9:PRINT " ";:TAB 13:PRINT " ";:TAB 17:PRINT " ";:TAB 21:PRINT " ";:TAB 25:PRINT " ";:TAB 33:PRINT " "
50 PRINT "     ";:TAB 9:PRINT "    ";:TAB 17:PRINT "    ";:TAB 25:PRINT " ";:TAB 33:PRINT "   "
60 PRINT " ";:TAB 5:PRINT " ";:TAB 9:PRINT " ";:TAB 17:PRINT " ";:TAB 25:PRINT " ";:TAB 33:PRINT " "
70 PRINT " ";:TAB 5:PRINT " ";:TAB 7:PRINT " ";:TAB 9:PRINT " ";:TAB 15:PRINT " ";:TAB 17:PRINT " ";
80 TAB 23:PRINT " ";:TAB 25:PRINT "     ";:TAB 31:PRINT " ";:TAB 33:PRINT "     ";:TAB 39:PRINT " "
90  POKE 50,255:PRINT
100 PRINT "A       P       P       L       E":TAB 2:PRINT "P       U       R       I       X"
110 TAB 3:PRINT "P       G       O       B       C":TAB 4:PRINT "L       E       G       R       H"
120 TAB 5:PRINT "E       T       R       A       A":TAB 14:PRINT "S       A       R       N"
130 TAB 15:PRINT "O       M       Y       G":TAB 16:PRINT "U";:TAB 40:PRINT "E";
140 TAB 17:PRINT "N":TAB 18:PRINT "D"
150  POKE 50,63:PRINT :PRINT :TAB 10:PRINT "                   ":TAB 10:PRINT "  P R E S E N T S  ":TAB 10:PRINT "                   "
160  POKE 50,255
170 FOR I=1 TO 3000:NEXT I
180 TEXT :CALL -936:VTAB 3:TAB 10:PRINT "A.P.P.L.E.  PRESENTS:":VTAB 7:PRINT "COLOR DEMOS":VTAB 10
190 PRINT "1  SQUARES":PRINT "2  WORM":PRINT "3  MOSAIC":PRINT "4  APPLE"
200 PRINT "5  BLENDED COLORS":PRINT "6  COLOR SKETCH"
210 PRINT :PRINT :PRINT
220 PRINT "HIT ANY KEY FOR NEW DEMO":PRINT :PRINT "WHICH DEMO # ?";
230 KEY= PEEK (-16384):IF KEY<177 OR KEY>182 THEN 230: POKE -16368,0
240 PRINT KEY-176:IF KEY#182 THEN GR
250 GOTO (KEY-176)*1000
1000 COLOR=  RND (14)+1:J= RND (10)+1:K=J*14/10:M= RND (40-K):N= RND (40-J):HLIN N,N+J AT M:VLIN M,M+K AT N+J:HLIN N,N+J AT M+K:VLIN M,M+K AT N
1010 COLOR= 0:IF J<2 THEN 1000:FOR I=N+1 TO N+J-1:VLIN M+1,M+K-1 AT I:NEXT I:GOSUB 10000:GOTO 1000
2000 R=0:J= RND (40):K= RND (40)
2010 L= RND (3)-1:M= RND (3)-1:IF L=0 AND M=0 THEN 2010:Q= RND (15)+6:GOSUB 10000
2020 FOR N=1 TO Q:J1=J+L:K1=K+M:IF J1<0 OR J1>39 OR K1<0 OR K1>39 THEN 2010:IF J1=C(5) AND K1=P(5) THEN 2010
2030 J=J1:K=K1:COLOR=  RND (14)+1:PLOT J,K:COLOR= 0:IF R=6 THEN PLOT C(1),P(1)
2040 FOR I=1 TO 5:C(I)=C(I+1):P(I)=P(I+1):NEXT I:C(6)=J:P(6)=K
2050 IF R<6 THEN R=R+1:NEXT N:GOTO 2010
3000 I= RND (20)
3010 R= RND (16):FOR J=1 TO 19:FOR L=0 TO 19:K=J+L
3020 COLOR= L*3/(J+3)+J*I/12+R:PLOT J,K:PLOT K,J:PLOT 40-J,40-K:PLOT 40-K,40-J:PLOT K,40-J:PLOT 40-J,K:PLOT J,40-K:PLOT 40-K,J
3030 NEXT L,J:GOSUB 10000:I=I+1:IF I>20 THEN I=0:GOTO 3010
4000 COLOR= 12:VLIN 1,4 AT 24:VLIN 2,7 AT 23:VLIN 3,8 AT 22:VLIN 4,9 AT 21:VLIN 6,10 AT 20
4010 HLIN 13,16 AT 10:HLIN 24,27 AT 10:HLIN 12,18 AT 11:HLIN 22,28 AT 11:HLIN 11,29 AT 12:HLIN 11,29 AT 13
4020 COLOR= 13:FOR I=14 TO 17:HLIN 10,30 AT I:NEXT I
4030 FOR I=18 TO 25:IF I=19 THEN COLOR= 9:IF I=24 THEN COLOR= 1:HLIN 9,31 AT I:NEXT I
4040 FOR I=26 TO 30:IF I=29 THEN COLOR= 3:HLIN 10,30 AT I:NEXT I
4050 FOR I=31 TO 33:HLIN 11,29 AT I:NEXT I
4060 COLOR= 2:FOR I=34 TO 36:HLIN 12,28 AT I:NEXT I
4070 HLIN 13,27 AT 37:HLIN 14,19 AT 38:HLIN 21,26 AT 38:HLIN 15,18 AT 39:HLIN 22,25 AT 39
4080 GOSUB 10000:GOTO 4080
5000 COLOR= 1:FOR I=0 TO 7:VLIN 16,23 AT I:NEXT I
5010 FOR I=8 TO 15:COLOR= 3:VLIN 8,15 AT I:COLOR= 11:VLIN 16,23 AT I:COLOR= 9:VLIN 24,31 AT I:NEXT I
5020 FOR I=16 TO 23:COLOR= 2:VLIN 0,7 AT I:COLOR= 7:VLIN 8,15 AT I:COLOR= 15:VLIN 16,23 AT I:COLOR= 13:VLIN 24,31 AT I:COLOR= 8:VLIN 32,39 AT I:NEXT I
5030 FOR I=24 TO 31:COLOR= 6:VLIN 8,15 AT I:COLOR= 14:VLIN 16,23 AT I:COLOR= 12:VLIN 24,31 AT I:NEXT I
5040 COLOR= 4:FOR I=32 TO 39:VLIN 16,23 AT I:NEXT I
5050 GOSUB 10000:GOTO 5050
6000 TEXT :CALL -936:PRINT "***************************************"
6010 VTAB 3:TAB 12:PRINT "COLOR SKETCH II":VTAB 5:PRINT "****************************************"
6020 VTAB 8:PRINT "THIS PROGRAM ALLOWES YOU TO":PRINT "SKETCH COLORED FIGURES IN"
6030 PRINT "LOW RESOLUTION GRAPHICS USING":PRINT "PADDLE CONTROL"
6040 PRINT
6050 PRINT "OR LOAD PREVIOUSLY SAVED DISPLAYS FROM":PRINT "CASSETTE TAPE"
6060 PRINT :PRINT "DO YOU WISH TO LOAD A SAVED DISPLAY":PRINT "FROM TAPE (Y OR N) ?"
6070 KEY= PEEK (-16384):IF KEY<128 THEN 6070: POKE -16368,0
6080 IF KEY= ASC("Y") THEN 6320
6090 IF KEY# ASC("N") THEN 6070
6100 GR :CALL -936
6110 FOR I=4 TO 34 STEP 2:COLOR= I/2-2:PLOT I,39:NEXT I
6120 TAB 5:PRINT "A B C D E F G H I J K L M N O P":IF F#0 THEN 6160
6130 PRINT "BACKGROUND COLOR ?"
6140 KEY= PEEK (-16384):IF KEY<193 OR KEY>208 THEN 6140:COL=KEY-193:COLOR= COL: POKE -16368,0
6150 FOR I=0 TO 38:HLIN 0,39 AT I:NEXT I
6160 VTAB 22:PRINT "TYPE LETTER TO CHANGE COLOR,":PRINT "'S' TO SAVE, 'X' TO CLEAR."
6170 PRINT "AND 'Q' TO QUIT";:F=0
6180 J=PDL (0):IF J<(L+1)*6+2 AND J>L*6-2 THEN 6190:L=J/6
6190 K=PDL (1):IF K<(M+1)*6+2 AND K>M*6-2 THEN 6200:M=K/6
6200 IF L>39 THEN L=39:IF M>38 THEN M=38
6210 IF N=L AND R=M THEN 6290
6220 COLOR= 15:IF COL=15 THEN COLOR= 5:PLOT L,M
6230 TAB 30:CALL -868:PRINT "X=";L;:TAB 35:PRINT "Y=";M;
6240 COLOR= COL:IF N=L THEN 6250:IF R=M THEN 6270:PLOT N,R:GOTO 6290
6250 IF M>R THEN 6260:VLIN M+1,R AT L:GOTO 6290
6260 VLIN R,M-1 AT L:GOTO 6290
6270 IF L>N THEN 6280:HLIN L+1,N AT M:GOTO 6290
6280 HLIN N,L-1 AT M
6290 N=L:R=M:KEY= PEEK (-16384): POKE -16368,0:IF KEY= ASC("Q") THEN 20:IF KEY= ASC("S") THEN 6310:IF KEY= ASC("X") THEN 6100
6300 IF KEY<193 OR KEY>208 THEN 6180:COL=KEY-193:GOTO 6180
6310 GOSUB 6330:GOTO 6110
6320 GOSUB 6350:F=1:GOTO 6110
6330 CALL -936:PRINT "REWIND TAPE, THEN START RECORDER":PRINT "IN RECORD MODE":PRINT "AND HIT RETURN":GOSUB 6370:CALL -936
6340  POKE 60,0: POKE 61,4: POKE 62,0: POKE 63,8:CALL -307:RETURN
6350 GR :CALL -936:PRINT "REWIND TAPE, THEN START RECORDER":PRINT "IN PLAY MODE":PRINT "AND HIT RETURN":GOSUB 6370:CALL -936
6360  POKE 60,0: POKE 61,4: POKE 62,0: POKE 63,8:CALL -259:RETURN
6370 IF  PEEK (-16384)#141 THEN 6370: POKE (-16368),0:RETURN
10000 IF  PEEK (-16384)<128 THEN RETURN : POKE -16368,0:POP :GOTO 20
65535 REM COPYRIGHT (C) 1978 BY APPLE PUGETSOUND PROGRAM LIBRARY EXCHANGE
