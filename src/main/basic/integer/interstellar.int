1 GOSUB 3000
5 DIM P(10)
6 DIM A$(40)
10 PRINT 
20 PRINT 
30 PRINT 
40 PRINT 
50 PRINT 
90 PRINT "DO YOU WANT INSTRUCTIONS?";
100 INPUT A$:IF A$(1,1)="N" THEN 480:IF A$(1,1)="Y" THEN 105:GOTO 90
105 PRINT :PRINT "IF YOU ARE READING THIS, ASSUME TERRA HAS BEEN OVERCOME, AND THE CONSORTIUM IS IN GRAVE DANGER."
108 FOR T=1 TO 1500:NEXT T:FOR T=1 TO 28:FOR G=1 TO 100:NEXT G:PRINT :NEXT T:CALL -936
110 PRINT "YOU ARE ABOUT TO RECIEVE A MAXIMUM      SECURITY INFORMATIONAL TRANSMISSION...":FOR T=1 TO 2000:NEXT T
112  POKE 35,5:CALL -936: POKE 35,23
130 PRINT :PRINT :PRINT 
140 PRINT "THIS IS THE YEAR "; RND (250)+2000;" ...CIVILISATION ON SOL III HAS"
150 PRINT "BEEN DESTROYED..."
160 PRINT "IN A MASSIVE INTERSTELLAR WAR"
170 PRINT "YOU ARE ON BOARD A STARSHIP WHOSE PRIMARY OBJECTIVE IS"
180 PRINT "TO SAFEGUARD THE 1500 SUBJECTS IN       THEIR HIBERNACULAE,"
190 PRINT "WHO ARE THE SOLE REMNANTS OF HUMANITY. "
195 FOR T=1 TO 8000:NEXT T
200 GOSUB 4000
219 VTAB 12
225 FOR TIME=1 TO 3500:NEXT TIME:CALL -936
230 TAB 16: POKE 50,63:PRINT " ";: POKE 50,255:PRINT " DATA ";: POKE 50,63:PRINT " ": POKE 50,255
260 PRINT :PRINT : POKE 50,63:TAB 14:PRINT " ";: POKE 50,255:PRINT " WEAPONRY ";: POKE 50,63:PRINT " ": POKE 50,255
270 PRINT :PRINT "TYPE";:TAB 6:PRINT "DESCRIPTION";:TAB 19:PRINT "RANGE";:TAB 29:PRINT "FUEL DRAIN"
275 PRINT :PRINT 
280 PRINT "  1";:TAB 6:PRINT "155MM GUNS";:TAB 19:PRINT "0-1000"
290 PRINT "  2";:TAB 6:PRINT "N-MISSLES";:TAB 16:PRINT "1000-10000  "
300 PRINT "  3";:TAB 6:PRINT "LASER";:TAB 15:PRINT "10000-20000";:TAB 29:PRINT "100 UNITS"
310 PRINT : POKE 50,63:TAB 15:PRINT " ";: POKE 50,255:PRINT " OPTIONS ";: POKE 50,63:PRINT " ": POKE 50,255:PRINT 
320 PRINT "  4";:TAB 6:PRINT "APPROACH";:TAB 29:PRINT "100 UNITS"
330 PRINT "  5";:TAB 6:PRINT "RETREAT";:TAB 29:PRINT "100 UNITS"
340 PRINT :PRINT "  6  BY TYPING '6' YOU CAN PASS, THEREBY":PRINT "GAINING ENERGY (SEE SEC. 14/51-01":PRINT "FOR DETAILS)"
350 PRINT 
351 IF U>1 THEN 523
355 GOSUB 2000
360 TAB 12:PRINT "ENEMY"
361  POKE 50,255
370 PRINT :PRINT "UNFORTUNATELY, NOT MUCH IS KNOWN OF":PRINT "YOUR ADVERSARIES OTHER THAN"
372 PRINT "THEIR EQUIPTMENT IS SOMEWHAT LIKE YOURS"
380 PRINT "EACH TIME A SHIP IS HIT, ITS ENERGY":PRINT "DRAIN IS EQUAL TO"
390 PRINT "THE AMOUNT OF ENERGY SPENT *10":PRINT "EXCEPT LASER WHICH EQUALS"
400 TAB 12:PRINT "THE AMOUNT SPENT *3 UNITS"
410 PRINT "ALL SHIPS ARE ON EQUAL TERMS"
430 PRINT "UNFORTUNATELY YOU MUST KILL THEM ":PRINT "TO SURVIVE"
440 PRINT "YOUR MAXIMUM JUMP IN ANY DIRECTION IS ":PRINT "5000 A.U., MINIMUM JUMP IS 100 A.U."
460 PRINT "N-MISSLES TRAVEL AT 3500KM/SEC... ":PRINT "155MM SHELLS AT 100KM/SEC"
468 FOR T=1 TO 18000:NEXT T:CALL -936:VTAB 7
470 PRINT "THIS MESSAGE WAS RECORDED...":PRINT "THE CONSORTIUM MUST CONTINUE... ":PRINT "YOU HAVE YOUR INSTRUCTION.":TAB 12:PRINT "EXECUTE"
471 PRINT 
475 FOR T=1 TO 4500:NEXT T
480 CALL -936:VTAB 5:PRINT " HAL 9500 AT YOUR COMMAND.":INPUT " YOUR NAME, SIR?",A$
490 PRINT :PRINT :PRINT "VERY GOOD, ";A$;"."
500 P=1000:P(1)=1000:A= RND (20000)
510 PRINT "LEAVING PLANETARY ORBIT, ";A$:PRINT "SENSORS REPORT ENEMY SHIP APPROACHING    AT ";A;" A.U."
519 IF A<1 THEN 770
520 PRINT :PRINT "WHAT ARE YOUR ORDERS, ";A$:GOSUB 5500:PRINT :INPUT C
521 IF C=7 THEN 2500
523 U=0:IF C=7 THEN 520
530 O= RND (2)+1
540 IF C=1 THEN 570:IF C=2 THEN 650:IF C=3 THEN 710:IF C=4 THEN 750:IF C=5 THEN 780:IF C=6 THEN 820
560 PRINT " ";A$;" NOW LET'S NOT CRACK UNDER PRESSURE":GOTO 520
570 IF A>1005 THEN 560
580 P=P-1:PRINT :PRINT : POKE 50,63:PRINT "<CTRL-G><CTRL-G><CTRL-G> GUNS FIRED": POKE 50,255
590 FOR X=1 TO A*2 STEP 100
600 FOR T=1 TO 100:NEXT T:NEXT X
610 IF O=1 THEN 630
620 PRINT :PRINT "MISSED... TOO BAD":GOTO 830
630 PRINT :PRINT "<CTRL-G><CTRL-G><CTRL-G><CTRL-G><CTRL-G>DIRECT HIT!!! ENEMY SHIPS POWER DOWN"
640 P(1)=P(1)-10:GOTO 830
650 IF A>10000 THEN 560:IF A<1000 THEN 560
660 P=P-10:PRINT :PRINT : POKE 50,63:PRINT "<CTRL-G> WARHEAD LAUNCHED": POKE 50,255:FOR X=1 TO A*2 STEP 3500
670 FOR T=1 TO 100:NEXT T:NEXT X
680 IF O=1 THEN 690:PRINT :PRINT "MISSED... TOO BAD":GOTO 830
690 PRINT :PRINT "<CTRL-G><CTRL-G><CTRL-G><CTRL-G><CTRL-G>DIRECT HIT!!! ENEMY SHIPS POWER DOWN"
700 P(1)=P(1)-100:GOTO 830
710 IF A<10000 THEN 560:P=P-100:PRINT :PRINT : POKE 50,63:PRINT "<CTRL-G>LAZER FIRED": POKE 50,255:FOR T=1 TO 300:NEXT T
720 IF O=1 THEN 730:PRINT :PRINT "MISSED... TOO BAD":GOTO 830
730 PRINT "<CTRL-G><CTRL-G><CTRL-G><CTRL-G><CTRL-G>DIRECT HIT!!! ENEMY SHIPS POWER DOWN"
740 P(1)=P(1)-300:GOTO 830
750 B= RND (4000)+1000:A=A-B:P=P-10:IF A<1 THEN 770
760 GOTO 830
770 CALL -936:VTAB 12:TAB 10:PRINT "***COLLISION***":TAB 7:PRINT "BOTH SHIPS DESTROYED!!!":GOTO 1080
780 B= RND (4000)+1000:A=A+B:P=P-10:IF A>20050 THEN 800
790 GOTO 830
800 PRINT A$;" YOUR RANGE IS ";A;", BUT WE CANNOT RUN, RANGE IS NOW 20000"
810 A=20000:GOTO 830
820 P=P+ RND (10)+10
830 PRINT :PRINT "<CTRL-G><CTRL-G><CTRL-G>ENEMY SHIP REPORT...":PRINT "RANGE= ";A;"  POWER= ";P(1)
840 PRINT :PRINT :PRINT :IF P(1)<1 THEN 1110
850 IF P(1)<50 THEN 1040
860 IF A<500 THEN 1070
870 R= RND (3)+1:O= RND (2)+1
880 IF R=1 THEN 970:IF R=2 THEN 920
890 IF A<10000 THEN 870:P(1)=P(1)-100:PRINT :PRINT "<CTRL-G><CTRL-G>ENEMY FIRES LAZER"
900 IF O=1 THEN 910:PRINT :PRINT "MISSED... WHEW!!":GOTO 1010
910 PRINT :PRINT "<CTRL-G><CTRL-G><CTRL-G><CTRL-G><CTRL-G><CTRL-G><CTRL-G><CTRL-G>DIRECT HIT!! POWER DOWN":P=P-300:GOTO 1010
920 IF A>10000 THEN 870:IF A<1000 THEN 870
930 P(1)=P(1)-10:PRINT :PRINT "<CTRL-G><CTRL-G>ENEMY WARHEAD FIRED"
940 FOR D=1 TO A STEP 3500:FOR T=1 TO 100:NEXT T:NEXT D
950 IF O=1 THEN 960:PRINT :PRINT "MISSED... WHEW":GOTO 1010
960 P=P-100:PRINT :PRINT "<CTRL-G><CTRL-G><CTRL-G><CTRL-G><CTRL-G><CTRL-G><CTRL-G><CTRL-G><CTRL-G><CTRL-G>DIRECT HIT!! POWER DOWN":GOTO 1010
970 IF A>1100 THEN 870:P(1)=P(1)-1:PRINT :PRINT "<CTRL-G><CTRL-G>ENEMY FIRES SHELL"
980 FOR D=1 TO A STEP 100:FOR T=1 TO 100:NEXT T:NEXT D
990 IF O=1 THEN 1000:PRINT :PRINT "MISSED... WHEW!!":GOTO 1010
1000 PRINT :PRINT "DIRECT HIT!!! POWER DOWN":P=P-10
1010 PRINT :PRINT :PRINT :PRINT "<CTRL-G>STATUS OF SHIP"
1020 PRINT :PRINT "RANGE= ";A;"    POWER SUPPLY= ";P:IF P<1 THEN 1129
1029 IF A<1 THEN 770
1030 I= RND (4):IF I=0 THEN 1040:IF I=1 THEN 1050:IF I=2 THEN 1070:GOTO 519
1040 P(1)=P(1)+ RND (20):PRINT :PRINT "<CTRL-G><CTRL-G>ENEMY SHIP RESTING":GOTO 519
1050 B= RND (4000)+1000:A=A-B:PRINT :PRINT "ENEMY SHIP APPROACHING, ";A$;".":IF A<1 THEN 3500:GOTO 519
1070 IF A>20000 THEN 519:B= RND (4000)+1000:A=A+B:PRINT "<CTRL-G><CTRL-G>ENEMY SHIP RETREATING":GOTO 519
1080 PRINT :PRINT :PRINT "THAT WAS A PRETTY DUMB THING TO DO!!!!!!";A$
1090 PRINT :PRINT "YOUR MISSION IS TO PROTECT YOUR PASSEN- GERS, NOT TO DESTROY!!!"
1100 GOTO 1140
1110 CALL -936:VTAB 12:PRINT "ENEMY SHIPS POWER GONE...":PRINT :PRINT "NO LIFE PRESENT"
1120 PRINT :PRINT "MISSION SUCESSFUL         ":FOR T=1 TO 2500:NEXT T:GOTO 1150
1129 CALL -936:VTAB 12
1130 PRINT "<CTRL-G><CTRL-G><CTRL-G><CTRL-G>THE ENEMY HAS WON!!!":PRINT :PRINT :PRINT "LIFE SUPPORT HAS FAILED, ESTIMATE ";
1135 T= RND (50)+1:PRINT T;:PRINT " MINUTES OF LIFE LEFT..."
1140 FOR T=1 TO 6000:NEXT T
1150 CALL -936:VTAB 5:INPUT "PLAY AGAIN?",A$
1200 IF A$(1,1)="Y" THEN 10
1210 PRINT :PRINT :PRINT "THIS LITTLE EXERSISE IN FUTILITY WAS BROUGHT TO YOU BY:":FOR T=1 TO 2000:NEXT T:PRINT :PRINT :TAB 10:PRINT "DOUG MCINTOSH"
1211 PRINT :TAB 17:PRINT "AND":PRINT :TAB 10:PRINT "RYAN CORMAN"
1220 END 
2000  POKE 50,63:INPUT "HIT 'RETURN' FOR NEXT PAGE.",A$: POKE 50,255:CALL -936:RETURN 
2500 CALL -936:U=5:GOTO 260
3000 CALL -936:VTAB 10
3010 TAB 13:PRINT "INTERSTELLAR"
3030 RETURN 
3500 CALL -936:VTAB 10:TAB 15:PRINT "***COLLISION***":PRINT "THE ENEMY HAS MADE A SUCCESSFUL SUICIDE ":PRINT "BID, AND, ";:GOTO 1130
4000 PRINT :PRINT :PRINT :PRINT 
4010 PRINT "P";:GOSUB 5000:PRINT "R";:GOSUB 5000:PRINT "O";
4011 GOSUB 5000:PRINT "B";:GOSUB 5000:PRINT "L";:GOSUB 5000:PRINT "E";:GOSUB 5000:PRINT "M";
4020 PRINT ":";:GOSUB 5010:PRINT " THE ANCIENT ENEMIES OF":PRINT "THE CONSORTIUM STILL EXIST, AND"
4022 PRINT "MUST BE ANNIHILATED..."
4025 FOR T=1 TO 4000:NEXT T
4030 PRINT :PRINT :PRINT :PRINT :PRINT "M";:GOSUB 5000:PRINT "I";:GOSUB 5000:PRINT "S";
4032 GOSUB 5000:PRINT "S";:GOSUB 5000:PRINT "I";:GOSUB 5000
4040 PRINT "O";:GOSUB 5000:PRINT "N";:GOSUB 5000:PRINT ":";:GOSUB 5010:PRINT " ENGAGE AND NEUTRALIZE"
4050 RETURN 
5000 J= PEEK (-16336):J= PEEK (-16336):FOR T=1 TO 100:NEXT T:RETURN 
5010 FOR T=1 TO 1500:NEXT T:RETURN 
5500 VTAB 22: POKE 50,63:PRINT " ";: POKE 50,255:PRINT " FOR A SUMMARY OF COMMANDS, TYPE '7' ";: POKE 50,63:PRINT " ": POKE 50,255:RETURN 
