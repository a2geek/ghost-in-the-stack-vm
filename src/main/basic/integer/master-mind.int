0 REM  GAME OF MASTERMIND 1-16-78 WOZ (APPLE COMPUTER)
10 DIM A(6),C(8),D(5),X(8),X$(8):X(1)=2:X(2)=12:X(3)=1:X(4)=13:X(5)=3:X(6)=9:X(7)=15:X(8)=5:X$="BGRYVOWX"
20 TEXT :CALL -936:PRINT "WELCOME TO THE GAME OF MASTERMIND! YOUR OBJECT IS TO GUESS 5 COLORS(WHICH"
30 PRINT "I WILL MAKE UP) IN THE MINIMUM NUMBER OF GUESS. THERE ARE EIGHT DIFFERENT COLORS TO CHOSE FROM."
40 PRINT "FEWER THAN 7 GUESS --EXCELLENT":PRINT "  7 TO 9 GUESSES----GOOD":PRINT "  10 TO 14 GUESSES----AVERAGE"
50 PRINT "MORE THAN 14 GUESSES--POOR":CALL -384:TAB 7:PRINT "HIT ANY KEY TO BEGIN PLAY"
100 CALL -380:IF  PEEK (-16384)<132 THEN 100: POKE -16368,0:GR :PRINT :FOR I=1 TO 8:C(I)= RND (8)+1:COLOR= X(I):HLIN I*4-2,I*4 AT 39:PRINT "   ";X$(I,I);:NEXT I
110 TRY=0:PRINT :PRINT " LETTERKEYS FOR COLOR CHANGE":PRINT " ALLOW KEYS FOR ADVANCE AND BACK":PRINT " HIT RETURN TO ACCEPT GUESS#";
200 Y=TRY*2 MOD 36+1:TRY=TRY+1:TAB 32:PRINT TRY;:COLOR= 0:HLIN 0,39 AT Y:FLASH=1:FOR N=1 TO 5:A(N)=8:GOSUB 1000:NEXT N:N=1
300 FOR WAIT=1 TO 10:KEY= PEEK (-16384):IF KEY<132 THEN 310: POKE -16368,0:FLASH=1:FOR I=1 TO 8:IF KEY<> ASC(X$(I)) THEN NEXT I:IF I=9 THEN 310:A(N)=I:KEY=149
310 GOSUB 1000:IF KEY=141 THEN 400:IF KEY=136 AND N>1 OR KEY=149 AND N<6 THEN N=N+KEY/5-28:NEXT WAIT:FLASH=1-FLASH:GOTO 300
400 COLOR= 15:M=0:FOR I=1 TO 5:D(I)=C(I):J=I:GOSUB 2000:NEXT I:IF M=5 THEN 500:COLOR= 5:FOR J=1 TO 5:FOR I=1 TO 5:GOSUB 2000:NEXT I,J:GOTO 200
500 PRINT :PRINT "YOU GOT IT IN";TRY;"TRIES (";:IF TRY<7 THEN PRINT "EXCELLENT";:IF TRY>6 AND TRY<10 THEN PRINT "GOOD";
510 IF TRY>9 AND TRY<15 THEN PRINT "AVERAGE";:IF TRY>14 THEN PRINT "POOR";:PRINT ")":CALL -384:TAB 5:PRINT "HIT ANY KEY TO PLAY AGAIN":GOTO 100
1000 IF N=6 THEN RETURN :COLOR= X(A(N))*FLASH:HLIN N*4-2,N*4 AT Y:RETURN 
2000 IF A(I)<>D(J) THEN RETURN :M=M+1:PLOT 21+M+M,Y:PRINT "";:A(I)=0:D(J)=9:RETURN 
3000 REM  CALL -384 SETS INVERSE VID
3010 REM  CALL -380 SETS NORMAL VID
3020 REM  PEEK(-16384) IS KBD(ASCII) (IF > 127 THEN STROBE SET)
3030 REM  POKE-16368 CLRS KBD STROBE
3040 REM  CALL-936 CLEARS SCREEN AND TABS CURSOR TO UPPER LEFT.
3050 REM  IN 310,KEY/5-28=-1 OR +1 (ARROW KEY=136 OR 149 ASCII)
4000 REM  STMTS 10-50 INTRO
4010 REM  STMTS 100-110 NEW SETUP
4020 REM  STMT 200 NEW GUESS
4030 REM  STMTS 300-310 USER INPUT
4040 REM  STMT 400 GUESS EVAL
4050 REM  STMTS 500-510 WIN
4060 REM  SUBR 1000 COLOR LINE
4070 REM  SUBR 2000 MATCH TEST
