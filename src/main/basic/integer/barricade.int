90 GOSUB 370
99  REM _
    Modification: Sound routine was in ZP @ $02. _
    Impacted interpreter and had trouble moving the interpreter where Applesoft _
    did not impact. So moving the routine to $300. _
        0303-   AD 30 C0    LDA   $C030 _
        0306-   AD 00 03    LDA   $0300 _
        0309-   20 A8 FC    JSR   $FCA8 _
        030C-   AD 01 03    LDA   $0301 _
        030F-   D0 05       BNE   $0316 _
        0311-   CE 02 03    DEC   $0302 _
        0314-   F0 06       BEQ   $031C _
        0316-   CE 01 03    DEC   $0301 _
        0319-   4C 03 03    JMP   $0303 _
        031C-   60          RTS
100  POKE 0x303,0xad: POKE 0x304,0x30: POKE 0x305,0xc0: _
     POKE 0x306,0xad: POKE 0x307,0x00: POKE 0x308,0x03: _
     POKE 0x309,0x20: POKE 0x30a,0xa8: POKE 0x30b,0xfc: _
     POKE 0x30c,0xad: POKE 0x30d,0x01: POKE 0x30e,0x03
110  POKE 0x30f,0xd0: POKE 0x310,0x05: _
     POKE 0x311,0xce: POKE 0x312,0x02: POKE 0x313,0x03: _
     POKE 0x314,0xf0: POKE 0x315,0x06: _
     POKE 0x316,0xce: POKE 0x317,0x01: POKE 0x318,0x03: _
     POKE 0x319,0x4c: POKE 0x31a,0x03: POKE 0x31b,0x03: _
     POKE 0x31c,0x60
120 KR=-16368:KY=-16384:SP=-16336
130 CALL -936:GR :COLOR= 5:VLIN 0,39 AT 0:HLIN 0,39 AT 0:VLIN 0,39 AT 39:HLIN 0,39 AT 39
140 W= ASC("W"):A= ASC("A"):X= ASC("X"):D= ASC("D"):I= ASC("I"):J= ASC("J"):M= ASC("M"):L= ASC("L")
150 IF GN>4 THEN 330:GN=GN+1
160 VTAB 21:PRINT "***HIT ANY KEY TO START GAME***"
170 IF  PEEK (KY)<128 THEN 170: POKE KR,0:CALL -936
180 V1=21:V2=19:H1=1:H2=37:K1=D:K2=J
190 K= PEEK (KY): POKE KR,0
200 IF K=A OR K=W OR K=D OR K=X THEN K1=K:IF K=I OR K=L OR K=M OR K=J THEN K2=K
210 V1=V1+2*((K1=X)-(K1=W)):V2=V2+2*((K2=M)-(K2=I)):H1=H1+2*((K1=D)-(K1=A)):H2=H2+2*((K2=L)-(K2=J))
220 IF V1<1 OR V1>37 OR H1<1 OR H1>37 THEN 280:IF V2<1 OR V2>37 OR H2<1 OR H2>37 THEN 290
230 IF  SCRN(H1,V1)#0 THEN 280:IF  SCRN(H2,V2)#0 THEN 290
240  POKE 0x301,100: POKE 0x302,1: POKE 0x300,8:CALL 0x303
250 COLOR= 3:HLIN H1,H1+1 AT V1:HLIN H1,H1+1 AT V1+1:COLOR= 9:HLIN H2,H2+1 AT V2:HLIN H2,H2+1 AT V2+1
260  POKE 0x301,100: POKE 0x302,2: POKE 0x300,7:CALL 0x303
270 GOTO 190
280 CALL -936:PRINT "***** PLAYER #2 WINS *****":P2=P2+1:GOSUB 320:GOTO 300
290 CALL -936:PRINT "***** PLAYER #1 WINS *****":P1=P1+1:GOSUB 320
300 FOR QQ=1 TO 1000:NEXT QQ
310  POKE KR,0:GOTO 130
320 FOR A=1 TO 10:P= PEEK (SP):NEXT A:FOR A=1 TO 100:P= PEEK (SP):NEXT A:RETURN
330 TEXT :CALL -936:VTAB 12:TAB 10:PRINT "<<<< GAME OVER >>>>":VTAB 20
340 PRINT
350 PRINT "   PLAYER #1 : ";P1;"   TO  PLAYER #2 : ";P2
360 END
370 CALL -936:PRINT :PRINT "   ***  BARRICADE  ***"
380 PRINT :PRINT :PRINT :PRINT
390 PRINT " THE OBJECT IS TO ADVANCE, ":PRINT
400 PRINT " MAKING YOUR BARRICADE.":PRINT
410 PRINT :PRINT :PRINT :PRINT
420 PRINT "PLAYER #1        PLAYER #2":PRINT
430 PRINT
440 PRINT "   W                 I"
450 PRINT "A     D           J     L"
460 PRINT "   X                 M"
469 REM *** FIXME - should be A$ not A!
470 VTAB 24:INPUT "   HIT RETURN TO CONTINUE",A:RETURN
