0 REM  HANGMAN BY WENDELL BITTER.
1 REM          7/7/77
2 REM  TO CHANGE/ADD WORDS, SEE
3 REM  STATEMENTS 20000-30000 & 7
4 DIM A$(20):DIM G$(5)
5 CALL -936
6 COUNT=0
7 GOSUB  RND (50)+20000
10 GR 
20 COLOR= 8
30 FOR I=28 TO 33
40 HLIN 3,11 AT I
50 NEXT I
60 COLOR= 9
70 HLIN 8,28 AT 9:HLIN 8,28 AT 10
80 VLIN 9,27 AT 7:PLOT 11,11:PLOT 10,12:PLOT 9,13:PLOT 8,14
90 COLOR= 13
100 VLIN 11,14 AT 27
130 N=0
140 VTAB 21:FOR X=1 TO  LEN(A$):TAB X*2:PRINT "<CTRL-G>-";:NEXT X
160 VTAB 22
170 TAB 1:CALL -868:TAB 15:PRINT "YOUR GUESS > ";
171 INPUT G$
175 IF  LEN(G$)#1 THEN 6000
180 GOSUB 1000
190 GOTO 160
1000 HIT=0
1001 G= ASC(G$)
1002 FOR K=1617 TO 1617+2* LEN(A$):IF G= PEEK (K) THEN 5000:NEXT K
1004 FOR L=1872 TO 1872+N:IF G= PEEK (L) THEN 5000:NEXT L
1010 FOR I=1 TO  LEN(A$)
1020 IF G$#A$(I,I) THEN 1050
1030 HIT=1
1040 VTAB 21:TAB I*2:PRINT G$;:PRINT "<CTRL-G>"
1045 COUNT=COUNT+1
1047 IF COUNT= LEN(A$) THEN 31000
1050 NEXT I
1060 IF NOT HIT THEN GOSUB 2000
1065 FOR T=1 TO 200:NEXT T
1070 RETURN 
2000 REM  INCORREXCT GUESS
2001 FOR T=1 TO 25
2002 M= PEEK (-16336)
2003 NEXT T
2010 N=N+1
2020 VTAB 23:TAB N:PRINT G$;:
2030 GOTO N*1000+10000
5000 VTAB 22:CALL -868:PRINT "YOU'VE ALREADY GUESSED THAT! GUESS > ";:GOTO 171
6000 VTAB 22:CALL -868:PRINT "INVALID GUESS, TRY AGAIN > ";:GOTO 171
11000 COLOR= 15:HLIN 26,28 AT 15:HLIN 25,29 AT 16:HLIN 25,29 AT 17:HLIN 26,28 AT 18:RETURN 
12000 COLOR= 1:PLOT 26,16:PLOT 28,16:RETURN 
13000 COLOR= 2:PLOT 27,18:RETURN 
14000 COLOR= 15:PLOT 27,19:RETURN 
15000 COLOR= 4:FOR L=26 TO 28:VLIN 20,26 AT L:NEXT L:HLIN 25,29 AT 27:RETURN 
16000 COLOR= 15:VLIN 20,21 AT 25:VLIN 20,25 AT 24:RETURN 
17000 VLIN 20,21 AT 29:VLIN 20,25 AT 30:RETURN 
18000 VLIN 28,33 AT 25:VLIN 28,33 AT 26:HLIN 23,24 AT 33:RETURN 
19000 VLIN 28,33 AT 28:VLIN 28,33 AT 29:HLIN 30,31 AT 33
19010 VLIN 28,33 AT 29
19020 HLIN 30,31 AT 33
19030 FOR T=1 TO 300:NEXT T
19033 VTAB 22:TAB 1:CALL -868:TAB 15:PRINT "YOU DIE!!!"
19035 FOR G=1 TO 400
19040 M= PEEK (-16336)
19050 NEXT G
19075 CALL -868:FOR T=1 TO 300:NEXT T
19080 PRINT "THE WORD IS ";:PRINT A$;:PRINT "<CTRL-G>"
19090 GOTO 31005
20000 A$="HANG":RETURN 
20001 A$="BEEP":RETURN 
20002 A$="APPLE":RETURN 
20003 A$="COLOR":RETURN 
20004 A$="BOTTLE"
20005 A$="MISTAKE":RETURN 
20006 A$="QUICKLY":RETURN 
20007 A$="XYLOPHONE":RETURN 
20008 A$="ELEPHANT":RETURN 
20009 A$="WORD":RETURN 
20010 A$="CRUNCH":RETURN 
20011 A$="EXPLODE":RETURN 
20012 A$="VOLCANO":RETURN 
20013 A$="PUZZLE":RETURN 
20014 A$="FINGER":RETURN 
20015 A$="DEMONSTRATE":RETURN 
20016 A$="LANGUAGE":RETURN 
20017 A$="NITROGEN":RETURN 
20018 A$="OTHER":RETURN 
20019 A$="PHONY":RETURN 
20020 A$="BOX":RETURN 
20021 REM  SEE ALSO STATEMENT 7
20022 A$="VACUUM":RETURN 
20023 A$="CERTIFICATE":RETURN 
20024 A$="DISK":RETURN 
20025 A$="DRAFT":RETURN 
20026 A$="BASEBALL":RETURN 
20027 A$="POLICE":RETURN 
20028 A$="FOOTBALL":RETURN 
20029 A$="SHELL":RETURN 
20030 A$="RADIO":RETURN 
20031 A$="SCOPE":RETURN 
20032 A$="BAND":RETURN 
20033 A$="LAWYER":RETURN 
20034 A$="LEAGUE":RETURN 
20035 A$="GASOLINE":RETURN 
20036 A$="STAR":RETURN 
20037 A$="COLOR":RETURN 
20038 A$="SUPER":RETURN 
20039 A$="MONKIES":RETURN 
20040 A$="TEXAS":RETURN 
20041 A$="FLORIDA":RETURN 
20042 A$="CHEWER":RETURN 
20043 A$="SUPERIOR":RETURN 
20044 A$="INTERNATIONAL":RETURN 
20045 A$="HIGH":RETURN 
20046 A$="BOOTS":RETURN 
20047 A$="HORSE":RETURN 
20048 A$="SCHOOL":RETURN 
20049 A$="CHRISTMAS":RETURN 
20050 A$="PICTURE":RETURN 
31000 CALL -868:FOR T=1 TO 300:NEXT T:PRINT "   YOU L<CTRL-G>I<CTRL-G>V<CTRL-G>E<CTRL-G>...";:FOR T=1 TO 500:NEXT T:PRINT "   FOR NOW...<CTRL-G>"
31005 FOR T=1 TO 2000
31006 NEXT T
31010 GOTO 5
32000 END 
32600 END 
