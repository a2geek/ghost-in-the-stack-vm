1300 DIM A$(10)
1301 DIM M$(48)
1302 GOTO 2000
1303 REM 
1304 REM 
1400 DX=OFF:DY=OFF
1410 COLOR= OFF:PLOT TX,TY
1415 IF T1>OFF THEN 1550
1420 IF TX=PX THEN 1500
1430 DX=SGN (PX-TX)
1440 IF  SCRN(TX+DX,TY)<>CC THEN 1700
1445 DX=OFF
1500 IF TY=PY THEN 1540
1510 DY=SGN (PY-TY)
1520 IF  SCRN(TX,TY+DY)<>CC THEN 1700
1530 DY=OFF
1540 T1=OFF
1550 D=C3* RND (TWO):ROT=SGN (TWO-D)
1560 T1=(T1+ONE) MOD C6
1600 GOTO 1610+C20*D
1610 DX=ONE:IF  SCRN(TX+DX,TY)<>CC THEN 1700
1620 DX=OFF:D=D+ROT
1630 DY=ONE:IF  SCRN(TX,TY+DY)<>CC THEN 1700
1640 D=D+ROT:DY=OFF
1650 DX=-ONE:IF  SCRN(TX+DX,TY)<>CC THEN 1700
1660 D=D+ROT:DX=OFF
1670 DY=-ONE:IF  SCRN(TX,TY+DY)<>CC THEN 1700
1680 DY=OFF
1700 TX=TX+TWO*DX:TY=TY+TWO*DY
1710 IF TX=PX AND TY=PY THEN 7000
1720 COLOR= CT:PLOT TX,TY
1730 IF  RND (H)>OFF THEN 1790
1740 COLOR= OFF:GOTO 1741+ RND (C4)
1741 IF TX>ONE THEN PLOT TX-ONE,TY:GOTO 1790
1742 IF TY>ONE THEN PLOT TX,TY-ONE:GOTO 1790
1743 IF TX<C37 THEN PLOT TX+ONE,TY:GOTO 1790
1744 IF TY<C37 THEN PLOT TX,TY+ONE:GOTO 1790
1790 RETURN 
1800 M= PEEK (K1): POKE K2,OFF
1802 X=PX:Y=PY
1810 IF M=CU AND  SCRN(PX,PY-ONE)=OFF THEN PY=PY-TWO
1820 IF M=CD AND  SCRN(PX,PY+ONE)=OFF THEN PY=PY+TWO
1830 IF M=CL AND  SCRN(PX-ONE,PY)=OFF THEN PX=PX-TWO
1840 IF M=CR AND  SCRN(PX+ONE,PY)=OFF THEN PX=PX+TWO
1845 COLOR= OFF:PLOT X,Y
1850 IF  SCRN(PX,PY)=CG THEN GOSUB 4000
1860 COLOR= CP:IF G=OFF THEN 1870
1861 COLOR= CG:IF PX>ONE THEN 1870
1862 IF PY=ONE THEN 6000
1870 IF  SCRN(PX,PY)=CT THEN 7000
1880 PLOT PX,PY
1889 IF  RND (C3)>OFF THEN 1900
1890 COLOR= OFF:GOTO 1891+ RND (4)
1891 IF PX>ONE THEN PLOT PX-ONE,PY:GOTO 1900
1892 IF PY>ONE THEN PLOT PX,PY-ONE:GOTO 1900
1893 IF PX<C37 THEN PLOT PX+ONE,PY:GOTO 1900
1894 IF PY<C37 THEN PLOT PX,PY+ONE:GOTO 1900
1900 RETURN 
2000 REM  MAIN-PGM
2002 GOSUB 9010
2100 IF G=ONE THEN 2220
2110 COLOR= OFF:PLOT G1,G1
2120 COLOR= CG:PLOT G1,G1
2220 GOSUB 1800
2320 GOSUB 1400
2400 A=TWO* RND (C18)+TWO
2410 B1=TWO* RND (C19)+ONE
2450 COLOR= CC* RND (2)
2460 PLOT B1,A:PLOT A,B1
2990 GOTO 2100
4000 G=ONE:REM  GOLD FOUND
4005 H=TWO
4010 CALL -936
4020 M$="YOU FOUND THE GOLD!!!":GOSUB 9000
4030 GOSUB 9001
4040 M$="BUT THE TROLL SAYS,":GOSUB 9000
4050 M$="YOU'LL NEVER GET IT OUT!!!":GOSUB 9000
4060 GOSUB 9070:RETURN 
6000 REM  YOU WIN
6001 REM  -----
6010 CALL -936
6020 PRINT "YOU DID IT!!!"
6040 M$="GOT AWAY WITH ALL THAT GOLD!!!":GOSUB 9000
6060 M$="         ":GOSUB 9000
6070 M$="WOULD YOU LOAN ME A FEW THOUSAND?":GOSUB 9000
6075 GOSUB 9001:GOSUB 9001
6080 TEXT :CALL -936
6090 VTAB 10:TAB 5
6100  POKE K2,0
6110 PRINT "CARE TO TRY FOR SOME MORE GOLD?";
6120 INPUT A$:IF A$(1,1)="Y" THEN 2000:END 
7000 REM  YOU LOSE
7001 REM  -------
7010 CALL -936
7020 M$="YUM, YUM, ARE YOUR RELATIVES THIS TASTY?"
7030 GOSUB 9000:GOSUB 9001
7050 M$="THE TROLL JUST ATE YOU FOR DINNER !!":GOSUB 9000
7070 GOTO 6075
9000 FOR Q=1 TO  LEN(M$):PRINT M$(Q,Q);:FOR Q1=1 TO 16:NEXT Q1,Q:PRINT :FOR Q=1 TO 400:NEXT Q:RETURN 
9001 M$="             ":GOSUB 9000:RETURN 
9009 REM  BANNER
9010  POKE -16300,0: POKE -16298,0:TEXT :CALL -936
9011 VTAB 10:TAB 14:PRINT "MAGIC CAVE"
9014 GOSUB 9001:GOSUB 9001
9015 REM  CAVE
9016 GR :CALL -936:CG=13:CT=12:CC=10:CP=15:COLOR= CC
9017 FOR I=0 TO 38 STEP 2:HLIN 0,38 AT I:VLIN 0,38 AT I:NEXT I
9018 FOR X=1 TO 37 STEP 2:FOR Y=2 TO 36 STEP 2:COLOR=  RND (2)*CC:PLOT X,Y:PLOT Y,X:NEXT Y,X
9020 REM  TROLL&PLAYER
9021 G=0:PX=1:PY=1:COLOR= CP:PLOT PX,PY
9022 TX=21:TY=19:COLOR= CT:PLOT TX,TY
9023 DT=1
9024 H=50
9030 REM  CONSTANTS
9031 ONE=1:TWO=2:C18=18:C19=19:G1=37
9032 C3=3:C20=20:C6=6:C4=4
9033 C37=37
9034 K1=-16384:K2=-16368
9035 CU=213:CD=196:CL=204:CR=210
9040 REM  INSTRUCTIONS
9041 IP=IP+1:IF IP>1 THEN 9071
9044 CALL -936:M$="YOU HAVE JUST ENTERED THE ENTRANCE OF ":GOSUB 9000
9046 M$="THE MAGIC CAVE..(AT THE TOP LEFT)":GOSUB 9000
9047 GOSUB 9001
9048 M$="RUMOR HAS IT THAT THERE IS GOLD ":GOSUB 9000
9049 M$="IN THE FARTHEST REACHES OF THE CAVERN.":GOSUB 9000
9050 GOSUB 9001
9051 M$="IT IS ALSO RUMORED THAT THE OWNER":GOSUB 9000
9052 M$="OF THE GOLD, A FIERCE LITTLE TROLL,":GOSUB 9000
9053 M$="LIVES THERE AND HUNGERS FOR HUMANS...":GOSUB 9000
9054 GOSUB 9001
9062 M$="I RECOMMEND THAT YOU AVOID HIM..":GOSUB 9000
9063 GOSUB 9001
9065 M$="IF YOU GET THE GOLD TO THE ENTRANCE":GOSUB 9000
9067 M$="THEN IT'S YOURS!!!":GOSUB 9000
9070 GOSUB 9001:GOSUB 9001
9071 CALL -936
9072 PRINT "PRESS 'U' TO GO UP, 'D' TO GO DOWN"
9074 PRINT "      'L' TO GO LEFT, AND 'R' FOR RIGHT"
9076 PRINT "       AND 'S' TO STAY PUT."
9999 RETURN 
10000 PR# 0:IN# 0:END 
