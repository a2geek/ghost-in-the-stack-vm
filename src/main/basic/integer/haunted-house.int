5 TEXT
10 CALL -936
20 VTAB 8:TAB (14): POKE 50,127:PRINT "HAUNTED";: POKE 50,255:TAB (23): POKE 50,127:PRINT "HOUSE": POKE 50,255
30 PRINT :TAB (15):PRINT "COPYRIGHT BY"
40 PRINT :TAB (12):PRINT "CREATIVE COMPUTING"
50 TAB (14):PRINT "P.O. BOX 789-M"
60 TAB (7):PRINT "MORRISTOWN, NEW JERSEY 07960"
70 FOR I=1 TO 2000:NEXT I
100 DIM F1(50),F2(50),F3(50),F4(50),M(50),R(28),R$(25),F(8),A(4),P(10)
102 DIM G(5),S$(5)
105 F(0)=0:F(1)=-1:F(2)=1:F(3)=0:F(4)=0:F(5)=1:F(6)=-1:F(7)=0
107 TEXT :CALL -936:VTAB 10:PRINT "PLEASE WAIT 15 SECONDS."
110 FOR I=1 TO 28:R(I)=0:NEXT I
120 GOSUB 9500:FOR I=0 TO 49:F1(I)=M(I):NEXT I
130 GOSUB 9500:FOR I=0 TO 49:F2(I)=M(I):NEXT I
140 GOSUB 9500:FOR I=0 TO 49:F3(I)=M(I):NEXT I
150 GOSUB 9500:FOR I=0 TO 49:F4(I)=M(I):NEXT I
160 FLR= RND (4)+1:GOSUB 9400:FACE= RND (4)
170 R= RND (50):IF M(R)#50 THEN 170
180 X=R MOD 10:Y=R/10:HR=6:MN=0
185 KY=0:LUCK=0
190 GOSUB 6000:CALL -936: POKE 34,10:GOSUB 8600
200 TAB 11:VTAB 2:PRINT "HAUNTED  HOUSE"
250 CALL -936:GOSUB 7000
300 PRINT :TAB 29: POKE 50,63:PRINT "NOW WHAT?";: POKE 50,255
310 GOSUB 9200
400 IF KEY=213 OR KEY=196 OR KEY=194 OR KEY=210 OR KEY=204 OR KEY=198 THEN GOSUB 8000
410 IF GFLAG=1 AND M(X+10*Y)=LT THEN GOSUB 4000
420 IF KEY=211 THEN GOSUB 5000
500 GOSUB 8600
990 GOTO 250
4000 REM  * GHOST! *
4010 CALL -936:PRINT :G(GG)=0
4020 PRINT "THIS ROOM IS HAUNTED ...":PRINT "YOU MEET ITS GHOST !!"
4025 FOR I=1 TO 1000:NEXT I
4030 IF ( RND (10)-LUCK)<3 THEN 4200
4040 PRINT :PRINT "THIS GHOST IS EVIL.":PRINT
4050 IF ( RND (10)-LUCK)<8 THEN 4100
4060 PRINT "HE SPIRITS YOU INTO THE DUNGEON,":PRINT "DEEP UNDERNEATH THE HOUSE."
4070 PRINT :PRINT "THERE, YOU ARE LEFT TO SLOWLY STARVE.":PRINT :PRINT "YOU LOSE.":GOTO 20000
4100 PRINT "THE SPIRIT CASTS A SPELL OVER YOU,":PRINT "AND WHEN YOU AWAKE,YOU FIND HIM GONE."
4110 PRINT :PRINT "THAT COST YOU MUCH TIME... ":PRINT "BETTER BE MORE CAREFUL!"
4120 HR=HR+1:IF HR>12 THEN 10000:GOSUB 8600
4130 GOTO 4490
4200 PRINT :PRINT "THIS GHOST IS FRIENDLY (WHEW!).":PRINT
4210 PRINT "HE GIVES YOU THIS HELPFUL CLUE:":PRINT
4220 IF  RND (10)<7 THEN 4240
4230 RM=P(0):PRINT "'YOU WILL FIND THE EXIT PASSAGEWAY IN"
4235 GOSUB 9300:PRINT R$;" .'":GOTO 4490
4240 R$=" NOT ":FOR I=0 TO 49
4250 IF M(I)=P(0) THEN R$=" ":NEXT I
4260 PRINT "'THE EXIT IS";R$;"ON THIS FLOOR.'"
4490 VTAB 24:PRINT "ARE YOU READY TO CONTINUE (Y/N) ?":GOSUB 11000:IF KEY=206 THEN 4490:RETURN
5000 REM  * SEARCH SUB *
5005 SFLAG=0
5010 T=X+10*Y:IF ABS (M(T))>0 AND ABS (M(T))<29 THEN 5040
5015 SFLAG=1
5020 PRINT :PRINT "YOU CAN'T SEARCH IN THE HALLWAY!":FOR R=1 TO 500:NEXT R:GOTO 5480
5040 CALL -936:VTAB 18:TAB 12:PRINT "<< SEARCHING >>":FOR I=1 TO 1000:NEXT I
5045 MN=MN+8+ RND (20):GOSUB 8600
5050 IF ABS (M(T))#P(0) THEN 5090:GOTO 5120
5090 FOR I=1 TO 3
5100 IF ABS (M(T))=P(I) OR ABS (M(T))=P(I)+14 THEN 5115
5110 NEXT I:GOTO 5400
5115 IF ABS (M(T))<15 THEN RM=ABS (M(T))+14:IF ABS (M(T))>14 THEN RM=ABS (M(T))-14
5120 PRINT :PRINT "YOU FIND A HIDDEN PASSAGEWAY!!":PRINT
5130 PRINT "DO YOU WANT TO FOLLOW IT(Y/N)?":GOSUB 11000:IF KEY#217 THEN 5480
5135 CALL -936:PRINT
5140 PRINT "THE SECRET ENTRANCE LEADS TO A NARROW":PRINT "PATH, WITH HEAVY STONE CEILINGS":PRINT "AND WALLS."
5150 FOR R=1 TO 3000:NEXT R
5160 IF ABS (M(T))#P(0) THEN 5180
5170 PRINT : POKE 50,63:PRINT "YOU FOUND THE WAY OUT!":PRINT :PRINT "YOU WIN!!!!!": POKE 50,255:PRINT :GOTO 20000
5180 IF ( RND (10)+LUCK)>0 THEN 5200
5190 PRINT :PRINT "ARRRRRRRRRRRRRRRGH!":PRINT :PRINT "A TRAP DOOR GIVES WAY UNDER YOU,"
5195 PRINT "AND YOU FALL INTO A DUNGEON.":PRINT :PRINT "THERE IS NO ESCAPING FROM THIS ROOM.":PRINT "YOU LOSE.":GOTO 20000
5200 PRINT :PRINT "YOU FOLLOW THE PASSAGE FOR SOME TIME..."
5210 FLR=1:GOSUB 9400:FOR B=0 TO 49:IF ABS (M(B))=RM THEN 5240:NEXT B
5220 FLR=2:GOSUB 9400:FOR B=0 TO 49:IF ABS (M(B))=RM THEN 5240:NEXT B
5225 FLR=3:GOSUB 9400:FOR B=0 TO 49:IF ABS (M(B))=RM THEN 5240:NEXT B
5230 FLR=4:GOSUB 9400:FOR B=0 TO 49:IF ABS (M(B))=RM THEN 5240:NEXT B
5235 PRINT "ERROR":END
5240 X=B MOD 10:Y=B/10:FACE= RND (4)
5250 PRINT :PRINT "FINALLY, YOU EMERGE INTO A ROOM ...":FOR R=1 TO 700:NEXT R
5390 SFLAG=1:GOTO 5480
5400 REM
5405 IF  RND (10)>2 THEN 5440
5407 IF  RND (10)<5 THEN 5420
5410 IF KY=1 THEN 5420
5411 CALL -936:PRINT :PRINT "YOU FIND A SKELETON KEY !!":KY=1:GOTO 5450
5420 CALL -936:PRINT :PRINT "HIDDEN IN THE ROOM,":PRINT
5421 PRINT "YOU DISCOVER AN ORNATE, OBLONG BOX.":PRINT
5423 PRINT "DO YOU WANT TO OPEN IT (Y/N) ?":GOSUB 11000:IF KEY=206 THEN 5480
5425 IF  RND (10)<4 THEN 5428
5426 PRINT :PRINT "THE BOX WAS A COFFIN!":PRINT "A SKELETON LIES THEREIN.":PRINT :PRINT "YOU ARE CURSED FOR DISTURBING"
5427 PRINT "THESE REMAINS.":LUCK=LUCK-2:PRINT :PRINT "YOUR LUCK FACTOR IS NOW : ";LUCK:GOTO 5480
5428 PRINT :PRINT "IN THE BOX, YOU FIND A TALISMAN ...":LUCK=LUCK+2:PRINT
5429 PRINT "YOUR LUCK FACTOR IS NOW : ";LUCK:GOTO 5480
5440 PRINT :PRINT "YOU FIND NOTHING DURING YOUR SEARCH.":SFLAG=1
5450 FOR I=1 TO 1500:NEXT I
5480 REM
5485 VTAB 24:IF SFLAG=1 THEN RETURN
5490 PRINT :PRINT "DO YOU WANT TO SEARCH SOME MORE (Y/N)?";:GOSUB 11000:IF KEY=217 THEN 5000:RETURN
6000 REM   * VARIABLE EFFECTS *
6010 P(0)= RND (14)+1:FOR I=1 TO 3
6020 R= RND (14)+1:FOR J=0 TO I-1
6030 IF P(J)=R THEN 6020:NEXT J
6040 P(I)=R:NEXT I
6050 G(0)= RND (28)+1:FOR I=1 TO 4
6060 R= RND (28)+1:FOR J=1 TO I:IF G(J)=R THEN 6060:NEXT J
6070 G(I)=R:NEXT I
6490 RETURN
6500 REM  * ROOM DESCRIP. *
6510 PRINT :R= RND (75)
6520 IF R>40 THEN 6700
6530 GOTO 6540+R
6540 PRINT "IN THE CORNER IS A SMALL FIREPLACE.":GOTO 6699
6541 PRINT "THE FURNITURE IS COVERED WITH DUST.":GOTO 6699
6542 PRINT "SEVERAL OLD PORTRAITS GLARE AT YOU":PRINT "FROM THE WALLS.":GOTO 6699
6543 PRINT "THE AIR IS FILLED WITH DISTURBED DUST.":GOTO 6699
6544 PRINT "AN OLD CLOCK ON THE WALL TICKS LOUDLY":GOTO 6699
6545 PRINT "OVERHEAD HANGS A CRYSTAL CHANDELIER.":GOTO 6699
6546 PRINT "A HEAVY TAPESTRY COVERS THE WALL.":GOTO 6699
6547 PRINT "SEVERAL ETRUSCAN URNS SIT ON THE MANTLE.":GOTO 6699
6548 PRINT "A FAINT BEAM OF MOONLIGHT FILTERS IN":PRINT "FROM A WINDOW OVERHEAD.":GOTO 6699
6549 PRINT "A BROKEN MIRROR REFLECTS YOUR IMAGE":PRINT "FROM ACROSS THE ROOM.":GOTO 6699
6550 PRINT "THE WIND OUTSIDE RATTLES THE SHUTTERS.":GOTO 6699
6551 PRINT "THE THICKLY CARPETED FLOOR MUFFLES":PRINT "YOUR FOOTSTEPS, AS YOU ENTER THE ROOM.":GOTO 6699
6552 PRINT "THE DARK ROOM IS FAINTLY ILLUMINATED":PRINT "BY TWO CANDLES, NEAR THE DOOR.":GOTO 6699
6553 PRINT "THE AIR IN THE ROOM IS COLD AND DRY.":GOTO 6699
6554 PRINT "AN OLD, FADED WATERCOLOR HANGS NEARBY.":GOTO 6699
6555 PRINT "A VASE ON THE TABLE HOLDS A DEAD ROSE.":GOTO 6699
6556 PRINT "PAPERS ARE STREWN OVER THE WOODEN FLOOR.":GOTO 6699
6557 PRINT "A DEAD MOUSE LIES IN THE CORNER.":GOTO 6699
6558 PRINT "THE ROOM IS ALMOST EMPTY.":GOTO 6699
6559 PRINT "YOU STEP AROUND AN OLD ROCKING CHAIR":PRINT "IN THE CLUTTERED ROOM.":GOTO 6699
6560 PRINT "THE FLOOR IS COVERED WITH PLASTER, ":PRINT "FROM THE CRACKED CEILING.":GOTO 6699
6561 PRINT "OUTSIDE, YOU CAN HEAR THE THUNDER":PRINT "FROM AN APPROACHING RAINSTORM.":GOTO 6699
6562 PRINT "SOMEWHERE IN THE HOUSE, YOU HEAR":PRINT "THE MUFFLED CLOSING OF A DOOR.":GOTO 6699
6563 PRINT "A FLICKERING CANDLE CASTS GROTESQUE":PRINT "SHADOWS ON THE WALL BESIDE YOU.":GOTO 6699
6564 PRINT "A SMOLDERING LOG IN THE FIREPLACE GIVES":PRINT "LITTLE HEAT TO THE CHILLY ROOM.":GOTO 6699
6565 PRINT "A SILVER CRUCIFIX ON THE WALL REFLECTS":PRINT "THE PALE LAMPLIGHT OF THE ROOM.":GOTO 6699
6566 PRINT "A YELLOWING NEWSPAPER LIES OPEN, ON":PRINT "A CHAIR, NEARBY.":GOTO 6699
6567 PRINT "AN OLD, BROKEN GRAMOPHONE STANDS IN":PRINT "THE CORNER OF THE ROOM.":GOTO 6699
6568 PRINT "A SCRATCHING NOISE FROM SOMEWHERE DOWN":PRINT "THE HALL SENDS A COLD SHIVER":PRINT "DOWN YOUR SPINE.":GOTO 6699
6569 PRINT "AN ODOR OF AGE AND DECAY FILLS THE AIR.":GOTO 6699
6570 PRINT "YOU HAVE THE FEELING OF BEING WATCHED.":GOTO 6699
6571 PRINT "IN THE CORNER STANDS":PRINT "A SUIT OF BLACK ARMOR.":GOTO 6699
6572 PRINT "YOU BRUSH THE COBWEBS OUT OF YOUR WAY":PRINT "AS YOU ENTER THE ROOM.":GOTO 6699
6573 PRINT "SEVERAL LARGE RATS SCURRY OUT OF YOUR":PRINT "WAY, AS YOU WALK IN.":GOTO 6699
6574 PRINT "YOU BRUSH A LARGE SPIDER OFF YOUR ARM,":PRINT "WHICH DESCENDED FROM THE CEILING.":GOTO 6699
6575 PRINT "A BLACK CAT STARES AT YOU FROM A TABLE.":GOTO 6699
6576 PRINT "IN THE DISTANCE YOU CAN HEAR":PRINT "THE RATTLING OF CHAINS.":GOTO 6699
6577 PRINT "YOU HEAR A FAINT MOANING SOUND ....":PRINT "COULD IT BE THE WIND?":GOTO 6699
6578 PRINT "OUT IN THE HALL, YOU HEAR":PRINT "THE STAIRS CREAK.":GOTO 6699
6579 PRINT "YOU SHIVER WITH THE COLD.":GOTO 6699
6580 PRINT "HUGE SPIDER WEBS COVER THE ROOM.":GOTO 6699
6699 FOR I=1 TO 2000:NEXT I: POKE -16368,0
6700 PRINT :RETURN
7000 REM   * DESCRIBE POSITION *
7010 PRINT :T=X+10*Y:PRINT "YOU ARE IN ";
7020 IF M(T)>28 THEN PRINT "THE HALLWAY, ";
7030 IF M(T)#45 THEN 7035: POKE 50,63:PRINT "(BY A STAIRCASE)";: POKE 50,255:CALL -868
7035 IF M(T)=50 OR M(T)=45 THEN PRINT "WHICH EXTENDS"
7040 IF M(T)>28 THEN 7160
7050 RM=ABS (M(T)):GOSUB 9300:PRINT R$
7055 IF LT#M(T) THEN GOSUB 6500
7060 IF R(M(T))=0 AND LT#M(T) THEN PRINT "THIS ROOM LOOKS FAMILIAR ..."
7070 R(M(T))=0:PRINT
7080 FOR I=0 TO 3:R=(FACE+I) MOD 4:T=(X+F(R*2))+10*(Y+F(R*2+1))
7085 IF T>49 THEN 7150:IF T<0 THEN T=49
7090 IF ABS (M(T))#100 THEN 7150:GOTO 7100+I*10
7100 PRINT "AT THE END OF THE ROOM";:GOTO 7140
7110 PRINT "TO YOUR RIGHT";:GOTO 7140
7120 PRINT "BEHIND YOU";:GOTO 7140
7130 PRINT "TO YOUR LEFT";
7140 PRINT " IS A DOOR."
7150 NEXT I:GOTO 7260
7160 FOR I=0 TO 3:R=(FACE+I) MOD 4:T=(X+F(R*2))+10*(Y+F(R*2+1)):IF T>49 THEN T=49:IF T<0 THEN T=49
7170 A(I)=T:NEXT I:I=0
7180 IF M(A(I))=50 OR M(A(I))=45 THEN 7190:I=I+1:GOTO 7180
7190 GOSUB 7300:PRINT R$;:PRINT " AND ";:I=I+1
7200 IF M(A(I))=50 OR M(A(I))=45 THEN 7210:I=I+1:GOTO 7200
7210 GOSUB 7300:PRINT R$;".":PRINT
7220 FOR I=0 TO 3:IF ABS (M(A(I)))#100 AND M(A(I))#45 THEN 7250
7230 GOSUB 7300:PRINT R$;:PRINT " THERE IS";:R$=" A DOOR."
7240 IF M(A(I))=45 THEN R$=" A STAIRWAY.":PRINT R$
7250 NEXT I
7260 LT=M(X+10*Y):GFLAG=0
7270 FOR I=0 TO 4:IF LT=G(I) THEN 7280:NEXT I:GOTO 7290
7280 PRINT :PRINT "YOU HEAR A NOISE ..."
7281 GG=I:GFLAG=1:FOR I=1 TO 250:NEXT I
7283 FOR I=1 TO 25:R= PEEK (-16336)
7284 FOR J=1 TO 5:NEXT J:NEXT I
7286 FOR I=1 TO 300:NEXT I
7287 PRINT "THERE IS SOMEONE ELSE IN THE ROOM !!"
7290 RETURN
7300 IF I=0 THEN R$="AHEAD OF YOU"
7310 IF I=1 THEN R$="TO YOUR RIGHT"
7320 IF I=2 THEN R$="BEHIND YOU"
7330 IF I=3 THEN R$="TO YOUR LEFT"
7340 RETURN
7400 REM   * DOOR SOUND *
7405 PRINT :PRINT "YOU OPEN THE DOOR ...":PRINT
7410 FOR I=1 TO 200:NEXT I
7420 FOR I=100 TO 30 STEP -10
7430 FOR J=1 TO 3:R= PEEK (-16336):NEXT J
7440 FOR J=1 TO I:NEXT J:NEXT I
7450 FOR I=1 TO 15:FOR J=1 TO 3:R= PEEK (-16336):NEXT J:FOR J=1 TO 18:NEXT J:NEXT I
7460 FOR I=20 TO 120 STEP 10:FOR J=1 TO 3:R= PEEK (-16336):NEXT J
7470 FOR J=1 TO I:NEXT J:NEXT I
7475 PRINT "AND WALK THROUGH."
7476 FOR I=1 TO 200:NEXT I
7477 FOR I=1 TO 3:R= PEEK (-16336)+ PEEK (-16336)+ PEEK (-16336):NEXT I
7480 RETURN
7500 REM  * STEP SOUND *
7505 PRINT :PRINT "YOU GO ";:IF KEY=213 THEN PRINT "UP";
7506 IF KEY=196 THEN PRINT "DOWN";:PRINT " THE STAIRS ..."
7510 FOR I=1 TO 8
7520 FOR J=1 TO 3:R= PEEK (-16336)- PEEK (-16336)- PEEK (-16336)- PEEK (-16336):NEXT J
7525 FOR J=1 TO 65:NEXT J
7528 FOR J=1 TO 5:R= PEEK (-16336):NEXT J
7530 FOR J=1 TO 200:NEXT J:NEXT I
7550 RETURN
8000 REM   * MOVE *
8005 OLDF=FACE
8010 IF KEY#213 AND KEY#196 THEN 8110
8020 IF M(X+10*Y)=45 THEN 8040
8030 PRINT :PRINT "YOU ARE NOT ON THE STAIRS!":GOTO 8200
8040 IF KEY#213 OR FLR#4 THEN 8060
8050 PRINT :PRINT "YOU ARE ALREADY ON THE TOP FLOOR.":GOTO 8200
8060 IF KEY#196 OR FLR#1 THEN 8080
8070 PRINT :PRINT "YOU ARE ALREADY ON THE GROUND FLOOR.":GOTO 8200
8080 IF KEY=213 THEN FLR=FLR+1
8090 IF KEY=196 THEN FLR=FLR-1
8095 GOSUB 7500
8100 GOSUB 9400:GOTO 8200
8110 IF KEY#194 THEN 8120:FACE=(FACE+2) MOD 4:GOTO 8140
8120 IF KEY#210 THEN 8130:FACE=(FACE+1) MOD 4:GOTO 8140
8130 IF KEY#204 THEN 8140:FACE=FACE-1:IF FACE=-1 THEN FACE=3
8140 NX=X+F(FACE*2):NY=Y+F(FACE*2+1):T=NX+10*NY:IF T>49 OR T<0 THEN T=49:IF M(T)=0 THEN 8160
8150 IF NX>-1 AND NX<9 AND NY>-1 AND NY<5 THEN 8170
8160 PRINT :PRINT "YOU CAN'T GO THAT WAY,":PRINT "BECAUSE OF THE WALL.":FACE=OLDF:GOTO 8200
8170 IF M(T)#-100 THEN 8190:PRINT
8180 PRINT "THE DOOR IS LOCKED.":PRINT
8181 IF KY=0 THEN 8188
8182 PRINT "DO YOU WANT TO USE YOUR SKELETON KEY":PRINT :PRINT "TO OPEN IT (Y/N)?":GOSUB 11000:IF KEY=206 THEN 8188
8184 M(T)=100:GOTO 8190
8188 FACE=OLDF:GOTO 8200
8190 X=NX:Y=NY:IF M(T)#100 THEN 8200:GOSUB 7400:GOTO 8140
8200 FOR I=1 TO 1000:NEXT I:RETURN
8600 REM   * CLOCK *
8610  POKE 50,63:MN=MN+1
8620 IF MN<60 THEN 8630:MN=MN-60:HR=HR+1:IF HR>11 THEN 10000
8630 FOR I=6 TO 8:TAB 30:VTAB I:PRINT "      ";:NEXT I
8640 TAB 31:VTAB 7:PRINT HR;
8650 PRINT ":";
8660 IF MN<10 THEN PRINT "0";
8670 PRINT MN;
8680 FOR I=1 TO 2:R= PEEK (-16336):NEXT I
8690  POKE 50,63
8700 R$="A":IF FACE=1 THEN R$=">":IF FACE=2 THEN R$="V":IF FACE=3 THEN R$="<"
8710 TAB 5:VTAB 7:PRINT R$;
8720 A(0)=X+10*(Y-1):A(1)=(X+1)+10*Y:A(2)=X+10*(Y+1):A(3)=(X-1)+10*Y
8725 FOR I=0 TO 3:IF A(I)<0 OR A(I)>49 THEN A(I)=49:NEXT I
8730 TAB 5:VTAB 6:R=ABS (M(A(0))):GOSUB 8800
8735 TAB 4:VTAB 7:R=ABS (M(A(3))):GOSUB 8800
8740 TAB 6:VTAB 7:R=ABS (M(A(1))):GOSUB 8800
8745 TAB 5:VTAB 8:R=ABS (M(A(2))):GOSUB 8800
8785  POKE 50,255
8790 TAB 1:VTAB 11:RETURN
8800  POKE 50,255:IF R=50 THEN  POKE 50,63
8810 IF R=100 THEN PRINT "D";:IF R=0 OR R=50 THEN PRINT " ";
8820 IF R=45 THEN PRINT "S";:RETURN
9200 REM  * GET KEY *
9210 KEY= PEEK (-16384):IF KEY<128 THEN 9210
9220  POKE -16368,0
9230 IF KEY=198 OR KEY=194 OR KEY=210 OR KEY=204 OR KEY=213 OR KEY=196 OR KEY=211 THEN 9245
9232 CALL -936:PRINT :PRINT "INVALID COMMAND!":PRINT :PRINT "VALID COMMANDS ARE:"
9234 PRINT "'F' = FORWARD":PRINT "'B' = BACK":PRINT "'R' = RIGHT"
9236 PRINT "'L' = LEFT":PRINT "'U' = UP":PRINT "'D' = DOWN"
9237 PRINT "'S' = SEARCH"
9239 PRINT :PRINT "WHICH DO YOU WANT?":GOTO 9210
9240 GOTO 9210
9245 R= PEEK (37)+1:CALL -956:TAB 18:VTAB R:PRINT "O.K."
9250 RETURN
9300 GOTO ABS (RM)+9300
9301 R$="THE LIBRARY":GOTO 9350
9302 R$="THE BILLIARDS ROOM":GOTO 9350
9303 R$="THE STUDY":GOTO 9350
9304 R$="THE NURSERY":GOTO 9350
9305 R$="THE MASTER BEDROOM":GOTO 9350
9306 R$="THE DINING ROOM":GOTO 9350
9307 R$="THE GAME ROOM":GOTO 9350
9308 R$="THE BALLROOM":GOTO 9350
9309 R$="THE BUTLER'S ROOM":GOTO 9350
9310 R$="THE MAID'S ROOM":GOTO 9350
9311 R$="THE GOVERNESS' ROOM":GOTO 9350
9312 R$="THE SERVANTS' LOUNGE":GOTO 9350
9313 R$="THE CHILDREN'S BEDROOM":GOTO 9350
9314 R$="THE PLAY ROOM":GOTO 9350
9315 R$="THE SEWING ROOM":GOTO 9350
9316 R$="THE CHAPEL":GOTO 9350
9317 R$="THE GREEN GUEST ROOM":GOTO 9350
9318 R$="THE GOLD GUEST ROOM":GOTO 9350
9319 R$="THE BLUE GUEST ROOM":GOTO 9350
9320 R$="THE RED GUEST ROOM":GOTO 9350
9321 R$="THE CONSERVATORY":GOTO 9350
9322 R$="THE TROPHY ROOM":GOTO 9350
9323 R$="THE LIVING ROOM":GOTO 9350
9324 R$="THE TEA ROOM":GOTO 9350
9325 R$="THE MUSIC ROOM":GOTO 9350
9326 R$="THE DRAWING ROOM":GOTO 9350
9327 R$="THE READING ROOM":GOTO 9350
9328 R$="THE GYMNASIUM"
9350 RETURN
9400 FOR I=0 TO 49:GOTO 9400+FLR*10
9410 J=F1(I):GOTO 9450
9420 J=F2(I):GOTO 9450
9430 J=F3(I):GOTO 9450
9440 J=F4(I)
9450 M(I)=J:NEXT I:RETURN
9500 REM   * GEN FLOOR PLAN *
9510 FOR I=0 TO 49:M(I)=0:NEXT I
9520 FOR I=2 TO 6:M(I)=50:M(I+40)=50:NEXT I:M(3)=45
9530 M(12)=50:M(22)=50:M(32)=50:M(16)=50:M(26)=50:M(36)=50
9540 GOSUB 9580:M(0)=R:GOSUB 9580:M(8)=R
9550 GOSUB 9580:M(20)=R:GOSUB 9580:M(24)=R
9560 GOSUB 9580:M(28)=R:GOSUB 9580:M(40)=R
9570 GOSUB 9580:M(48)=R:GOTO 9600
9580 R= RND (28)+1:IF R(R)=1 THEN 9580
9590 R(R)=1:RETURN
9600 R1= RND (4):R2= RND (4):IF R1=R2 THEN 9600
9610 M(14)=100:IF R1=0 OR R2=0 THEN M(14)=0
9620 M(23)=100:IF R1=1 OR R2=1 THEN M(23)=0
9630 M(25)=100:IF R1=2 OR R2=2 THEN M(25)=0
9640 M(34)=100:IF R1=3 OR R2=3 THEN M(34)=0
9650 R1= RND (5):R2= RND (5)
9660 M(1)=100:M(10)=100:M(21)=100:M(30)=100:M(41)=100
9670 M(7)=100:M(18)=100:M(27)=100:M(38)=100:M(47)=100
9680 IF R1=0 THEN M(1)=0:IF R1=1 THEN M(10)=0
9690 IF R1=2 THEN M(21)=0:IF R1=3 THEN M(30)=0
9700 IF R1=4 THEN M(41)=0:IF R2=0 THEN M(7)=0
9710 IF R2=1 THEN M(18)=0:IF R2=2 THEN M(27)=0
9720 IF R2=3 THEN M(38)=0:IF R2=4 THEN M(47)=0
9730 FOR I=0 TO 49:IF M(I)#100 THEN 9750
9740 IF  RND (50)<10 THEN M(I)=-100
9750 NEXT I:RETURN
10000 CALL -936:PRINT
10005 TAB 31:VTAB 7:PRINT "12:00";:VTAB 18:TAB 1
10010 PRINT : POKE 50,63:PRINT "IT IS MIDNIGHT ... YOUR TIME IS UP !"
10020 PRINT :PRINT "YOU ARE TRAPPED FOREVER":PRINT "IN THE HAUNTED HOUSE !!"
10030 PRINT :PRINT "YOU LOSE !!!": POKE 50,255:GOTO 20000
11000 KEY= PEEK (-16384):IF KEY<128 THEN 11000
11010  POKE -16368,0
11020 IF KEY=206 OR KEY=217 THEN RETURN
11030 GOTO 11000
20000 PRINT "WANT TO TRY AGAIN (Y/N)?":GOSUB 11000:IF KEY=217 THEN 105
20010 TEXT :CALL -936
20020 PRINT :PRINT "THANKS FOR PLAYING!"
20030 PRINT :PRINT :PRINT "TO PLAY AGAIN, TYPE 'RUN'.":PRINT :END
