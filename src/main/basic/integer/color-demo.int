30 DIM A$(40):U=9:D=3:E=-16384:F=-16368:G=-16287:H=-16286:C=-936:T=2000:Z=127
99 REM _
    Moved this to $300 to not conflict with interpreter _
        0302-   AD 30 C0    LDA   $C030 _
        0305-   88          DEY _
        0306-   D0 05       BNE   $030D _
        0308-   CE 01 03    DEC   $0301 _
        030B-   F0 09       BEQ   $0316 _
        030D-   CA          DEX _
        030E-   D0 F5       BNE   $0305 _
        0310-   AE 00 03    LDX   $0300 _
        0313-   4C 02 03    JMP   $0302 _
        0316-   60          RTS
100  POKE 0x302,0xad: POKE 0x303,0x30: POKE 0x304,0xc0: _
     POKE 0x305,0x88: _
     POKE 0x306,0xd0: POKE 0x307,0x05: _
     POKE 0x308,0xce: POKE 0x309,0x01: POKE 0x30a,0x03
110  POKE 0x30b,0xf0: POKE 0x30c,0x09: _
     POKE 0x30d,0xca: _
     POKE 0x30e,0xd0: POKE 0x30f,0xf5: _
     POKE 0x310,0xae: POKE 0x311,0x00: POKE 0x312,0x03: _
     POKE 0x313,0x4c: POKE 0x314,0x02: POKE 0x315,0x03: _
     POKE 0x316,0x60
200 TEXT :CALL C:PRINT : POKE F,0
220 A$="APPLE DEMONSTRATION PROGRAMS":GOSUB T:PRINT 
230 PRINT "    TO OPERATE A DEMONSTRATION,  TYPE"
240 PRINT "ITS NUMBER.  THEN TYPE THE KEY MARKED"
250 PRINT "'RETURN' AT THE RIGHT EDGE OF THE KEY-"
260 PRINT "BOARD.  TYPE THE 'RETURN' KEY TO STOP"
270 PRINT "ANY DEMONSTRATION.":PRINT :PRINT 
290 PRINT "1.";
300 A$="STANDARD COLOR NAMES":GOSUB T
310 PRINT "2.";
320 A$="STANDARD COLOR NUMBERS":GOSUB T
330 PRINT "3.";
340 A$="KALEIDOSCOPE":GOSUB T
350 PRINT "4.";
360 A$="SKETCHING SCREEN":GOSUB T
362 PRINT "5. ";
364 A$="EXIT":GOSUB T
370 PRINT :PRINT 
380 INPUT "WHICH WOULD YOU LIKE? ",A$
390 IF  LEN(A$)=0 THEN GOTO 200:N= ASC(A$(1,1))-176
400 IF N<1 OR N>5 THEN GOTO 200
402 IF N=5 THEN END 
410 GOTO 400+N*100
450 IF  PEEK (E)<Z THEN 450
460 GOTO 200
500 GOSUB 3000
510 A$="BMDPDGMLBOGPLYAW":GOSUB 4000:PRINT 
520 A$="LGBUGRBBRRRIGEQI":GOSUB 4000:PRINT 
530 A$="ATLRRELLWNENRLUT":GOSUB 4000:PRINT 
540 A$="KAUPNYUUNGYKNOAE":GOSUB 4000
550 GOTO 450
600 GOSUB 3000
610 FOR I=0 TO 14 STEP 2
620 TAB 2*I+4:PRINT I;
630 NEXT I:PRINT 
640 FOR I=1 TO 15 STEP 2
650 TAB 2*I+4:PRINT I;
660 NEXT I:PRINT :PRINT :GOTO 450
700 GR :CALL C:FOR W=3 TO 50:FOR I=1 TO 19:FOR J=0 TO 19:K=I+J:IF  PEEK (E)>Z THEN 200
750 COLOR= J*3/(I+3)+I*W/12
760 PLOT I,K:PLOT K,I:PLOT 40-I,40-K:PLOT 40-K,40-I
770 PLOT K,40-I:PLOT 40-I,K:PLOT I,40-K:PLOT 40-K,I
780 NEXT J,I,W:GOTO 450
800 CALL C:A$="USE THE GAME CONTROLS AND BUTTONS.":GOSUB T
810 A$="THE BUTTONS CLEAR THE TV AND CHANGE":GOSUB T
820 A$="THE DISPLAYED COLOR.  TRY ONE NOW.":GOSUB T
900 IF  PEEK (G)>Z OR  PEEK (H)>Z THEN 930
910 IF  PEEK (E)>Z THEN 200
920 GOTO 900
930 CALL C:GR 
940 P=PDL (0):X=P/6:IF X>39 THEN X=39
950 I=PDL (1):Y=I/6:IF Y>39 THEN Y=39
960 P=(I+P)/2+9:IF P>255 THEN P=255:GOSUB 1500
980 VTAB 21:PRINT "X IS ";X;" ":PRINT "Y IS ";Y;" "
990 IF  PEEK (E)>Z THEN 200
1000 IF  PEEK (G)>Z THEN GR 
1010 IF  PEEK (H)>Z THEN U=(U+1) MOD 16
1020 COLOR= U:PLOT X,Y:GOTO 940
1500  POKE 0x300,P: POKE 0x301,D:CALL 0x302:RETURN
2000 TAB ((40- LEN(A$))/2+1):PRINT A$:PRINT :RETURN 
3000 GR :FOR I=0 TO 31:COLOR= I/2:VLIN 0,39 AT I+3:NEXT I
3010 COLOR= 5:VLIN 0,39 AT 2:VLIN 0,39 AT 35:HLIN 2,35 AT 0:HLIN 2,35 AT 39:RETURN 
4000 FOR I=1 TO  LEN(A$)
4010 TAB 2*I+2:PRINT A$(I,I);
4020 NEXT I:RETURN 
65535 REM *COPYRIGHT 1978 APPLE COMPUTER,INC.*
